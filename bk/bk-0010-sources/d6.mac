




;  ************************************************
;  *                                              *
;  *    Д Р А Й В Е Р    М А Г Н И Т О Ф О Н А    *
;  *                                              *
;  ************************************************



;   ======================
;   *** КОНСТАНТЫ DMAG ***
;   ======================

	ASPORT	= 177716	;- АДРЕС СИСТЕМНОГО ПОРТА
	APSTOP	= 4		;- АДРЕС ПЕРЕХОДА ПО "СТОП"

	KDBIT0	= 100		;- ДЛИНА БИТА НА ЗАПИСЬ
	KDBIT1	= 400		;
	DRAZBR	= 4		;- ДИАПАЗОН РАЗБРОСА Н.П.

	KPUSK	= 20		;- КОНС. ПУСК
	KSTOP	= 220		;- КОНС. СТОП
	KBIT00	= 20		;- КОНС. -0
	KBIT10	= 60		;- КОНС. -1
	KBIT01	= 160		;- КОНС. +0
	KBIT11	= 120		;- КОНС. +1





; ==================================
; ***** МОДУЛЬ УПРАВЛЕНИЯ DMAG *****
; ==================================


;	ВХОД:	R1 - АДРЕС БЛОКА ПАРАМЕТРОВ

;	R3  -  АДРЕС ПОРТА

MDMAG:	JSR	R4,PSREG

	MOV	R1,ABP		;  ПОДГ. ПАРАМ
	MOV	#ASPORT,R3
	CLRB	PROH
	CLR	PRFCT

	MOV	@#APSTOP,-(SP)	;  ПОДГ. ВЫХ. ПО "СТОП"
	MOV	#AWDMAG,@#APSTOP
	MOV	SP,BUFSP

	MOVB	(R1),R0		;  АНАЛИЗ КОМАНДЫ

	BNE	1$		;- СТОП
	MOV	#KSTOP,(R3)
	BR	KMDMAG

1$:	DEC	R0		;- ПУСК
	BNE	2$
	MOV	#KPUSK,(R3)
	BR	KMDMAG

2$:	DEC	R0		;- ЗАПИСЬ МАССИВА
	BNE	3$
	JSR	PC,BZMAS
	BR	KMDMAG

3$:	DEC	R0		;- ЧТЕНИЕ МАССИВА
	BNE	4$
5$:	JSR	PC,BCTMAS
	BR	KMDMAG

4$:	INC	PRFCT		;- ФИКТИВНОЕ ЧТЕНИЕ МАССИВА
	DEC	R0
	BEQ	5$

	MOV	#KDBIT0,DLBIT0	;- УСТАНОВКА СКОРОСТИ ЗАПИСИ
	MOV	#KDBIT1,DLBIT1
	BR	6$
7$:	ASR	DLBIT0
	ASR	DLBIT1
6$:	SOB	R0,7$
	BR	KMDMAG

AWDMAG:	MOV	BUFSP,SP	;  АВАРИЙНЫЙ ВЫХОД ИЗ DMAG
	MOVB	#4,PROH
	MOV	#KSTOP,(R3)	;- ОСТАНОВ ДВИГАТЕЛЯ

KMDMAG:	MOV	ABP,R1		;  ВЫХОД
	MOVB	PROH,1(R1)
	MOV	(SP)+,@#APSTOP

	JSR	R4,PWREG
	RTS	PC



;   ==============================
;   *** 1. БЛОК ЗАПИСИ МАССИВА ***
;   ==============================


;	ВХОД:	R1  - АДРЕС БЛОКА ПАРАМЕТРОВ

BZMAS:	MOV	#KPUSK,(R3)	;  ПУСК ДВИГАТЕЛЯ

	MOV	2(R1),R5	;  ПОДСЧЕТ КОНТРОЛЬНОЙ СУММЫ
	MOV	4(R1),R4
	BEQ	1$
	CLR	R0
2$:	CLR	R2
	BISB	(R5)+,R2
	ADD	R2,R0
	ADC	R0
	SOB	R4,2$
	MOV	R0,BUFKS

	MOV	#11,R4		;  ЗАПИСЬ НАСТРОЕЧНОЙ ПОСЛЕДОВАТЕЛЬНОСТИ
	MOV	#10000,R0
	BR	3$
4$:	MOV	R4,R0
3$:	JSR	PC,PZNP
	SOB	R4,4$

	MOV	(R1)+,R0	;  ЗАПИСЬ ИМЕНИ
	MOV	#24,R2
	JSR	PC,PZBL

	MOV	ABP,R1		;  ЗАПИСЬ МАССИВА
	MOV	4(R1),R2
	MOV	2(R1),R1
	JSR	PC,PZBL

	MOV	#BUFKS,R1	;  ЗАПИСЬ KS
	MOV	#10,R2
	JSR	PC,PZBL

1$:	MOV	#KSTOP,(R3)	;- ОСТАНОВ ДВИГАТЕЛЯ

	RTS	PC


;    ------------------------------------------------
;    ** 1.1. ЗАПИСЬ НАСТРОЕЧНОЙ ПОСЛЕДОВАТЕЛЬНОСТИ **
;    ------------------------------------------------

;	ВХОД:   R0  - СЧЕТЧИК НУЛЕВЫХ ИМПУЛЬСОВ

;	ВЫХОД:  C = 0

;	R0,R5  - РАБОЧИЕ

PZNP:	BCS	1$

2$:	JSR	PC,PZ0P		;  ЗАПИСЬ 0 В ЦИКЛЕ
1$:	JSR	PC,PZ0M
	SOB	R0,2$

	JSR	PC,PZ1P		;  ЗАПИСЬ МАРКЕРА(+1,-1)
	JSR	PC,PZ1M

	RTS	PC


;    -----------------------
;    ** 1.2. ЗАПИСЬ БЛОКА **
;    -----------------------

;	ВХОД:   R1  - АДРЕС БЛОКА
;		R2  - ДЛИНА БЛОКА

;	R0 - R2,R4,R5  - РАБОЧИЕ

PZBL:	MOV	#20,R0		;  ЗАПИСЬ БУФ. ПОСЛЕД.
	JSR	PC,PZNP

PZBL1:	BISB	(R1)+,R0	;- ПОДГОТОВКА БАЙТА
	MOV	#10,R4

PZB:	BCS	PZMBIT		;  ЗАПИСЬ БАЙТА

	ASR	R0
	BCS	1$
	JSR	PC,PZ0P		;- ЗАПИСЬ +0
	BR	KZB

1$:	JSR	PC,PZ1P		;- ЗАПИСЬ +1
	BR	KZB

PZMBIT:	ASR	R0
	BCS	1$
	JSR	PC,PZ0M		;- ЗАПИСЬ -0
	BR	KZB

1$:	JSR	PC,PZ1M		;- ЗАПИСЬ -1
	BR	KZB

KZB:	SOB	R4,PZB		;  КОНЕЦ ЗАПИСИ БАЙТА

KZBL:	SOB	R2,PZBL1	;  КОНЕЦ ЗАПИСИ БЛОКА

	RTS	PC


;    ----------------------
;    ** 1.3. ЗАПИСЬ БИТА **
;    ----------------------

PZ0P:	MOV	#KBIT01,(R3)	;  ЗАПИСЬ +0
	MOV	DLBIT0,R5
1$:	SOB	R5,1$
	SEC
	RTS	PC

PZ0M:	MOV	#KBIT00,(R3)	;  ЗАПИСЬ -0
	MOV	DLBIT0,R5
1$:	SOB	R5,1$
	CLC
	RTS	PC


PZ1P:	MOV	#KBIT11,(R3)	;  ЗАПИСЬ +1
	MOV	DLBIT1,R5
1$:	SOB	R5,1$
	SEC
	RTS	PC

PZ1M:	MOV	#KBIT10,(R3)	;  ЗАПИСЬ -1
	MOV	DLBIT1,R5
1$:	SOB	R5,1$
	CLC
	RTS	PC



;   ==============================
;   *** 2. БЛОК ЧТЕНИЯ МАССИВА ***
;   ==============================


;	R0 - R5  - РАБОЧИЕ
;	R5  - МАСКА ИНФОРМАЦИОННОГО РАЗРЯДА

BCTMAS:	MOV	#40,R5		;  ПОДГ.ПАРАМ. И ПУСК ДВИГАТЕЛЯ
	MOV	#KPUSK,(R3)
	MOV	#1,INCADR

	CLR	PROH
	JSR	PC,PPNF		;  ПОИСК НАЧАЛА ФАЙЛА

	JSR	PC,PCTIM	;  ЧТЕНИЕ IMMAS
	TSTB	PROH
	BNE	KCTMAS

	JSR	PC,PCTMAS	;  ЧТЕНИЕ БЛОКА ИНФОРМАЦИИ
	MOV	#KSTOP,(R3)

KCTMAS:	RTS	PC


;    -----------------------------
;    ** 2.1. ПОИСК НАЧАЛА ФАЙЛА **
;    -----------------------------

PPNF:	MOV	#2000,R2	;  ПОИСК НУЛЕЙ
	CLR	R0

4$:	CLR	R4		;- ЧТЕНИЕ ИМПУЛЬСА
1$:	BIT	R5,(R3)
	BEQ	1$
2$:	INC	R4
	BIT	R5,(R3)
	BNE	2$

	SUB	R4,R0		;  ОПРЕДЕЛЕНИЕ РАЗБРОСА
	BMI	3$
	CMP	R0,#DRAZBR
	BHI	PPNF
3$:	MOV	R4,R0
	SOB	R2,4$

	CLR	R0		;  НАСТРОЙКА НА СКОРОСТЬ
	MOV	#100,R2
5$:	JSR	PC,PCTBIT	;- СЧЕТ ИМПУЛЬСОВ
	ADD	R4,R0
	SOB	R2,5$

	MOV	#5,R2		;- ВЫЧИСЛЕНИЕ ГРАНИЦЫ 0
6$:	ASR	R0
	SOB	R2,6$
	ADD	#4,R0
	MOV	R0,GRDL0

	JSR	PC,PPM		;  ПРОВЕРКА КЛЮЧА
	MOV	#20,R0
11$:	MOV	R0,R2
10$:	JSR	PC,PCTBIT
	BCS	PPNF
	SOB	R2,10$
	JSR	PC,PCTBIT
	BCC	PPNF
	JSR	PC,PCTBIT
	BCC	PPNF
	DEC	R0
	SOB	R0,11$

	RTS	PC


;    -------------------------------
;    ** 2.2. ЧТЕНИЕ ИМЕНИ МАССИВА **
;    -------------------------------

PCTIM:	MOV	ABP,R1		;  ЧТЕНИЕ ИМЕНИ
	ADD	#26,R1
	MOV	#24,R2
	JSR	PC,PCTBL

	MOV	#10,R2
2$:	CMP	-(R1),-24(R1)	;  СРАВНЕНИЕ ИМЕН
	BNE	1$
	SOB	R2,2$

	MOV	-(R1),BUFDL	;- УСТ. ДЛИНЫ МАССИВА
	MOV	-26(R1),BUFSTA	;- УСТ. СТАРТ. АДРЕСА
	BNE	KCTIM
	MOV	-(R1),BUFSTA
	BR	KCTIM

1$:	INCB	PROH

KCTIM:	RTS	PC


;    -------------------------
;    ** 2.3. ЧТЕНИЕ МАССИВА **
;    -------------------------

PCTMAS:	MOV	BUFSTA,R1	;  ЧТЕНИЕ МАССИВА
	MOV	BUFDL,R2
	SUB	PRFCT,INCADR
	BNE	1$
	MOV	#BUFKS,R1	;- ФИКТИВНОЕ ЧТЕНИЕ
1$:	JSR	PC,PCTBL
	MOV	R0,-(SP)

	MOV	#BUFKS,R1	;  ЧТЕНИЕ KS
	MOV	#2,R2
	MOV	#1,INCADR
	JSR	PC,PCTBL

	CMP	(SP)+,BUFKS	;  СРАВНЕНИЕ KS
	BEQ	KCTM
	MOVB	#2,PROH

KCTM:	RTS	PC


;    -----------------------
;    ** 2.4. ЧТЕНИЕ БЛОКА **
;    -----------------------

;	ВХОД:	R1  - АДРЕС ОЗУ
;		R2  - ДЛИНА БЛОКА

;	ВЫХОД:  R0  - КОНТРОЛЬНАЯ СУММА

PCTBL:	JSR	PC,PPM		;  ПОИСК МАРКЕРА
	CLR	-(SP)

2$:	MOV	#10,R0		;  ЧТЕНИЕ БЛОКА

1$:	JSR	PC,PCTBIT	;  ЧТЕНИЕ БАЙТА
	RORB	(R1)
	SOB	R0,1$

	BISB	(R1),R0		;- ПОДСЧЕТ KS
	ADD	R0,(SP)
	ADC	(SP)

	ADD	INCADR,R1	;- ИЗМЕНЕНИЕ АДРЕСА
	SOB	R2,2$

	MOV	(SP)+,R0
KCTBL:	RTS	PC


;    ----------------------
;    ** 2.5. ЧТЕНИЕ БИТА **
;    ----------------------

;	ВЫХОД:  C = 0  - 0 БИТ
;		C = 1  - 1 БИТ

;	R4  - РАБОЧИЙ

PCTBIT:	CLR	R4		;  СЧЕТЧИК ДЛИНЫ БИТА

	BIT	R5,(R3)
	BEQ	1$

2$:	INC	R4		;  +
	BIT	R5,(R3)
	BNE	2$
	BR	3$

1$:	INC	R4		;  -
	BIT	R5,(R3)
	BEQ	1$

3$:	CMP	GRDL0,R4	;- УСТАНОВКА БИТА C

	RTS	PC


;    ------------------------
;    ** 2.6. ПОИСК МАРКЕРА **
;    ------------------------

;	R4  - РАБОЧИЙ

PPM:	JSR	PC,PCTBIT
	BCC	PPM
	JSR	PC,PCTBIT
	BCC	PPM

	RTS	PC





;  **********************************************
;  *                                            *
;  *    Д Р А Й В Е Р    Т Л Г - К А Н А Л А    *
;  *                                            *
;  **********************************************



;   ======================
;   *** КОНСТАНТЫ DTLG ***
;   ======================


	ASPORT	= 177716	;- АДРЕС СИСТЕМНОГО ПОРТА

	KTLG0	= 200		;- КОНС. УСТАНОВКИ 0
	KTLG1	= 220		;- КОНС. УСТАНОВКИ 1





; =====================================
; ***** МОДУЛЬ ИНИЦИАЛИЗАЦИИ DTLG *****
; =====================================


;	ВХОД:	R0 - НОМЕР ЗНАЧЕНИЯ СКОРОСТИ


MIDTLG:	ASL	R0
	MOV	KDLBIT(R0),DLBIT

	RTS	PC





; =================================
; ***** МОДУЛЬ ПЕРЕДАЧИ БАЙТА *****
; =================================


;	ВХОД:	R0 - МЛ. БАЙТ


MPDBYT:



;   ===========================
;   *** БЛОК ПЕРЕДАЧИ БАЙТА ***
;   ===========================


BPDB:	MOV	R0,-(SP)
	MOV	R4,-(SP)

	BIS	#1400,R0	;  ПОДГ. ПАРАМЕТРОВ
	MOV	#13,R4

6$:	BIT	#200,@#ASPORT	;  ОЖ. ГОТОВНОСТИ
	BEQ	6$

	CLC			;  УСТАНОВКА БИТА
	BR	1$
4$:	ASR	R0
1$:	BCC	2$
	MOV	#KTLG1,@#ASPORT
	BR	3$
2$:	MOV	#KTLG0,@#ASPORT
	BR	3$

3$:	MOV	DLBIT,R5	;  ЦИКЛ ПЕРЕДАЧИ БИТА
5$:	SOB	R5,5$

	SOB	R4,4$

	MOV	(SP)+,R4
	MOV	(SP)+,R0
	RTS	PC





; ===============================
; ***** МОДУЛЬ ПРИЕМА БАЙТА *****
; ===============================


;	ВЫХОД:	R0 - МЛ. БАЙТ


MPRBYT:



;   =========================
;   *** БЛОК ПРИЕМА БАЙТА ***
;   =========================


BPRB:	MTPS	#0		;- РАЗРЕШЕНИЕ ПРЕРЫВАНИЙ
	MOV	R3,-(SP)
	MOV	R4,-(SP)

	MOV	#ASPORT,R3	;  ПОДГ. ПАРАМ.
	MOV	#20,R4		;- СЧЕТЧИК БАЙТА
	CLR	R0

1$:	BIT	R4,(R3)		;  ПОИСК СТАРТОВОГО БИТА
	BEQ	1$
11$:	MOV	#260,(R3)	;- УСТ. ГОТОВН. ПРИЕМА
	BIT	R4,(R3)
	BNE	11$
	MTPS	#200		;- ЗАПРЕТ ПРЕРЫВАНИЙ

	MOV	DLBIT,R5	;  ПОИСК СЕРЕД. СТ. БИТА
	ASR	R5
2$:	SOB	R5,2$
	ASR	R4

6$:	MOV	DLBIT,R5	;- ЧТЕНИЕ БАЙТА
3$:	SOB	R5,3$
	BIT	#20,(R3)
	BNE	4$
	CLC
	BR	5$
4$:	SEC
	BR	5$
5$:	RORB	R0
	SOB	R4,6$
	MOV	#220,(R3)	;- СБРОС ГОТОВН. ПРИЕМА

	MOV	(SP)+,R4
	MOV	(SP)+,R3
	RTS	PC





; ===================================
; ***** МОДУЛЬ ПЕРЕДАЧИ МАССИВА *****
; ===================================


;	ВХОД:	R1 - АДРЕС МАССИВА
;		R2 - ДЛИНА МАССИВА В БАЙТАХ


MPDMAS:	MOV	R0,-(SP)

1$:	MOVB	(R1)+,R0	;  ПЕРЕДАЧА МАССИВА
	JSR	PC,BPDB
	SOB	R2,1$

	MOV	(SP)+,R0
	RTS	PC





; =================================
; ***** МОДУЛЬ ПРИЕМА МАССИВА *****
; =================================


;	ВХОД:	R1 - АДРЕС ПАМЯТИ ДЛЯ МАССИВА
;		R2 - ДЛИНА МАССИВА В БАЙТАХ


MPRMAS:	MOV	R0,-(SP)

1$:	JSR	PC,BPRB		;  ПРИЕМ МАССИВА
	MOVB	R0,(R1)+
	SOB	R2,1$

	MOV	(SP)+,R0
	RTS	PC



;   ===================
;   *** ДАННЫЕ DTLG ***
;   ===================


KDLBIT:				;  КОНСТАНТЫ ДЛИНЫ БИТА
DL9600:	.WORD	14
DL4800:	.WORD	36
DL2400:	.WORD	103
DL1200:	.WORD	213
DL600:	.WORD	435
DL300:	.WORD	1100
DL200:	.WORD	1542
DL150:	.WORD	2206
DL110:	.WORD	3300
DL75:	.WORD	4422
DL50:	.WORD	6633





; ================================
; ***** БЛОК ПЕРЕМЕННЫХ DMBK *****
; ================================


	.ASECT


;   ===============================
;   *** СИСТЕМНЫЕ ПРИЗНАКИ DMBK ***
;   ===============================

	. = 40

SYSPR:
PRCW:	.BYTE	0	;- ПРИЗНАК ЦВЕТНОГО РЕЖИМА
PRINWF:	.BYTE	0	;- ПРИЗНАК ИНВЕРСИИ ФОНА
PRRP:	.BYTE	0	;- ПРИЗНАК РАСШИРЕННОЙ ПАМЯТИ
PRRUS:	.BYTE	0	;- ПРИЗНАК РУССКОГО РЕГИСТРА
PRPODC:	.BYTE	0	;- ПРИЗНАК ПОДЧЕРКИВАНИЯ СИМВОЛА
PRINWS:	.BYTE	0	;- ПРИЗНАК ИНВЕРСИИ СИМВОЛА
PRISU:	.BYTE	0	;- ПРИЗНАК ИНДИКАЦИИ СУ
PRBLR:	.BYTE	0	;- ПРИЗНАК БЛОКИРОВКИ РЕДАКТИРОВАНИЯ
PRGRAF:	.BYTE	0	;- ПРИЗНАК ГРАФИЧЕСКОГО РЕЖИМА
PRZAP:	.BYTE	0	;- ПРИЗНАК РЕЖИМА ЗАПИСИ
PRSTIR:	.BYTE	0	;- ПРИЗНАК РЕЖИМА СТИРАНИЯ
PRCWSS:	.BYTE	0	;- ПРИЗНАК ЦВЕТНОГО РЕЖИМА В СС
PRPSSS:	.BYTE	0	;- ПРИЗНАК ПОДЧЕРКИВАНИЯ СИМВОЛА В СС
PRISSS:	.BYTE	0	;- ПРИЗНАК ИНВЕРСИИ СИМВОЛА В СС
PRGAHK:	.BYTE	0	;- ПРИЗНАК ГАШЕНИЯ КУРСОРА



;   ========================
;   *** ПЕРЕМЕННЫЕ DKLAW ***
;   ========================

	. = 100

WPTM:	.WORD	0	;- ВЕКТОР ПРЕРЫВАНИЯ ТАЙМЕРА
SSPTM:	.WORD	0	;- СЛОВО СОСТОЯНИЯ ПРЦ

BUFKL:	.BYTE	0	;- БУФЕР КЛАВИАТУРЫ
PRZKB:	.BYTE	0	;- ПРИЗНАК ЗАПИСИ КОДА В БУФЕР
BKPOWT:	.WORD	0	;- БУФЕР КОНСТАНТЫ ПОВТОРА
PRPOWT:	.BYTE	0	;- ПРИЗНАК ПОВТОРА КОДА

SCTAB:	.BYTE	0	;- СЧЕТЧИК ТАБУЛЯЦИИ
MTAB:	.BLKW	4	;- МАСКА ТАБУЛЯЦИИ

SCKLC:	.BYTE	0	;- СЧЕТЧИК КЛЮЧА
	.EVEN
TAKLC:	.WORD	0	;- ТЕКУЩИЙ АДРЕС КЛЮЧА
AKLC:	.BLKW	12	;  АДРЕСА КЛЮЧЕЙ



;   ========================
;   *** ПЕРЕМЕННЫЕ DTVMN ***
;   ========================


PRNRUL:	.BYTE	0	;- ПРИЗНАК НАРУШЕНИЯ РУЛОНА

PRZTCK:	.BYTE	0	;- ПРИЗНАК ЗАПИСИ ТОЧКИ
MASPGT:	.BYTE	0	;- МАСКА ПОЗИЦИИ ГТ
NMPGT:	.BYTE	0	;- НАЧАЛЬНАЯ МАСКА ПОЗИЦИИ ГТ

NOMSIM:	.WORD	0	;- НОМЕР СИМВОЛА НА ЭКРАНЕ
AS:	.WORD	0	;- АДРЕС СИМВОЛА
DSIMB:	.WORD	0	;- ДЛИНА СИМВОЛА В БАЙТАХ
KSVP:	.WORD	0	;- КОЛИЧЕСТВО СИМВОЛОВ НА ЭКРАНЕ

AGTVP:	.WORD	0	;- АДРЕС ГТ НА ЭКРАНЕ
AGT:	.WORD	0	;- АДРЕС ГТ
DGW:	.WORD	0	;- ДЛИНА ГРАФИЧЕСКОГО ВЕКТОРА
SCTVS:	.WORD	0	;- СЧЕТЧИК TV-СТРОК
BUFX:	.WORD	0	;- БУФЕР КООРДИНАТЫ X
BUFY:	.WORD	0	;- БУФЕР КООРДИНАТЫ Y

ANVP:	.WORD	0	;- АДРЕС НАЧАЛА ВИДЕОПАМЯТИ
BAZVP:	.WORD	0	;- БАЗА ВИДЕОПАМЯТИ
DVPB:	.WORD	0	;- ДЛИНА ВИДЕОПАМЯТИ В БАЙТАХ
DGPB:	.WORD	0	;- ДЛИНА ГРАФ. ПАМЯТИ В БАЙТАХ

FON:	.WORD	0	;- ФОН ЭКРАНА
MASCW:	.WORD	0	;- МАСКА ЦВЕТА
FONSS:	.WORD	0	;- ФОН СС
MCWSS:	.WORD	0	;- МАСКА ЦВЕТА СС

SCKOD:	.WORD	0	;- СЧЕТЧИК КОДОВ
SCUIND:	.WORD	0	;- СЧЕТЧИК УСТАНОВКИ ИНДИКАТОРОВ



;   =======================
;   *** ПЕРЕМЕННЫЕ DMAG ***
;   =======================

	. = 300

PROH:	.BYTE	0	;- ПРИЗНАК ОШИБКИ
	.EVEN
PRFCT:	.WORD	0	;- ПРИЗНАК ФИКТИВНОГО ЧТЕНИЯ
INCADR:	.WORD	0	;- ИНКРЕМЕНТ АДРЕСА МАССИВА

ABP:	.WORD	0	;- АДРЕС БЛОКА ПАРАМЕТРОВ
BUFSP:	.WORD	0	;- БУФЕР УКАЗАТЕЛЯ СТЕКА
BUFKS:	.WORD	0	;- БУФЕР КОНТРОЛЬНОЙ СУММЫ

GRDL0:	.WORD	0	;- ГРАНИЦА ДЛИНЫ 0

	. = 320

BPDMAG:			;  БЛОК ПАРАМЕТРОВ DMAG
KOM:	.BYTE	0	;- КОМАНДА
OTWET:	.BYTE	0	;- ОТВЕТ
ADRMAS:	.WORD	0	;- АДРЕС МАССИВА
DLMAS:	.WORD	0	;- ДЛИНА МАССИВА НА ЗАПИСЬ
IMMAS:	.BLKB	20	;- ИМЯ МАССИВА
ADRTM:	.WORD	0	;- АДРЕС ТЕКУЩЕГО МАССИВА
DLTMAS:	.WORD	0	;- ДЛИНА ТЕКУЩЕГО МАССИВА
IMTMAS:	.BLKB	20	;- ИМЯ ТЕКУЩЕГО МАССИВА



;   =================================
;   *** СИСТЕМНЫЕ ПЕРЕМЕННЫЕ DMBK ***
;   =================================


	. = 250

DLBIT0:	.WORD	0	;- ДЛИНА 0 DMAG
DLBIT1:	.WORD	0	;- ДЛИНА 1 DMAG
DLBIT:	.WORD	0	;- ДЛИНА БИТА DTLG
KPORT:	.WORD	0	;- КОПИЯ ПОРТА
APOPKL:	.WORD	0	;- АДР.ПРОГР.ОБР.ПРЕР.ОТ КЛАВ.
PRWK:	.WORD	0	;- ПРИЗНАК КОДА ВК
BUFSTA:	.WORD	0	;- БУФЕР СТАРТОВОГО АДРЕСА
BUFDL:	.WORD	0	;- БУФЕР ДЛИНЫ МАССИВА



	.END
