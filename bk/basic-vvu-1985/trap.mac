.ENABLE	LC
.GLOBL	DFRE,IFRE,$FRE,DLOCX,SLOCX,ILOCX,DLOCY,SLOCY,ILOCY
.GLOBL	FRE,SFRE,PEEK,LEN,INP,LOCX,LOCY,EOF			;INSTR
.GLOBL	ASC,VAL,SUM,SKIRT,SAND,POINT				;INSTR2
.GLOBL	LPOS,ILPOS,DLPOS,SLPOS
.GLOBL	$DADD$,$DSUB$,TMP,TSTSQZ
.GLOBL	$DDIV$,$DMUL$
.GLOBL	PI,RAN,$B91,$B93,$B95,$TAN,$B97,$B99
.GLOBL	$B103,$INT,$FIX,ABS,IABS,ISGN,DSGN,SSGN
.GLOBL	ISVEIL,NUMBER,ATLIN1,LNUM,ISV,DI,ID,SI,SD,IS,DS
.GLOBL	SEPAP,DVAR,SVAR,IVAR,DCONST,SCONST,ICONST
.GLOBL	APLMI,ADGDL,INEG,NEG,PACK,ADRHLT,AFUN,IRPS2,BREAK,BREAKW
.GLOBL	TABTOP,LENT,STAR,LYGIS,ERRS,SETST,DATINP,SAVJMP
.GLOBL	INDEX,ARRAY,ARR10,$USR,ISTR,DSTR,SSTR,BIN,OCT,HEX
.GLOBL	DXVAR,SXVAR,IXVAR,$XVAR,DCMP,SCMP,ICMP,$CMP,NOT,ADLOG,ADDRBR
.GLOBL	ATCONV,MOD,IDIV,$B103,$DMUL$,$B99,POW,BUFOUT
.GLOBL	FCB,INKEY,NEWSTR,CURLIN,STRREG,I1,TYPE,CIKL,STACK,LIMIT
.GLOBL	STR,STR2,IODEV,CONCAT,CHR,ALLSM,ALLSK	;SIGNAL
.GLOBL	FNST,FNCALL,DPAR,SPAR,IPAR,APAR,PFACT			;APRL
TRAP::	MOV	R0,-(SP)
	MOV	R2,-(SP)
	MOV	4(SP),R0
	MOV	-2(R0),R2
	BIC	#177400,R2
	SUB	#100,R2
	BLT	ERROR
	ASL	R2
	CALL	@ATRAP(R2)
	MOV	R0,4(SP)
TREND:	MOV	(SP)+,R2
	MOV	(SP)+,R0
	RTI
;
ERROR:	CLR	IODEV
	MOV	R2,R5
	ADD	#100,R5
;	MOV	#400,R1
;	MOV	#24,R2
;	CALL	SIGNAL
	MOV	#ERR,R1
	MOV	#10,R2
	CALL	ISVEIL
;	MOV	#400,R1
;	MOV	#37,R2
;	CALL	SIGNAL
	MOV	R5,R2
	TRAP	114	;KEICIA R1,R3,R5(R4 NE)
	TST	LYGIS
	BEQ	MONERR
	BMI	RUNERR
	TSTB	LYGIS
	BEQ	MONERR
	MOV	@PC,ERRS
	MOV	NUMBER,R5
	MOV	R5,CURLIN
	CALL	ATLIN1
	TSTB	LYGIS
	BMI	TREND
	BR	COMERR
RUNERR:	CMPB	R2,#13.
	BNE	10$
	TST	DATINP
	BEQ	GETERR
10$:	TST	SAVJMP
	BEQ	1$
	MOV	SAVJMP,R4
	CLR	SAVJMP
1$:	CMP	R4,STRREG
	BHI	MONERR
	JSR	R2,SEPAP
	MOV	#IRPS2-10,@#BREAKW
	CALL	LNUM
	MOV	R2,CURLIN
;	CMP	-(4),-(4)
;	JSR	R0,SETST
;	BR	2$
	CLR	ADRHLT
;	BR	3$
;2$:	MOV	@R4,ADRHLT
3$:	CLR	LYGIS
	MOV	#BREAK,@#BREAKW
	BR	COMERR
MONERR::MOV	#12,R0
	CALL	ISV
COMERR:	MOV	#STACK,SP
COMER2:	JMP	STAR
GETERR:	MOV	#12,R0
	CALL	ISV
	BR	TREND
;
ERR:	.BYTE	357,373,351,342,353,341,40,7
	.EVEN
;
ATRAP:	.WORD	T100
	.WORD	T101
	.WORD	T102
	.WORD	T103
	.WORD	T104
	.WORD	T105
	.WORD	T106
	.WORD	T107
	.WORD	T110
	.WORD	T111
	.WORD	T112
	.WORD	T113
	.WORD	T114
	.WORD	T115
	.WORD	T116
	.WORD	T117
	.WORD	T120
	.WORD	T121
	.WORD	T122
	.WORD	T123
	.WORD	T124
	.WORD	T125
	.WORD	T126
	.WORD	T127
	.WORD	T130
;
RWMOD:	.ASCII	/MOD /
RWLOG:	.ASCII	/NOT IMP EQV XOR OR AND /
FUN:	.ASCII	/PI POINT INP PEEK /
	.ASCII	/RND SQR SIN COS TAN ATN /
	.ASCII	/EXP LOG INT FIX ABS SGN CINT CDBL CSNG /
	.ASCII	/EOF CSRLIN POS LPOS FRE LEN ASC VAL /
;	.ASCII	/INSTR VARPTR /
	.ASCII	/USR FN /
SFUN:	.ASCII	/INKEY MID STRING STR CHR BIN OCT HEX /
RELAT:	.BYTE	'=,'>,'<,0
PLMI::	.BYTE	'+,'-,0
DGDL:	.BYTE	'/,'*,0
PRSPEC::.BYTE	'"
SPECHR:	.BYTE	'&,'.,'(,0
TYPBOH:	.BYTE	'B,'O,'H,0
TYPCHR:	.BYTE	'$,'!,'#,'%,0
	.EVEN
;
T100:	MOV	#-1,R5
1$:	MOV	R5,-(SP)
	CALL	SANT
	TRAP	113
	.WORD	RWLOG+4
	.BYTE	3,10
	BR	4$
	CALL	CINT
2$:	CMP	R5,@SP
	BGT	1$
	MOV	(SP)+,R2
	MOV	ADLOG(R2),(R1)+
	BR	2$
3$:	CALL	CINT
	MOV	ADLOG(R2),(R1)+
4$:	MOV	(SP)+,R2
	BGE	3$
	RETURN
;
SANT:	TRAP	112
	CLR	-(SP)
	TRAP	113
	.WORD	RWLOG,3
	INC	@SP
	MOV	TMP,-(SP)
	TRAP	115
	BR	1$
	CALL	LYGOP
	BCS	10$
	TRAP	115
10$:	TRAP	13.
	MOV	(SP)+,TMP
	MOV	#$CMP,(R1)+
	BR	7$
1$:	TST	(SP)+
	CALL	SUDAT
2$:	CALL	LYGOP
	BMI	8$
	MOV	R2,-(SP)
	MOV	R1,-(SP)
	MOV	TYPE,-(SP)
	CALL	SUDAT
	MOVB	TYPE,R5
	CMPB	R5,@SP
	BEQ	5$
	BHI	4$
	MOV	@SP,TYPE
	MOV	R1,R4
	TST	(R4)+
	MOV	R4,@SP
3$:	MOV	-(R1),-(R4)
	CMP	R1,2(SP)
	BHI	3$
	INC	R5
	ASL	R5
	CALL	@ATCONV(R5)
	MOV	@SP,R1
	BR	6$
4$:	MOVB	@SP,R5
	INC	R5
	ASL	R5
	CALL	@ATCONV(R5)
	BR	6$
5$:	INC	R5
	ASL	R5
6$:	MOV	ACMP(R5),(R1)+
	CMP	(SP)+,(SP)+
	MOV	(SP)+,R2
7$:	MOV	#377,TYPE
	MOV	ADDRBR (R2),(R1)+
	BR	2$
8$:	TST	(SP)+
	BNE	9$
	CALL	CINT
	MOV	#NOT,(R1)+
9$:	RETURN
;
ACMP:	.WORD	ICMP,	DCMP,	SCMP
;
LYGOP:	TRAP	101
	.WORD	RELAT
	BR	1$
	MOV	R4,R2
	TRAP	101
	.WORD	RELAT
	RETURN
	CMP	R4,R2
	BEQ	2$
	CMP	(R4)+,(R4)+
	ADD	R4,R2
	RETURN
1$:	COM	R4
	RETURN
2$:	TRAP	2
;
SUDAT:	CALL	LIEK
3$:	TRAP	101
	.WORD	PLMI
	RETURN
	MOV	TYPE,-(SP)
	MOV	R4,-(SP)
	CALL	CDBL
	MOV	R1,-(SP)
	CALL	LIEK
	MOV	(SP)+,R4
	MOV	(SP)+,R2
	TSTB	TYPE
	BPL	1$
	TSTB	@SP
	BPL	1$
	CALL	SUSP
	CMP	(R2)+,(R2)+
	BR	2$
1$:	CALL	CDBL
2$:	MOV	APLMI(R2),(R1)+
	TST	(SP)+
	BR	3$
;
LIEK:	CALL	SDAL
1$:	TRAP	113
	.WORD	RWMOD
	.WORD	3
	RETURN
	CALL	CINT
	CALL	SDAL
	CALL	CINT
	MOV	#MOD,(R1)+
	BR	1$
;
SDAL:	CALL	DAUG
1$:	CMPB	@R3,#'\
	BNE	D3
	INC	R3
	CALL	CINT
	CALL	DAUG1
	CALL	CINT
	MOV	#IDIV,(R1)+
	BR	1$
;
DAUG:	CLR	-(SP)
	TRAP	101
	.WORD	PLMI
	BR	DAUG2
	MOV	R4,@SP
	BR	DAUG2
DAUG1:	CLR	-(SP)
DAUG2:	CALL	LAIPS
	TST	(SP)+
	BEQ	D2
	TSTB	TYPE
	BPL	D1
	MOV	#INEG,(R1)+
	BR	D2
D1:	MOV	#NEG,(R1)+
D2:	TRAP	101
	.WORD	DGDL
D3:	RETURN
	MOV	TYPE,-(SP)
	MOV	R4,-(SP)
	CALL	CDBL
	MOV	R1,-(SP)
	CALL	LAIPS
	MOV	(SP)+,R4
	MOV	(SP)+,R2
	BEQ	1$
	TSTB	TYPE
	BPL	1$
	TSTB	@SP
	BPL	1$
	CALL	SUSP
	TST	(R2)+
	BR	2$
1$:	CALL	CDBL
2$:	MOV	ADGDL(R2),(R1)+
	TST	(SP)+
	BR	D2
;
LAIPS:	CALL	OPER
1$:	CMPB	(R3),#'^
	BNE	D3
	INC	R3
	CALL	CDBL
	MOV	#$B103,(R1)+	; LOG
	MOV	R1,-(SP)
	CALL	OPER
	MOV	(SP)+,R4
	TSTB	TYPE
	BMI	2$
	CALL	CDBL
	MOV	#$DMUL$,(R1)+
	MOV	#$B99,(R1)+	;EXP
	BR	1$
2$:	CALL	SUSP
	MOV	#POW,(R1)+
	CLR	TYPE
	BR	1$
;
SUSP:	MOV	R4,R5
	TST	-(R5)
3$:	MOV	(R4)+,(R5)+
	CMP	R4,R1
	BLO	3$
	TST	-(R1)
	RETURN
;
OPER:	CALL	CISLO
	TRAP	24.
	BR	3$
	BR	2$
	MOV	R4,TYPE
	BMI	13$
	BEQ	11$
	MOV	#SCONST,(R1)+
	CMP	(R1)+,(R1)+
	MOV	R1,R4
	BR	12$
11$:	MOV	#DCONST,(R1)+
	ADD	#10,R1
	MOV	R1,R4
	MOV	(SP)+,-(R4)
	MOV	(SP)+,-(R4)
12$:	MOV	(SP)+,-(R4)
	MOV	(SP)+,-(R4)
	RETURN
13$:	MOV	#ICONST,(R1)+
	MOV	R5,(R1)+
	CLRB	TYPE+1
	RETURN
2$:	JMP	OP2
3$:	TRAP	105
	BR	31$
	TRAP	13.
	TRAP	13.
	CLR	R4
	TRAP	121
	TRAP	103
30$:	RETURN
31$:	CMP	R5,#66
	BLT	4$
;	BEQ	34$	;VARPTR
	MOV	TMP,-(SP)
	CALL	USR	;USR
	BMI	32$
	MOV	(SP)+,TMP
	RETURN
32$:	TRAP	13.
;33$:	TRAP	5
;34$:	TRAP	127	;VARPTR
;	CLR	R4
;	TRAP	107
;	NOP
;	TST	R5
;	BEQ	33$
;	MOV	#377,TYPE
;	TSTB	R5
;	BMI	35$
;	MOV	#ICONST,(1)+
;	MOV	R4,(1)+
;35$:	JMP	OP3
4$:	MOV	R5,-(SP)
	BEQ	6$
	CMP	R5,#46		; SURISTA SU FUNKCIJOMIS
	BLT	5$
	CMP	R5,#60		; SURISTA SU FUNKCIJOMIS
	BGE	40$
	DECB	TYPE
	CMP	R5,#46
	BEQ	6$
	CMPB	@R3,#'(
	BNE	6$
	SUB	#61,R5
	ASL	R5
	ADD	R5,@SP
40$:	ADD	#32,@SP
	TRAP	127
	CMP	@SP,#100		;SURISTA SU FUNKCIJOMIS
	BGT	42$
41$:	TRAP	100
	MOVB	TYPE,R5
	INC	R5
	SUB	R5,TYPE
	ASL	R5
	ADD	@SP,R5
	BR	46$
42$:	MOV	TMP,-(SP)
	TRAP	122
	BR	47$
	MOV	(SP)+,TMP
	CLR	TYPE
	CMP	@SP,#116
	BEQ	45$
;	BLT	44$
;	TRAP	126
;	TRAP	115
;43$:	TRAP	13.
44$:	DECB	TYPE
45$:	MOV	@SP,R5
46$:	TRAP	130
	BR	6$
47$:	TST	(SP)+
	ADD	#2,@SP
	CMP	@SP,#104
	BEQ	41$
;	CMP	@SP,#122
	BNE	32$
;	TRAP	125
;	TRAP	126
;	BR	42$
48$:	TRAP	124
	BR	45$
49$:	TRAP	125
	BR	45$
5$:	TRAP	127
	CMP	R5,#6
	BLT	48$
	BEQ	49$
	TRAP	100
	TRAP	130
	MOV	(SP),R5
	CMP	R5,#26
	BLE	52$
	MOVB	TYPE,R4
	INC	R4
	ASL	R4
	CMP	R5,#36
	BGT	7$
	BEQ	51$
	CMP	R5,#34
	BEQ	50$
	TST	R4
	BNE	52$
	BR	60$
50$:	TST	R4
	BNE	6$
51$:	TST	(R4)+
	ADD	R4,R5
	BR	6$
52$:	CALL	CDBL
6$:	MOV	AFUN(R5),(R1)+
60$:	TST	(SP)+
	RETURN
7$:	SUB	#40,R5
	CALL	@ATCONV(R5)
	BR	60$
OP2:	TRAP	100
T130:
OP3:	CMPB	(R3)+,#')
	BEQ	SKLUZ
OP4:	TRAP	2
;
T127:	TRAP	112
	CMPB	(R3)+,#'(
	BNE	OP4
SKLUZ:	TRAP	112
	RETURN
;
CINT::	MOVB	TYPE,R4
	BLT	1$
	ASL	R4
	MOV	ACINT(R4),(R1)+
	MOV	#377,TYPE
1$:	RETURN
;
CSNG::	MOVB	TYPE,R4
	BGT	1$
	INC	R4
	ASL	R4
	MOV	ACSNG(R4),(R1)+
	MOV	#1,TYPE
1$:	RETURN
;
CDBL::	MOVB	TYPE,R4
	BEQ	1$
	INC	R4
	MOV	ACDBL(R4),(R1)+
	CLR	TYPE
1$:	RETURN
;
ACDBL:	.WORD	ID,SD
ACSNG:	.WORD	IS,DS
ACINT:	.WORD	DI,SI
ABOH:	.WORD	CBIN,COCT,CHEX
;
CHEX:	CLR	R5
6$:	TRAP	102
	BR	C1
	BR	7$
	SUB	#77,R4
8$:	ADD	#15.,R4
	INC	R3
	CMP	R5,#10000
	BHIS	C3
	ASL	R5
	ROL	R5
	ROL	R5
	ROL	R5
	BIS	R4,R5
	BR	6$
7$:	SUB	#'F,R4
	BLE	8$
C1:	RETURN
;
COCT:	CLR	R5
5$:	MOVB	@R3,R4
	SUB	#60,R4
	BIT	#177770,R4
	BNE	C1
	INC	R3
	ASL	R5
	BCS	C3
	ROL	R5
	BCS	C3
	ROL	R5
	BCS	C3
	BIS	R4,R5
	BR	5$
;
CBIN:	CLR	R5
2$:	MOVB	@R3,R4
	SUB	#60,R4
	ROR	R4
	BNE	C1
	INC	R3
	ROL	R5
	BCC	2$
C3:	TRAP	6
;
;
BOHPAP::TRAP	102
	BR	14$
	BR	11$
14$:	MOV	4(SP),R4
	TRAP	2
11$:	TRAP	101
	.WORD	TYPBOH
	BR	14$
	CALL	@ABOH(R4)
	RETURN
;
CISLO::	MOV	(SP)+,R2
	TST	(R2)+
	TRAP	101
	.WORD	SPECHR
	BR	1$
	ADD	R4,PC
	BR	10$
	BR	13$
	TST	(R2)+
	MOV	R2,PC
10$:	CALL	BOHPAP
12$:	MOV	#-1,R4
15$:	CMP	(R2)+,(R2)+
	MOV	R2,PC
1$:	TRAP	102
	BR	16$
	MOV	R2,PC
	BR	2$
16$:	TST	LYGIS
	BMI	2$
	TST	-(R2)
	MOV	R2,PC
13$:	DEC	R3
2$:	MOV	R3,-(SP)
	CLR	-(SP)
	TRAP	101
	.WORD	PLMI
	NEG	R4
	MOV	R4,@SP
	TRAP	117
	TRAP	101
	.WORD	TYPCHR+1
	NEG	R4
	CMP	R4,#2
	BLE	20$
	MOV	(SP)+,R4
	BMI	21$
	INC	@SP
21$:	MOV	(SP)+,R3
	MOV	R4,-(SP)
	TRAP	110
	CLR	R5
	TRAP	117
	INC	R3
	TST	R5
	BMI	24$
	TST	(SP)+
	BLE	12$
26$:	NEG	R5
	BMI	12$
	BR	27$
24$:	TST	(SP)+
	BGT	26$
27$:	TRAP	6
20$:	MOV	R4,R5
	TST	(SP)+
	MOV	@SP,R3
	MOV	#500,-(SP)
	CALL	PACK
	CLR	R4
	ROR	(SP)+
	BCS	23$
	TST	R5
	BEQ	22$
	BMI	15$
	INC	R3
	BR	15$
22$:	INC	R3
23$:	ROL	4(SP)
	ADC	2(SP)
	ADC	@SP
	BVS	27$
	BCS	27$
	MOV	(SP)+,2(SP)
	MOV	(SP)+,2(SP)
	INC	R4
	BR	15$
;
T101:	TRAP	112
	MOV	(R0)+,R2
	MOV	#-2,R4
1$:	TSTB	@R2
	BEQ	2$
	ADD	#2,R4
	CMPB	(R2)+,@R3
	BNE	1$
	INC	R3
	TST	(R0)+
2$:	RETURN
;
MZ:	BICB	#40,@R3
T102:	MOVB	@R3,R4
	CMPB	R4,#172
	BHI	2$
	CMPB	R4,#140
	BHI	MZ
	CMPB	R4,#132
	BHI	2$
	CMPB	R4,#100
	BHI	1$
	CMPB	R4,#71
	BHI	2$
	CMPB	R4,#60
	BLO	2$
	TST	(R0)+
1$:	TST	(R0)+
2$:	RETURN
;
T103:	TRAP	112
	CLR	R2
	CMPB	@R3,#'(
	BNE	2$
	MOV	R4,R2
	TST	(R2)+
	INC	R3
	MOV	TYPE,-(SP)
	MOV	R5,-(SP)
	TRAP	125
	CLR	-(SP)
6$:	TRAP	116
	BR	1$
	INC	@SP
	CALL	CINT
	BR	6$
1$:	MOV	R2,R4
	MOV	(SP)+,R2
	MOV	(SP)+,R5
	MOV	(SP)+,TYPE
	TRAP	130
	BR	8$
2$:	TST	R4
	BEQ	7$
8$:	BIS	#200,R5
	INC	R2
	DECB	TYPE+1
	BR	4$
7$:	TST	R5
	BMI	4$
	TSTB	TYPE
	BEQ	5$
	BPL	4$
	TST	(R4)+
5$:	CMP	(R4)+,(R4)+
4$:	MOV	AKINPP(R4),(R1)+
	TRAP	104
	MOV	R4,(R1)+
	TSTB	R5
	BPL	3$
	MOV	R2,(R1)+
	MOV	#6,R2
	INCB	TYPE+1
	BMI	9$
	MOVB	TYPE,R2
	INC	R2
	ASL	R2
9$:	MOV	AXKINP(R2),(R1)+
3$:	RETURN
;
AKINPP:	.WORD	SVAR,	INDEX,	DVAR,IVAR
	.WORD	ARR10,	ARRAY
;
AXKINP:	.WORD	IXVAR,DXVAR,SXVAR,$XVAR
;
T104:	MOV	LENT,R4
1$:	CMP	R4,TABTOP
	BHIS	2$
	MOV	(R4)+,R2
	CMP	R5,R2
	BEQ	3$
	TSTB	R2
	BMI	10$
	ROL	R2
	BCS	10$
	BMI	11$
	ROL	R2
	BMI	10$
	CMP	(R4)+,(R4)+
10$:	TST	(R4)+
11$:	TST	(R4)+
	BR	1$
2$:	TST	LYGIS
	BMI	50$
	MOV	LENT,R4
	SUB	#6,R4
	MOV	TYPE,R2
	BMI	20$
	TSTB	R2
	BEQ	22$
	BGT	20$
	TST	(R4)+
	BR	20$
22$:	CMP	-(R4),-(R4)
20$:	CMP	R4,R1
	BLOS	4$
	MOV	R4,LENT
	MOV	R4,LIMIT
	MOV	R4,CIKL
	MOV	R5,(R4)+
	MOV	R4,R2
	TST	TYPE+1
	BMI	30$
	TSTB	TYPE
	BMI	33$
	BNE	30$
	CLR	(R2)+
	CLR	(R2)+
30$:	CLR	(R2)+
33$:	CLR	(R2)+
	TSTB	R5
	BPL	3$
	MOVB	#2,-(R2)
	TST	R5
	BMI	40$
	TSTB	TYPE
	BMI	3$
	BNE	35$
	ASLB	@R2
35$:	ASLB	@R2
3$:	RETURN
40$:	INCB	@R2
	RETURN
50$:	CLR	R4
	RETURN
4$:	TRAP	7
;
T105:	MOV	R3,-(SP)
	CLR	TYPE
	TRAP	113
	.WORD	FUN
	.BYTE	6,110		;КОЛИЧЕСТВО ФУНКЦИЙ
	BR	2$
	TST	(SP)+
	CMP	R5,#72		;КОЛИЧЕСТВО ЧИСЛОВЫХ ФУНКЦИЙ
	BLT	1$
	SUB	#72,R5		;ПЕРЕКОДИРОВАТЬ С НУЛЯ
	CMPB	(R3)+,#'$
	BEQ	6$
	TRAP	13.
2$:	MOV	(SP)+,R3
	CMP	(R0)+,(R0)+
	CLR	R5		;MOV	#20000,R5	TRACE
	BISB	(R3)+,R5
	BIC	#100,R5
	SWAB	R5
	TRAP	102
	BR	4$
	NOP			;CLRB	R5		TRACE
	BISB	(R3),R5
	BIC	#100,R5
3$:	INC	R3
	TRAP	102
	BR	4$
	BR	3$
	BR	3$
4$:	TRAP	101
	.WORD	TYPCHR
	BR	6$	;8$	SU DEFTYP
9$:	ADD	R4,PC
	BR	5$
	BR	7$
	BR	6$
	BIS	#40000,R5
	DECB	TYPE
	BR	6$
7$:	BIS	#20000,R5
	INC	TYPE
6$:	TST	(R0)+
1$:	RETURN
;8$:	MOV	R5,R4
;	CLRB	R4
;	SWAB	R4
;	MOVB	APRL-1(R4),R4
;	BR	9$
5$:	BIS	#160000,R5
	COMB	TYPE+1
	RETURN
;
APLMI::	.WORD	$DADD$,$DSUB$,SUM,SKIRT
ADGDL::	.WORD	$DDIV$,$DMUL$,SAND
AFUN::	.WORD	PI,POINT,INP,PEEK,RAN,$B91,$B93,$B95,$TAN,$B97,$B99
	.WORD	$B103,$INT,$FIX,ABS,IABS,ISGN,DSGN,SSGN
	.WORD	EOF,LOCY,LOCX,LPOS,FRE,ILOCY,DLOCY,SLOCY
	.WORD	ILOCX,DLOCX,SLOCX,ILPOS,DLPOS,SLPOS
	.WORD	$FRE,IFRE,DFRE,SFRE
	.WORD	LEN,ASC,VAL			;INSTR2,INSTR
;
T106:	TRAP	112
	TST	4(SP)
	BEQ	2$
	CMPB	@R3,#12
	BEQ	2$
	CMPB	@R3,#''
	BNE	1$
2$:	RETURN
1$:	TRAP	2
;
T107:	MOV	R4,R2
	TRAP	112
	TRAP	102
	TRAP	2
	BR	2$
3$:	TRAP	2
1$:	SUB	#2,R5
	BNE	3$
	RETURN
2$:	TRAP	105
	TRAP	2
	BR	1$
	TST	-(R0)
	TST	(R0)+
	MOV	R2,R4
	TRAP	121
	BR	5$
	CMP	-(1),-(1)
	MOV	#APAR,(1)+
	MOV	-6(4),(1)+
	NEGB	R5
	RETURN
5$:	TRAP	103
	TSTB	R5
	BMI	4$
	TST	-(R1)
4$:	TST	-(R1)
	RETURN
;
T110:	CLR	R5
	TRAP	112
	TRAP	102
	RETURN
	RETURN
1$:	SUB	#60,R4
	INC	R3
	ASL	R5
	ADD	R5,R4
	ASL	R5
	ASL	R5
	ADD	R4,R5
	BCS	2$
	TRAP	102
	BR	3$
	BR	3$
	CMP	R5,#14631
	BLOS	1$
2$:	MOV	16(SP),R4
	TRAP	6
3$:	TST	(R0)+
	RETURN
;
T111:	MOV	FCB,R4
	TST	-(R4)
1$:	CMP	R4,TABTOP
	BLO	2$
	CMP	R5,(R4)
	BEQ	3$
	BLO	2$
	SUB	#6,R4
	BR	1$
2$:	TST	(R0)+
3$:	RETURN
;
T112:	CMPB	(R3)+,#40
	BEQ	T112
	DEC	R3
	RETURN
;
T113:	MOV	R1,-(SP)
	MOV	R3,-(SP)
	MOV	#-1,-(SP)
	CLR	R1
	CLR	R5
	MOV	(R0)+,R2
	TRAP	102
	TRAP	112
	BR	1$
	BR	14$
1$:	CLRB	@SP
2$:	CMPB	(R3),(R2)+
	BNE	3$
	INC	R3
	INCB	@SP
	CMPB	@R2,#40
	BEQ	6$
	TRAP	102
	BR	3$
	BR	2$
3$:	DECB	@SP
	BLE	4$
	CMP	R1,R3
	BHI	4$
	BEQ	7$
	MOV	R3,R1
	MOVB	R5,1(SP)
4$:	MOV	2(SP),R3
	CMPB	R5,1(R0)
	BEQ	11$
5$:	CMPB	(R2)+,#40
	BNE	5$
	TST	(R5)+
	BR	1$
7$:	MOV	#-1,@SP
	BR	4$
11$:	MOVB	1(SP),R5
	BMI	14$
	CMPB	@R0,#3
	BGE	14$
	MOV	R1,R3
6$:	TRAP	112
	TST	(R0)+
14$:	TST	(R0)+
	CMP	(SP)+,(SP)+
	MOV	(SP)+,R1
	RETURN
;
T114:	MOV	#BUFOUT,R3
	CALL	I1
	CALL	ISVEIL
	RETURN
;
T115:	TRAP	112
	CLR	R2
	MOV	R3,-(SP)
	TRAP	102
	BR	1$
	BR	6$
	BR	NOT$
1$:	TRAP	120
	BR	NOT$
	BR	S10
6$:	TRAP	105
	BR	SIMUSR
	BR	SIMFUN
	BR	8$
	BR	NOT$
8$:	CLR	R4
	TRAP	121
	TRAP	103
S10:	TRAP	112
	TST	(SP)+
	CMPB	(R3),#'+
	BNE	2$
	TST	R2
	BGT	3$
4$:	INC	R2
	BEQ	4$
	CALL	TSTSQZ
3$:	INC	R3
	TRAP	115
	TRAP	13.
	TST	R4
	BGT	1$
	MOV	#NEWSTR,(R1)+
1$:	MOV	#CONCAT,(R1)+
2$:	TST	(R0)+
	MOV	R2,R4
	RETURN
;
SIMUSR:	CMP	R5,#66	;USR$
	BLT	NOT$
	MOV	R1,-(SP)
	CALL	USR
	BMI	S2
	MOV	(SP)+,R1
NOT$:	MOV	(SP)+,R3
	RETURN
S2:	TST	(SP)+
	INC	R2
	BR	S10
;
SIMFUN:	ASR	R5	;MID$
	BEQ	10$
	TRAP	127
	DEC	R5
	BNE	S30
	TRAP	115
	TRAP	13.
	CALL	MIDEND
	DEC	R2
	BR	S10
;
10$:	MOV	#INKEY,(R1)+	;INKEY$
	BR	S10
;
S20:	DEC	R5	;CHR$,BIN$,OCT$,HEX$
	ASL	R5
	MOV	R5,-(SP)
21$:	TRAP	125
	MOV	(SP)+,R5
	MOV	ASFINT(R5),(R1)+
	BR	S33
;
S30:	DEC	R5	;STRING$
	BNE	S40
	TRAP	125
	TRAP	126
	CALL	TSTSQZ
	TST	-(R1)
	TRAP	122
	BR	31$
	MOV	#ALLSM,(R1)+
	BR	S32
31$:	TRAP	125
	MOV	#ALLSK,(R1)+
S32:	INC	R2
S33:	TRAP	130
	BR	S10
;
S40:	DEC	R5	;STR$
	BNE	S20
	TRAP	100
	MOVB	TYPE,R4
	INC	R4
	ASL	R4
	MOV	ASTR(R4),(R1)+
	BR	S33
;
ASTR:	.WORD	ISTR,	DSTR,	SSTR
ASFINT:	.WORD	CHR,	BIN,	OCT,	HEX
;
LMID::	TRAP	127
	CLR	R4
	TRAP	107
	BR	10$
	TRAP	13.
10$:	TST	(R1)+
	TSTB	R5
	BMI	MIDEND
	TST	(R1)+
MIDEND:	TRAP	126
	TRAP	125
	TRAP	116
	BR	11$
	CALL	CINT
	MOV	#STR,(R1)+
	BR	12$
11$:	MOV	#STR2,(R1)+
12$:	TRAP	130
	RETURN
;
USR:	TRAP	112
	CLR	R2
	TRAP	102
	BR	1$
	BR	4$
	MOVB	(R3)+,R2
	SUB	#'0,R2
	ASL	R2
1$:	CMP	R5,#70
	BEQ	SYNERR
	TRAP	127
	TRAP	122
	BR	2$
	TST	R4
	BGT	3$
	CALL	TSTSQZ
	BR	3$
2$:	TRAP	100
3$:	MOV	#$USR,(R1)+
	MOV	R2,(R1)+
	TRAP	130
	MOV	TYPE,(R1)+
	RETURN
4$:	CMP	R5,#70
	BNE	SYNERR
	TRAP	105
	TRAP	2
	TRAP	2
	NOP
	BIS	#100100,R5
	MOV	TYPE,-(SP)
	MOV	#-1,TYPE
	TRAP	104
	MOV	R4,-(SP)
	CALL	TSTSQZ
	MOV	#FNST,-2(1)
	TRAP	112
	CMPB	@R3,#'(
	BNE	7$
40$:	INC	R3
	TRAP	122
	BR	41$
	MOV	#177400,-(SP)
	TST	R4
	BGT	5$
	MOV	#NEWSTR,(R1)+
	BR	5$
41$:	TRAP	100
	MOV	TYPE,-(SP)
5$:	INC	R2
	CMPB	@R3,#',
	BEQ	40$
6$:	TRAP	130
7$:	MOV	#FNCALL,(1)+
	MOV	R1,R4
	TST	(1)+
	MOV	R2,(1)+
	BEQ	9$
8$:	MOV	(SP)+,(R1)+
	SOB	R2,8$
9$:	MOV	(SP)+,@R4
	MOV	(SP)+,TYPE
	RETURN
;
T116:	TRAP	112
	CMPB	@R3,#',
	BNE	2$
	MOV	R3,-(SP)
	INC	R3
	TRAP	112
	CMPB	@R3,#',
	BEQ	1$
	CMPB	@R3,#12
	BEQ	1$
	TRAP	100
	TST	(R0)+
	TST	(SP)+
	RETURN
1$:	MOV	(SP)+,R3
2$:	RETURN
;
T117:	CLR	R2
1$:	TRAP	102
	BR	3$
	BR	4$
2$:	INC	R3
	BR	1$
3$:	CMPB	R4,#'.
	BNE	4$
	INC	R2
	BR	2$
4$:	DEC	R2
	BGT	SYNERR
	RETURN
SYNERR:	TRAP	2
;
T120:	CMPB	@R3,#'"
	BNE	1$
3$:	INC	R3
	MOV	#SCONST,(R1)+
	MOV	R3,(R1)+
	CLR	R4
4$:	CMPB	@R3,#12
	BEQ	5$
	CMPB	(R3)+,#'"
	BEQ	5$
	INC	R4
	BR	4$
5$:	MOV	R4,(R1)+
	TST	(R0)+
	MOV	#177400,TYPE
1$:	RETURN
;
T121:	MOV	PFACT,R2
	BEQ	4$
1$:	TST	(2)+
	BEQ	4$
	TST	(2)+
	CMP	(2)+,R5
	BNE	1$
	TST	TYPE
	BMI	3$
	TSTB	TYPE
	BEQ	2$
	BPL	3$
	TST	(4)+
2$:	TST	(4)+
3$:	MOV	PARMOV(R4),(1)+
	MOV	(2),(1)+
	MOV	R2,R4
	TST	(0)+
4$:	RETURN
;
PARMOV:	.WORD	SPAR,DPAR,IPAR
;
T122:	MOV	R3,-(SP)
	TRAP	115
	BR	1$
	TRAP	101
	.WORD	RELAT
	BR	2$
1$:	MOV	(SP)+,R3
	RETURN
2$:	CMP	(SP)+,(R0)+
	RETURN
;
T123:	TRAP	125
	TRAP	126
T124:	TRAP	125
	TRAP	126
T125:	TRAP	100
	CALL	CINT
	RETURN
;
T126:	TRAP	112
	CMPB	(R3)+,#',
	BNE	1$
	TRAP	112
	RETURN
1$:	TRAP	2
;
	.END
