	.TITLE	MONIT
	.ENABLE LC
	; **********************
	; *                    *
	; *   bejsik monitor   *
	; *                    *
	; **********************
	;
	; =============================
	; ***ОБЛАСТЬ ДАННЫХ МОНИТОРА***
	; =============================
;
BASVU:	.BYTE	14,40,342,345,352,363,351,353,40,40,40,40,40
	.BYTE	50,367,351,354,370,356,340,363
	.ASCII	/,1986.07.24)/<12>	;НАЧАЛЬНОЕ СООБЩЕНИЕ
;
OK:	.BYTE	117,153,12	;OK
;
OPRNAM::.ASCII	/  /	;ТАБЛИЦА ИМЕН ОПЕРАТОРОВ
	.ASCII	/' /
	.ASCII	/DIM /
	.ASCII	/LINE /
	.ASCII	/CIRCLE /
	.ASCII	/DEF /
	.ASCII	/TROFF /
OPFOR::	.ASCII	/FOR /
	.ASCII	/GOSUB /
	.ASCII	/GOTO /
	.ASCII	/IF /
	.ASCII	/INPUT /
	.ASCII	/LET /
	.ASCII	/OPEN /
	.ASCII	/? /
	.ASCII	/CLOSE /
	.ASCII	/NEXT /
	.ASCII	/PAINT /
	.ASCII	/PRINT /
	.ASCII	/DRAW /
	.ASCII	/REM /
	.ASCII	/RETURN /
	.ASCII	/_ /
	.ASCII	/CALL /
	.ASCII	/READ /
	.ASCII	/POKE /
	.ASCII 	/OUT /
	.ASCII	/STOP /
	.ASCII	/LPRINT /
	.ASCII	/ON /
	.ASCII	/RESTORE /
	.ASCII	/END /
	.ASCII	/DATA /
	.ASCII	/LOCATE /
	.ASCII	/CLEAR /
	.ASCII	/TRON /
	.ASCII	/KEY /
	.ASCII	/BEEP /
	.ASCII	/CLS /
	.ASCII	/COLOR /
	.ASCII	/PSET /
	.ASCII	/PRESET /
	.ASCII	/CLOAD /
	.ASCII	/BLOAD /
	.ASCII	/FIND /
	.ASCII	/CSAVE /
	.ASCII	/BSAVE /
	.ASCII	/LOAD /
	.ASCII	/SAVE /
	.ASCII	/NEW /
	.ASCII	/AUTO /
	.ASCII	/LIST /
	.ASCII	/MONIT /
	.ASCII	/LLIST /
	.ASCII	/RENUM /
	.ASCII	/DELETE /
	.ASCII	/CONT /
	.ASCII	/RUN /
;	.ASCII	/MOTOR /
;	.ASCII	/MERGE /
;	.ASCII	/ERASE /
;	.ASCII	/SWAP /
;	.ASCII	/MAXFILES /
	.EVEN
;
;	ТАБЛИЦА АДРЕСОВ ИМЕН ОПЕРАТОРОВ
;
AOPNAM::.WORD	OPRNAM,OPRNAM+2,OPRNAM+4,OPRNAM+10,OPRNAM+15		;-CIRCLE
	.WORD	OPRNAM+24,OPRNAM+30,OPRNAM+36,OPRNAM+42,OPRNAM+50	;-GOTO
	.WORD	OPRNAM+55,OPRNAM+60,OPRNAM+66,OPRNAM+72			;-OPEN
	.WORD	OPRNAM+77,OPRNAM+101,OPRNAM+107,OPRNAM+114		;-PAINT
	.WORD	OPRNAM+122,OPRNAM+130,OPRNAM+135,OPRNAM+141,OPRNAM+150	;-_
	.WORD	OPRNAM+152,OPRNAM+157,OPRNAM+164,OPRNAM+171,OPRNAM+175	;-STOP
	.WORD	OPRNAM+202,OPRNAM+211					;-ON
	.WORD	OPRNAM+214,OPRNAM+224,OPRNAM+230			;-DATA
	.WORD	OPRNAM+235,OPRNAM+244,OPRNAM+252,OPRNAM+257		;-KEY
	.WORD	OPRNAM+263,OPRNAM+270,OPRNAM+274			;-COLOR
	.WORD	OPRNAM+302,OPRNAM+307,OPRNAM+316,OPRNAM+324		;-BLOAD
	.WORD	OPRNAM+332,OPRNAM+337,OPRNAM+345,OPRNAM+353,OPRNAM+360	;-SAVE
	.WORD	OPRNAM+365,OPRNAM+371,OPRNAM+376,OPRNAM+403,OPRNAM+411	;-LLIST
	.WORD	OPRNAM+417,OPRNAM+425,OPRNAM+434,OPRNAM+441		;-RUN
;	.WORD	OPRNAM+445,OPRNAM+453,OPRNAM+461,OPRNAM+467,OPRNAM+474
;
;	ТАБЛИЦА АДРЕСОВ ПРОГРАММ КОМПИЛЯЦИИ
;
	.GLOBL	END,FOR,GOSUB,GO,IF,INPUT,LET,NEXT,PIJ,CURON,CUROFF
	.GLOBL	PRINT,RET,STOP,DIM,PAINT,LINE,POKE,OUT,TRON,TROFF,DRAW
	.GLOBL	IODEV,STRREG,STRSIZ,FREBEG,FRELEN,KEY,CIRCLE
	.GLOBL	LOCATE,CLEAR,HIMEM,BEEP,CLS,COLOR,RWREN,PSET,PRESET,DEF
	.GLOBL	LOAD,SAVE,CLOAD,CSAVE,BLOAD,BSAVE,FIND
	.GLOBL	DATA,READ,RESTOR,DATBEG,DATPTR,ON,OPEN,CLOSE,REM     ;MOTOR
	.GLOBL	LPRINT,CALL,MSTART,MAG,HALT			;ERASE,SWAP
AOPSUB::.WORD	LET,REM,DIM,LINE,CIRCLE,DEF,TROFF,FOR,GOSUB
	.WORD	GO,IF,INPUT,LET,OPEN,PRINT,CLOSE
	.WORD	NEXT,PAINT,PRINT,DRAW
	.WORD	REM,RET,CALL,CALL,READ
	.WORD	POKE,OUT,STOP,LPRINT,ON
	.WORD	RESTOR,END,DATA,LOCATE
	.WORD	CLEAR,TRON,KEY,BEEP,CLS,COLOR,PSET,PRESET
;
;
;	ТАБЛИЦА АДРЕСОВ ПРОГРАММ НЕПОСРЕДСТВЕННОГО ВЫПОЛНЕНИЯ
;
AOPMON::
	.WORD	CLOAD,BLOAD,FIND,CSAVE,BSAVE,LOAD,SAVE
	.WORD	NEW,AUTO,LIST,ADDRMN,LLIST,RENUM,DELETE,CONT,RUN
;	.WORD	MOTOR,MERGE,ERASE,SWAP,MAXFILES
	;
	; ========================
	; ***БЛОК ИНИЦИАЛИЗАЦИИ***
	; ========================
	.GLOBL	TABTOP,LENT,TEXT,TXEND,ENDCOD,ERRS,ENDPRG,ADDRMN,STACK
	.GLOBL	ISV,ISVEIL,IVEIL,BUF,LYGIS,CIKL,NUMBER,ADRHLT,MAGNIT,IRPS
	.GLOBL	INK,START,FCB,CURLIN,AUTOON,AUINC,TRFLAG,TRIN,SEPAP,PNTSAV
	.GLOBL	ISVSS,LIMIT,RESTV,BREAK,COPOUT,ISVETO,TMP
	.GLOBL	BUFOUT,CLOUTF,CLOALL,NAMTYP,COPYIN,TABMOV,BOHPAP,PFACT,WRITE
;	.GLOBL	MAXFIL
	;
MONIT::	CLR	ADRHLT
	;
	; ======================================
	; ***blok wwoda direktiw polxzowatelq***
	; ======================================
	;
STAR::
AUTOCL:	CLR	AUTOON
	CLR	LYGIS		;ПРИЗНАК РАБОТЫ МОНИТОРА
	MOV	#OK,R1		;ВЫВОД OK
	MOV	#3,R2
	CALL	ISVEIL
	CALL	CURON
PRGLIN:	TST	AUTOON
	BNE	AUNEXT
	CALL	IVEIL		;ВВОД СТРОКИ
	;
	; --------------------------
	; ...АНАЛИЗ СТРОКИ...
	; --------------------------
	;
ANALIZ::MOV	#BUF,R3
	CMPB	@R3,#'.
	BNE	1$
	INC	R3
	JMP	EDIT
1$:	JSR	R1,ASCCOD
	BR	COMAND
	BR	PRGLIN
;
AUNEXT:	MOV	CURLIN,R5
	ADD	AUINC,R5
	BCS	AUTOCL
	JMP	EDAUIN
;
COMAND: CALL	CUROFF
	CMP	R5,#AOPMON-AOPSUB
	BLT	1$
	JMP	@AOPSUB(R5)
1$:	JMP	STATM
;
ASCCOD::MOV	#BUF,R3
	TRAP	110
	BR	10$		;БЕЗ НОМЕРА
	MOV	@PC,ERRS
	CLR	STRREG
	MOV	R5,CURLIN
	TRAP	111
	BR	5$
	CMPB	@R3,#12
	BEQ	7$
	MOV	TABTOP,R0
	MOV	R0,R2
	SUB	#6,R2
	CMP	R2,TXEND
	BLOS	MEMFUL
	MOV	R2,TABTOP
	MOV	R2,LENT
	MOV	R2,LIMIT
	MOV	R2,CIKL
17$:	MOV	(R0)+,(R2)+
	CMP	R0,R4
	BLOS	17$
	MOV	R5,@R4
5$:	MOV	R4,R5
10$:	TRAP	112
; ОПОЗНАНИЕ ОПЕРАТОРА
	CMPB	@R3,#12
	BNE	16$
	TST	R5
	BEQ	7$
	CALL	DEL
	BR	7$
16$:	MOV	R5,R2
	TRAP	113
	.WORD	OPRNAM
	.BYTE	2
	.BYTE	MONIT-AOPSUB-2
	CLR	R5
25$:	TST	R2
	BEQ	8$
    	MOV	TXEND,R0
	SUB	R3,@SP
	ADD	R0,@SP
	CMP	@SP,TABTOP
	BHIS	1$
	MOV	R0,-(R2)
	MOVB	R5,(R0)+
2$:	MOVB	(R3)+,(R0)
	CMPB	(R0)+,#12	;BUTINA PAGAL 12 DEL LOAD
	BNE	2$
	MOV	R0,TXEND
	INC	R0
	BIC	#1,R0
	MOV	R0,ENDCOD
7$:	TST	(R1)+
8$:	RTS	R1
;
1$:	MOV	R2,R4
	CALL	DEL
MEMFUL:	TRAP	7
;
DEL:	MOV	R4,R0
DELINT:	TST	(R4)+
	CMP	-(R0),-(R0)
20$:	CMP	R0,TABTOP
	BLOS	21$
	MOV	-(R0),-(R4)
	BR	20$
21$:	MOV	R4,TABTOP
	RETURN
;
CLRBUF::MOV	HIMEM,FCB
;	MOV	MAXFIL,R1
;	BEQ	2$
1$:	SUB	#422,FCB
	CLR	@FCB
;	SOB	R1,1$
2$:	SUB	#400,FCB
	RETURN
;
CLRTXT::CALL	CLRBUF
	MOV	FCB,TABTOP	;
	MOV	#TEXT,TXEND
	CLR	PFACT
	MOV	#BASVU,R1	;wywod na~alxnogo soob}eniq
	MOV	#42,R2
	CALL	ISVEIL
;
CLRCOD::MOV	TABTOP,LENT
	MOV	TABTOP,LIMIT
	MOV	TABTOP,CIKL
	MOV	TXEND,ENDCOD
	INC	ENDCOD
	BIC	#1,ENDCOD
	MOV	@PC,ERRS	;ЗАПРЕТИТЬ ВЫПОЛНЕНИЕ РАБОЧЕЙ ПРОГРАММ
	clr	strreg
	return
;
strmem:	mov	endcod,r1
	mov	r1,frebeg
	add	strsiz,r1
	mov	strsiz,frelen
	cmp	r1,lent
	bhi	memful
	mov	r1,endcod
	mov	frebeg,strreg
	return
;
new:	trap	106
	mov	#stack,sp
	call	cloall
	call	clrtxt
	call	isvss
	jmp	monit
;
llist:	mov	#-1,iodev
;
list::	mov	r5,-(sp) 
	call	larg		; pirmas argumentas - r4, antras - r0
;	clr	-(sp)
	inc	lygis
	cmp	r4,r0
	blo	ls66	;prie puslapiavimo ls6
ls1:	mov	r4,-(sp)
ls2:	mov	r0,-(sp)
   	call	curoff		
;	clr	-(sp)
ls3:	call	eilute
;	add	r2,@sp
;	cmp	@sp,#1340
;	bgt	ls7
ls4:	mov	#5000,r2
	mov	2(r4),curlin
   	call	isveto
;11$:	bit	#37,@sp 
;	beq	8$
;	inc	@sp
;	br	11$
8$:	cmp	-(r4),-(r4)
	cmp	r4,@sp	;2(sp) prie puslapiavimo
	bhis	ls3
;	tst	iodev
;	beq	ls8
	clr	iodev
ls6:	cmp	(sp)+,(sp)+	;tst prie  puslapiavimo
;	bne	ls6
ls66:	jmp	st0
;ls7:	tst	iodev
;	bmi	ls4
;	tst	(r4)+
;ls8:	mov	r4,@sp
;ls9:	call	curon
;	call	iveil
;	cmpb	buf,#12
;   	beq	3$
;	clr	lygis
;44$:	tst	(sp)+
;	bne	44$
;  	jmp	analiz
;3$:	cmp	(sp),2(sp)
;	blo	ls9
;	mov	(sp)+,r4
;	mov	(sp)+,r0
;	br	ls1
;listbc:	mov	6(sp),r4
;	beq	ls9
;	tst	(sp)+
;	mov	(sp)+,r0
;	tst	(sp)+
;	br	ls2
;
larg:	clr	-(sp)		;pirmas argumentas
	call	argum		;ПРИЗНАК НАЛИЧИЯ ПЕРВОГО АРГУМЕНТА
	BEQ	1$
	INC	(SP)
	TRAP	111
	BR	11$
	TST	4(SP)
	BEQ	111$
	NEG	(SP)
11$:	MOV	R4,-(SP)	; (SP)-PIRMOS EILUTES ADRESAS RYSIU LENT.
	CMPB	#'-,(R3)	;AR YRA MINUSAS ?
	BNE	4$
	INC	R3
	CALL	ARGUM		;ANTRAS ARGUMENTAS
	BEQ	3$
	TRAP	111
	BR	2$
	TST	6(SP)
	BEQ	111$
	TST	(R4)+		;ARGUMENTAS NURODYTAS KLAIDINGAI -
31$:	CMP	(R4)+,(R4)+	;PASTUMIAM RODYKLE RYSIU LENTELEJE
2$:	MOV	R4,R0
	MOV	(SP)+,R4
	TST	(SP)+
	MOV	(SP)+,(SP)
	TRAP	106
	RETURN
111$:	TRAP	8.
;
1$:	MOV	FCB,R4		;PIRMO ARGUMENTO NERA => JIS = PIRMOS EILUTES
	TST	-(R4)
	BR	11$
4$:	TST	2(SP)
	BGT	2$
3$:	MOV	TABTOP,R4
	BR	31$
;
ARGUM:	TRAP	112
	CMPB	#'.,(R3)
	BNE	2$
	MOV	CURLIN,R5
	INC	R3
	RETURN
2$:	TRAP	110
	CLR	R4
	RETURN
;
EILUTE::MOV	@R4,R5
	CALL	IUNPCK
	MOV	R5,R3
	MOV	-(R4),R5
	MOVB	(R5)+,R1
	BEQ	1$
	MOV	AOPNAM(R1),R1
	MOV	#40,R0
	CALL	INBUF
1$:	MOV	R5,R1
	MOV	#12,R0
	CALL	INBUF
	MOV	#BUF,R1
	RETURN
;
STATM:	MOV	#400,LYGIS
	TST	STRREG
	BNE	5$
	CALL	STRMEM
5$:	MOV	ENDCOD,R1
	CLR	TMP
	MOV	#-1,NUMBER
	CALL	@AOPSUB(R5)
ASTAR::	MOV	#STAR,(R1)+
	MOV	ENDCOD,R4
	CMP	R1,CIKL
	BHIS	MEMORY
	NEG	LYGIS
	JMP	VYK1
;
RUN::	MOV	FCB,R2
	TRAP	110
	MOV	-(R2),R5
	TRAP	106
	CALL	CLOALL
LRUN::	CMP	TABTOP,FCB
	BEQ	KL1
	TRAP	111
	BR	1$
	TRAP	8.
1$:	JSR	R0,SETST
	BR	4$
	BR	KL1
4$:   	CALL	CURON
	MOV	#DATBEG,DATPTR
	CLR	AUTOON
	CLR	DATBEG
2$:	INC	ERRS
	BEQ	2$
	INC	LYGIS
	MOV	R4,ADRHLT
	MOV	TABTOP,CIKL
	MOV	TABTOP,LIMIT
	MOV	TABTOP,LENT
	CLR	STRREG
	CALL	GRBCOL
	MOV	TABTOP,R0
3$:	CLR	(R0)+
	CMP	(R0)+,(R0)+
	CMP	R0,FCB
	BLO	3$
	CLR	PFACT
	MOV	#STACK,SP
COMPIL:	CMP	R0,TABTOP
	BLOS	ENDTXT
	MOV	-(R0),NUMBER
	MOV	-(R0),R3
	TST	-(R0)
	MOV	R0,R5
1$:	MOV	@R5,R2
	MOV	R1,@R5
	MOV	R2,R5
	BNE	1$
	MOVB	(R3)+,R2
	CLR	TMP
	CMP	R2,#AOPMON-AOPSUB
	BLT	2$
	TRAP	12.
2$:	CALL	@AOPSUB(R2)
	CMP	R1,LENT
	BLOS	COMPIL
MEMORY:	CLR	LYGIS
2$:	CMP	R1,TABTOP
	BLOS	TXTSV
	SUB	#6,TABTOP
	BR	2$
KL1:	JMP	STAR
SET1:	CMPB	@-(4),#50	;REM
	BEQ	1$
	CMPB	@0(4),#2	;'
	BEQ	1$
	CMPB	@0(4),#100	;DATA
	BNE	SET2
1$:	CMP	-(4),-(4)
SETST::	CMP	R4,TABTOP
	BHIS	SET1
	TST	(0)+
SET2:	TST	-(4)
	RTS	R0
TXTSV:	TRAP	7
ENDTXT:	CMP	-2(R1),#ENDPRG
	BEQ	2$
	CMP	-2(R1),#HALT
	BEQ	2$
	MOV	#ENDPRG,(R1)+
2$:	MOV	DATBEG,R0
	BEQ	11$
	MOV	R1,DATBEG
1$:	MOV	2(0),R3
	INC	R3
	MOV	@R0,R2
	MOV	R1,@R0
13$:	CMP	LENT,R1
	BLOS	MEMORY
	CMPB	@R3,#12
	BEQ	3$
	MOVB	(3)+,(1)+
	BR	13$
3$:	CMP	R0,DATPTR
	BEQ	4$
	MOVB	#54,(1)+
	MOV	R2,R0
	BR	1$
4$:	MOVB	@R3,(1)+
11$:	MOV	DATBEG,DATPTR
	MOV	AUTOON,R0
	BEQ	10$
7$:	MOV	@R0,R4
5$:	CMPB	@(4),#100	;DATA
	BEQ	6$
	SUB	#6,R4
	CMP	R4,TABTOP
	BHI	5$
	CLR	@R0
	BR	8$
6$:	MOV	-(4),@R0
8$:	MOV	-(0),R4
	MOV	#RESTV,@R0
	MOV	R4,R0
	BNE	7$
10$:	MOV	R1,ENDCOD
	CALL	STRMEM
	MOV	@ADRHLT,R4
	DEC	LYGIS
VYK2:	CLR	ADRHLT
	CLR	ERRS
	CLR	AUTOON
	CLR	NUMBER
	TST	TRFLAG
	BEQ	VYKDYT
	JMP	TRIN
VYKDYT::MOV	#-1,LYGIS
VYK1:	CALL	CUROFF
	CLR	INK
	JMP	@(R4)+
;
PRGFUL:	TRAP	17.
;
CONT:	TST	ERRS
	BNE	PRGFUL
	MOV	ADRHLT,R4
	BEQ	KL1
	BR	VYK2
;
;MERGE:	MOV	#-1,-(SP)
;	BR	L0
;
LOAD:	CMP	TABTOP,FCB
	BEQ	1$
	TRAP	5
1$:	CLR	-(SP)
L0:	MOV	#MAG,R0
	MOV	#3,(R0)+
	MOV	FCB,(0)+
	MOV	#400,-(SP)
	MOV	@SP,(0)+
	CALL	FILNAM
	CALL	ASC
;	TST	2(SP)
;	BMI	11$
	CMPB	@R3,#',
	BNE	11$
	INC	R3
	TRAP	101
	.WORD	RDRM
	TRAP	2
	INC	2(SP)
11$:	TRAP	106
	CALL	CAS
	CALL	ISVARD
	MOV	FCB,-(SP)
4$:	MOV	(SP)+,R3
	MOV	SP,R0
	MOV	#MAG+26,R5
	CALL	COPYIN
	CMP	R1,#BUF
	BEQ	L6
	MOV	R3,-(SP)
	JSR	R1,ASCCOD
	BR	7$
	BR	4$
7$:	MOV	#12,R0
	MOVB	R0,#BUF
	CALL	ISV
	TRAP	57.
L6:	TST	(SP)+
L1:	CALL	LASTBL
L2:	CALL	CAS
	CALL	TESTR
	JMP	ST0
;
FIND:	MOV	#MAGNIT,WRITE
	MOV	#MAG,R0
	MOV	#4,(R0)+
	CMP	(R0)+,(R0)+
	CLR	@R0
	CLR	-(SP)
	CMP	-(SP),-(SP)
	MOV	SP,R1
	TRAP	120
	NOP
	TRAP	106
	TST	(SP)+
	MOV	2(SP),R1
	BEQ	L6
	MOV	(SP)+,@SP
	MOV	R1,-(SP)
	CALL	NAMTYP
	MOV	#MAG+13.,R3
	TRAP	113
	.WORD	FILTYP
	.BYTE	3,2
	BR	1$
	DEC	R3
	MOV	R3,R0
	CALL	@BINCOD(R5)
	CLR	-(SP)
	BR	L2
1$:	CLR	-(SP)
	BR	L1
;
BLOAD:	MOV	#MAG,R0
	MOV	#3,(0)+
	CMP	(0)+,(0)+
	CALL	FILNAM
	CALL	BIN
	SUB	#24,R0
	CLR	-(SP)
	CLR	R5
	CMPB	@R3,#',
	BNE	2$
	INC	R3
	TRAP	101
	.WORD	RDRM
	BR	1$
	INC	@SP
	TRAP	112
	CMPB	@R3,#',
	BNE	2$
	INC	R3
1$:	CALL	SAVPAP
2$:	MOV	R5,(0)+
	TRAP	106
	CALL	CAS
	CALL	ISVARD
	TST	(SP)+
	BEQ	ST
	JMP	@MSTART
;
SAVPAP:	TRAP	112
	CMPB	(3)+,#'&
	BNE	11$
	CALL	BOHPAP
	RETURN
11$:	DEC	R3
	TRAP	110
	TRAP	2
	RETURN
;
BSAVE:	MOV	#MAG,R0
	MOV	#2,(0)+
	CMP	(0)+,(0)+
	CALL	FILNAM
	CALL	BIN
	SUB	#24,R0
	TRAP	126
	CALL	SAVPAP
	MOV	R5,(0)+
	MOV	R5,R2
	TRAP	126
	CALL	SAVPAP
	SUB	R2,R5
	BLO	CL0
	MOV	R5,(0)+
	TRAP	106
	CALL	CAS
	BR	ST
;
CL0:	TRAP	5
;
CLOAD:	CMP	TABTOP,FCB
	BNE	CL0
	MOV	TABTOP,CIKL
	MOV	TABTOP,LIMIT
	MOV	TABTOP,LENT
	CLR	STRREG
	MOV	#MAG,R0
	MOV	#3,(R0)+
	MOV	#TEXT,(R0)+
	MOV	FCB,@R0
	SUB	#TEXT,(R0)+
	CALL	FILNAM
	CLR	-(SP)
	CMPB	@R3,#',
	BNE	CL1
	INC	R3
	TRAP	101
	.WORD	RDRM
	TRAP	2
	INC	@SP
CL1:	CALL	COD
	TRAP	106
	CALL	CAS
	MOV	#MAG+26,R3
	MOV	(R3)+,R2
	MOV	@R3,R0
	MOV	#TEXT,R3
	ADD	R3,R0
	SUB	R2,R3
	MOV	FCB,R2
	MOV	R2,R4
	SUB	-(R0),R4
5$:	MOV	-(R0),-(R2)
	MOV	-(R0),-(R2)
	ADD	R3,@R2
	MOV	-(R0),-(R2)
	CMP	R2,R4
	BHI	5$
	MOV	R0,ENDCOD
	MOV	R0,TXEND
	MOV	R2,LENT
	MOV	R2,LIMIT
	MOV	R2,CIKL
	MOV	R4,TABTOP
	CALL	TESTR
ST:	JMP	STAR
;
TESTR:	CALL	ISVARD
	MOV	(SP)+,R5
	TST	(SP)+
	BLE	1$
	MOV	FCB,R4
	MOV	-(R4),R5
	JMP	LRUN
1$:	MOV	R5,PC
;
CSAVE:	CMP	TABTOP,FCB
	BEQ	ST
	MOV	#MAG,R0
	MOV	#2,(R0)+
	MOV	#TEXT,(R0)+
	MOV	R3,-(SP)
	CALL	GRBCOL
	MOV	R1,R5
	MOV	TABTOP,R4
	MOV	R5,@R0
	SUB	#TEXT-2,(R0)+
	MOV	(SP)+,R3
	CALL	FILNAM
	CALL	COD
	TRAP	106
	MOV	@PC,ERRS
7$:	MOV	(R4)+,(R5)+
	CMP	R4,FCB
	BLO	7$
	MOV	R4,@R5
	SUB	TABTOP,(R5)
	MOV	#MAG,R1
	ADD	(R5)+,4(R1)
	CALL	@WRITE
	CALL	ATSTAB
	CALL	CASERR
	BR	ST
SAVE:	CMP	TABTOP,FCB
	BEQ	ST
	MOV	#MAG,R0
	MOV	#2,(R0)+
	MOV	FCB,R4
	MOV	R4,(R0)+
	MOV	#400,(R0)+
	CLR	-(SP)
	CALL	FILNAM
	CALL	ASC
	TRAP	106
	MOV	R4,-(SP)
	TST	-(R4)
1$:	CALL	EILUTE
	CMP	-(R4),-(R4)
	MOV	(SP)+,R3
	MOV	SP,R0
	MOV	#MAG+26,R5
	CALL	COPOUT
	MOV	R3,-(SP)
	CMP	R4,TABTOP
	BHI	1$
	CALL	CLOUTF
	CMP	(SP)+,(SP)+
ST0:	MOV	#12,BUF
	BR	ST
;
FILTYP:	.ASCII	/COD BIN /
RDRM:	.BYTE	'R,162,0
TT:	.BYTE	'T,164,0
BINCOD:	.WORD	CODEND,BINEND
;
ISVARD::TSTB	LYGIS
	BNE	1$
	MOV	#MAG+32,R1
	MOV	#20,R2
	CALL	ISVEIL
	MOV	#12,R0
	CALL	ISV
1$:	RETURN
;
FILNAM:
	CMPB	(R3)+,#'"
	BNE	7$
	CALL	TTANAL
	MOV	#6,R2
1$:	CMPB	@R3,#'"
	BEQ	2$
	CMPB	@R3,#12
	BEQ	2$
	MOVB	(R3)+,(R0)+
	SOB	R2,1$
2$:	CMP	R2,#6
	BNE	4$
	TRAP	56.
3$:	MOVB	#40,(R0)+
4$:	DEC	R2
	BGE	3$
5$:	CMPB	@R3,#12
	BEQ	6$
	CMPB	(R3)+,#'"
	BNE	5$
6$:	TRAP	112
	RETURN
7$:	TRAP	13.
;
TTANAL::MOV	#MAGNIT,WRITE
	MOV	R4,-(SP)
	MOV	R3,-(SP)
	TRAP	101
	.WORD	TT
	BR	12$
	TRAP	101
	.WORD	TT
	BR	12$
	TRAP	112
	CMPB	@R3,#':
	BNE	12$
	INC	R3
	TRAP	112
	ADD	(SP)+,R2
	SUB	R3,R2
	MOV	#IRPS,WRITE
	BR	13$
12$:	MOV	(SP)+,R3
13$:	MOV	(SP)+,R4
	RETURN
;
BIN:	MOV	#".B,(R0)+
	MOV	#"IN,(R0)+
BINEND:	MOV	#20040,(R0)+
	MOV	#20040,(R0)+
	MOV	#20040,(R0)+
	RTS	PC
;
COD:	MOV	#".C,(R0)+
	MOV	#"OD,(R0)+
CODEND:	CLR	(R0)+
	CLR	(R0)+
	CLR	(R0)+
	RTS	PC
;
ASC:	MOV	#".A,(R0)+
	MOV	#"SC,(R0)+
BLOCK0::MOV	#" #,(R0)+
	MOV	#"00,(R0)+
	MOV	#15060,(R0)+
	RETURN
;
DAT::	MOV	#".D,(R0)+
	MOV	#"AT,(R0)+
	BR	BLOCK0
;
LASTBL::MOV	#MAG+21,R1
	MOVB	#40,(R1)+
	CLR	(R1)+
	CLR	(R1)+
	RETURN
;
ATSTAB:	CMP	R5,TABTOP
	BLO	2$
	MOV	R5,R4
	SUB	FCB,R4
	ADD	R5,R4
1$:	MOV	-(R4),-(R5)
	CMP	R5,TABTOP
	BHI	1$	
2$:	RETURN
;
CASCII::TST	LYGIS
	BEQ	2$
	MOV	#MAG,R1
	MOV	(0)+,(1)+
	MOV	R0,@R1
	ADD	#20,(1)+
	MOV	#400,(1)+
	MOV	#10,R5
1$:	MOV	(0)+,(1)+
	SOB	R5,1$
	MOV	R0,R5
	SUB	#22,R0
2$:	RETURN
CAS::	MOV	#MAG,R1
	CALL	@WRITE
CASERR:	MOVB	1(R1),R1
	BEQ	2$
	DEC	R1
	BEQ	1$
	DEC	R1
	BEQ	3$
	JMP	BREAK
1$:	CALL	ISVARD
	BR	CAS
2$:	RETURN
3$:	MOV	#12,R0
	CALL	ISV
	TRAP	19.
;
BLOCKN::MOV	#3,R1
	DEC	R5
6$:	CMPB	-(R5),#'9
	BNE	7$
	MOVB	#'0,@R5
	SOB	R1,6$
	TST	(R5)+
7$:	INCB	@R5
	RETURN
;
BC::	ADD	#16,SP
;	TST	LYGIS
;	BGT	1$
	CLR	R2
	MOV	#BUF,R3
	MOV	R3,R1
	BR	E4
;1$:	JMP	LISTBC
;
EDAUIN:	TRAP	111
	BR	E6
	CALL	IUNPCK
	CALL	ISVEIL
	MOV	R1,R2
	SUB	#BUF,R2
	MOV	R1,R3
	BR	EDEND
;
EDIT:	CLR	R2
	MOV	CURLIN,R0
	TRAP	110
	MOV	R0,R5
E0:	TRAP	106
	TRAP	111
	BR	E6
	TRAP	8.
E6:	CALL	IUNPCK
	MOV	-(R4),R4
	MOV	R5,R3
	MOVB	(R4)+,R1
	BEQ	1$
	MOV	AOPNAM(R1),R1
	MOV	#40,R0
	CALL	INBUF
1$:	MOV	R4,R1
E4:	MOV	#12,R0
	CALL	INBUF
	MOV	#BUF,R1
	DEC	R3
	DEC	R2
	BEQ	5$
	MOV	R2,R0
	CALL	ISVEIL
	MOV	R0,R2
5$:	MOV	R3,R1
EDEND:	MOV	#ANALIZ,-(SP)
	MOV	R0,-(SP)
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R5,-(SP)
	CLR	-(SP)
	MOV	#420,-(SP)
	SUB	@SP,R2
	NEG	R2
	JMP	PIJ
;
AUTO:	CALL	CURON
	CLR	R2
	CMPB	#',,@R3
	BEQ	2$
	MOV	#12,R2
	CALL	ARGUM
	BEQ	2$
	MOV	R5,R2
2$:	TRAP	112
	MOV	#12,R5
	CMPB	#',,@R3
	BNE	4$
3$:	MOV	AUINC,R0
	INC	R3
	TRAP	110
	MOV	R0,R5
	TST	R5
	BEQ	ILCALL
4$:	TRAP	106
	MOV	@PC,AUTOON
	MOV	R2,CURLIN
	MOV	R5,AUINC
	MOV	R2,R5
	BR	EDAUIN
;
INBUF:	MOVB	(1),(3)+
	INC	R2
	CMPB	(1)+,R0
	BNE	INBUF
	RETURN
;
DELETE:	CLR	-(SP)
	CALL	LARG
	MOV	@PC,ERRS
	CALL	DELINT
	JMP	STAR
;
ISGUNP::MOV	#BUFOUT,R3
	MOVB	#40,(R3)+
	TST	R5
	BPL	1$
	MOVB	#'-,-1(R3)
	NEG	R5
1$:	CALL	I1
	DEC	R1
	INC	R2
	RETURN
;
IUNPCK::MOV	#BUF,R3
I1::	MOV	R3,-(SP)
	ADD	#5,R3
	MOVB	#40,(R3)
1$:	MOV	#5,R2
	CLR	R1
2$:	ASL	R2
	BCS	4$
	CMP	R2,R5
	BLOS	2$
3$:	CMP	R2,#12
	BEQ	5$
	CLC
4$:	ROR	R2
	ASL	R1
	CMP	R2,R5
	BHI	3$
	SUB	R2,R5
	INC	R1
	BR	3$
5$:	ADD	#60,R5
	MOVB	R5,-(R3)
	MOV	R1,R5
	BNE	1$
	MOV	@SP,R5
	MOV	R5,R1
	CLR	R2
	ADD	#6,@SP
6$:	MOVB	(R3)+,(R5)+
	INC	R2
	CMP	R3,@SP
	BNE	6$
	TST	(SP)+
	RETURN
;
GRBCOL::CLR	R4
	MOV	#TEXT,R1
1$:	MOV	#-1,R3
	MOV	FCB,R2
	CMP	-(R2),-(R2)
2$:	CMP	@R2,R4
	BLOS	3$
	MOV	@R2,R3
	MOV	R2,R5
3$:	CMP	-(R2),-(R2)
	CMP	R2,TABTOP
	BLO	4$
	CMP	-(R2),R3
	BLO	2$
	BR	3$
4$:	CMP	R3,#-1
	BEQ	6$
	MOV	R1,@R5
	MOV	R3,R4
	MOVB	(R3)+,(R1)+
5$:	MOVB	(R3)+,@R1
	CMPB	(R1)+,#12
	BNE	5$
	BR	1$
6$:	MOV	R1,TXEND
	INC	R1
	BIC	#1,R1
	MOV	R1,ENDCOD
	RETURN
;
ILCALL:	TRAP	5
;
RENUM:	INC	ERRS
	MOV	R3,R0
	CALL	GRBCOL
	MOV	R0,R3
	CLR	R2
	MOV	#12,R0
	MOV	#12,-(SP)
	CMPB	#',,@R3
	BEQ	1$
	CALL	ARGUM
	BEQ	3$
	MOV	R5,@SP
	TRAP	112
	CMPB	#',,@R3
	BNE	3$
1$:	INC	R3		;2 ARG. TIKRINIMAS
	TRAP	112
	CMPB	#',,@R3
	BEQ	5$		;2 ARG. PRALEISTAS
	CALL	ARGUM
	BEQ	3$
	MOV	R5,R2
	TRAP	112
	CMPB	#',,@R3
	BNE	3$
5$:	INC	R3
	TRAP	110
	BR	3$
	MOV	R5,R0
	BEQ	ILCALL
3$:	TRAP	106
;	ARGUMENTAI ISANALIZUOTI
;	@SP /NEW/,  R2/OLD/,  R0/INC/
	MOV	R2,R5
	TRAP	111
	BR	8$
	CMP	R4,TABTOP
	BHIS	8$
	JMP	STAR
8$:	MOV	R4,R2
	MOV	R4,R1
	TST	-(R1)
9$:	MOV	@SP,-(R1)
	MOV	@R1,R5
	TRAP	111
	BR	13$
	BR	14$
13$:	CMP	R4,R2
	BHI	ILCALL
14$:	CMP	R1,TABTOP
	BLOS	10$
	ADD	R0,@SP
	BCS	ILCALL
	CMP	-(R1),-(R1)
	BR	9$
10$:	TST	(SP)+
38$:	CMP	R1,FCB
	BEQ	30$		;TEKSTO ANALIZE BAIGTA
	TST	(R1)+
	MOV	@R1,R0
	JSR	R5,KODTST
	BR	12$
	BR	24$
15$:	CMP	(R1)+,(R1)+
	BR	38$
12$:	INC	R0
	MOV	R0,R3
	TRAP	110
	BR	15$
	CLR	-(SP)
	CALL	RENPAP
	BR	23$
	BR	23$
24$:	CLR	-(SP)
	MOV	R0,R3
20$:	CMPB	(3)+,#12
	BEQ	23$
22$:	JSR	R2,IFGAUD
	BR	23$
	BR	20$
29$:	MOV	R3,R0
	TRAP	110
	BR	22$
	CALL	RENPAP
	BR	28$
28$:	TRAP	112
	CMPB	#',,@R3
	BNE	22$
	INC	R3
	BR	29$
23$:	CLR	(SP)+
	BR	15$
30$:	TST	(R2)+
31$:	MOV	R2,R0		;SIUNCIAM NEW NR. OF LINE
	CMP	-(R2),-(R2)
	MOV	-(R2),-(R0)
	CMP	R2,TABTOP
	BNE	31$
;	SUTVARKOM RL MONOTONISKUMA
;	R0/@MAX EIL NR
	CLR	R3
36$:	MOV	R0,R4
33$:	ADD	#6,R4
	CMP	R4,FCB
	BHI	34$		;BAIGTA
35$:	CMP	(R0)+,(R4)+
	BLT	32$
	CMP	R4,FCB
	BEQ	34$
	CMP	-(R0),-(R4)
	MOV	R4,R0
	BR	33$
32$:	TST	R3
	BNE	37$
	MOV	R4,R3
37$:	MOV	-(R4),-(SP)
	MOV	-(R4),-(SP)
	MOV	-(R4),-(SP)
	SUB	#6,R0
	MOV	(R0)+,(R4)+
	MOV	(R0)+,(R4)+
	MOV	(R0)+,(R4)+
	MOV	R0,R4
	SUB	#6,R0
	MOV	(SP)+,(R0)+
	MOV	(SP)+,(R0)+
	MOV	(SP)+,(R0)+
	SUB	#10,R0
	TST	-(R4)
	CMP	R0,TABTOP
	BHI	35$
	MOV	R3,R0
	TST	-(R0)
	CLR	R3
	BR	36$
34$:	JMP	STAR
RENPAP:	MOV	R2,-(SP)
	MOV	R1,-(SP)
	MOV	R3,-(SP)
	TRAP	112
	SUB	R0,@SP
	INC	@SP		;R3/SK. ILGIS +1/
	TRAP	111
	BR	3$
	CMP	R1,R2
	BHI	1$
	MOV	-2(R1),NUMBER
	BR	2$
1$:	MOV	2(R1),NUMBER
2$:	MOV	#200,LYGIS
	TRAP	10
	BR	7$
3$:	CMP	R4,R2
	BHI	7$
	MOV	-4(R4),R5
	CALL	IUNPCK
	SUB	R2,@SP
	BLT	8$
4$:	DEC	R2
	BEQ	5$		;EIL. NR. SUVESTI
	MOVB	(R1)+,(R0)+
	BR	4$
5$:	DEC	@SP
	BLT	6$
	MOVB	#40,(R0)+
	BR	5$
6$:	MOV	R0,@SP
7$:	MOV	(SP)+,R3
	MOV	(SP)+,R1
	MOV	(SP)+,R2
	CLR	LYGIS
	RTS	PC
8$:	TST	10(SP)
	BNE	13$
	MOV	R0,R4
9$:	CMPB	(R4)+,#12
	BNE	9$
	SUB	@2(SP),R4
	SUB	@SP,R4
	ADD	TXEND,R4
	CMP	TABTOP,R4
	BHIS	15$		;OK
	SUB	TXEND,R4		;NE OK
	MOV	R4,-(SP)
	CALL	GRBCOL
	MOV	(SP)+,R4
	ADD	TXEND,R4
	CMP	TABTOP,R4
	BHI	15$		;GERAI
10$:	CMP	2(SP),4(SP)
	BHI	11$
	MOV	-2(R1),NUMBER
	BR	12$
11$:	MOV	2(R1),NUMBER
12$:	MOV	#200,LYGIS
	TRAP	7
	ADD	#2,6(SP)
	BR	7$
13$:	MOV	@SP,R2
	NEG	R2
	ADD	TXEND,R2
	CMP	TABTOP,R2
	BLO	10$		;NETILPS
	MOV	TXEND,R4
14$:	MOVB	-(R4),-(R2)
	CMP	R4,R0
	BNE	14$
	MOV	R0,R1
	SUB	@SP,R1
	BR	17$
15$:	MOV	@2(SP),R1
	MOV	TXEND,R4
	MOV	R4,@2(SP)
16$:	MOVB	(R1)+,(R4)+
	CMP	R1,R0
	BNE	16$
17$:	MOV	#BUF,R2
18$:	CMPB	@R2,#40
	BEQ	19$
	MOVB	(R2)+,(R4)+
	INC	R1
	BR	18$
19$:	ADD	@SP,R1
	MOV	R4,@SP
20$:	MOVB	@R1,(R4)+
	CMPB	(R1)+,#12
	BNE	20$
	MOV	R4,TXEND
	INC	10(SP)
	BR	7$
;
KODTST::CMPB	#20,@R0	;GOSUB
	BEQ	12$
	CMPB	#22,@R0	;GOTO
	BEQ	12$
	CMPB	#52,@R0	;RETURN
	BEQ	12$
	CMPB	#74,@R0	;RESTORE
	BEQ	12$
	TST	(5)+
	CMPB	#24,@R0	;IF
	BEQ	12$
	CMPB	#72,@R0	;ON
	BEQ	12$
	TST	(5)+
12$:	RTS	R5
;
IFGAUD::TRAP	102
	BR	25$
	BR	21$		;RAIDE
	BR	20$
25$:	CMPB	@R3,#47
	BEQ	23$
	CMPB	@R3,#42
	BNE	20$
26$:	INC	R3
	CMPB	@R3,#12
	BEQ	23$
	CMPB	@R3,#42
	BNE	26$
	BR	20$
21$:	TRAP	113
	.WORD	RWREN
	.BYTE	2
	.BYTE	16
	BR	20$
	TST	R5
	BEQ	23$
	TST	(2)+
20$:	TST	(2)+
23$:	RTS	R2
;
;MAXFL:	CMPB	(R3)+,#'=
;	BNE	1$
;	TRAP	110
;1$:	TRAP	2
;	TRAP	106
;	CMP	R5,#1
;	BHI	2$
;	MOV	R5,-(SP)
;	CALL	CLOALL
;	CLR	R2
;	MOV	#422,R4
;	MOV	MAXFIL,R1
;	SUB	@SP,R1
;	BEQ	3$
;	BPL	4$
;	NEG	R4
;	NEG	R1
;4$:	ADD	R4,R2
;	SOB	R1,4$
;	CALL	TABMOV
;	MOV	(SP)+,MAXFIL
;	CALL	CLRBUF
;3$:	CALL	CLRCOD
;	JMP	STAR
;2$:	TRAP	5
;
	.END
