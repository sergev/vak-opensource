	.TITLE	INOUT
	.ENABLE	LC
	;
	; ###################
	; #                 #
	; #   BASIC - VVU   #
	; #    BK - 0010    #
	; #                 #
	; ###################
	;
	; ИСПОЛНИТЕЛЬ: ВЦКП ВГУ
	; ВИЛЬНЮС,1985
	;
	; ======================
	; ****БЛОК НАСТРОИКИ****
	; ======================
	;
	BK0010=0
	EL60=1
	NC80=2
	SM4=3	;RAFOS
	MDS=4	;MDS RAFOS
	RAM=10000
	ROM=120000
	MVIDEO=40000
	NORM=0
	KLAS=1
;
	MASINA=BK0010
	MEMTYP=ROM
	KONTYP=NORM
;
	; ===========================
	; ****СИСТЕМНЫЕ КОНСТАНТЫ****
	; ===========================
	;
	BREAKW==4	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ПО КЛАВИШЕ "СТОП"
	ilcodw=10
	trapw=34	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ПО ИНСТРУКЦИИ TRAP
	TIMER==100	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ТАЙМЕРА
	$drwx=176	;КООРДИНАТА x
	$drwy=200	;КООРДИНАТА y
	tstkey=105	;АДРЕС ПРИЗНАКА НАЖАТИЯ НА КЛАВИШУ
	mstart==264	;СТАРТОВЫЙ АДРЕС МАССИВА
	mlen=266	;ДЛИНА МАССИВА
	mag==320	;АДРЕС БЛОКА ПАРАМЕТРОВ МАГНИТОФОНА
	irpsw=360	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ИРПС
	irpsrg=176560	;АДРЕС РЕГИСТРА ИРПС
	ausrom=160000	;АДРЕС ПЕРЕХОДА НА ПОЛЬЗОВАТЕЛЬСКОЕ БИС ПЗУ
	arport=177714	;АДРЕС РЕГИСТРА ПРОГРАММИРУЕМОГО ПОРТА
	ASPORT=177716	;adres registra sistemnogo porta
	;
	; ============================
	; ****СИСТЕМНЫЕ ПЕРЕМЕННЫЕ****
	; ============================
	;
	.ASECT
	.=2000
;
STACK::
LYGIS::	.BLKW	1	;РЕЖИМ РАБОТЫ СИСТЕМЫ
TXEND::	.BLKW	1	;МЕТКА КОНЦА ТЕКСТА И НАЧАЛА ОБ'ЕКТНОГО КОДА
STRREG::.BLKW	1	;МЕТКА НАЧАЛА ОБЛАСТИ СТРОК
STRSIZ::.BLKW	1	;УКАЗАТЕЛЬ ДЛИНЫ ОБЛАСТИ СТРОК
FREBEG::.BLKW	1	;МЕТКА СВОБОДНОЙ ЧАСТИ ОБЛАСТИ СТРОК
FRELEN::.BLKW	1	;ТЕКУЩАЯ ДЛИНА СВОБОДНОЙ ЧАСТИ ОБЛАСТИ СТРОК
ENDCOD::.BLKW	1	;МЕТКА НАЧАЛА СВОБОДНОЙ ОБЛАСТИ
			;И КОНЦА ОБ'ЕКТНОГО КОДА
CIKL::	.BLKW	1	;metka na~ala steka ciklow
LIMIT::	.BLKW	1	;ukazatelx wer{iny steka podprogramm
LENT::	.BLKW	1	;МЕТКА НАЧАЛА ТАБЛИЦЫ ИМЕН
TABTOP::.BLKW	1	;МЕТКА КОНЦА ТАБЛИЦ СВЯЗИ И ИМЕН
FCB::	.BLKW	1	;МЕТКА НАЧАЛА ОБЛАСТИ ФАЙЛОВ И ТАБЛИЦЫ СВЯЗИ
HIMEM::	.BLKW	1	;УКАЗАТЕЛЬ ВЕРХНЕГО АДРЕСА, ДОСТУПНОГО БЕЙСИКУ
ERRS::	.BLKW	1	;ПРИЗНАК ЗАПРЕТА ВЫПОЛНЕНИЯ ПРОГРАММЫ
TYPE::	.BLKW	1	;ТИП КОМПИЛИРУЕМОГО ВЫРАЖЕНИЯ
DATBEG::.BLKW	1	;ukazatelx na~ala bufera DATA
DATPTR::.BLKW	1	;teku}ij ukazatelx bufera DATA
DATINP::.BLKW	1	;flag READ/INPUT
NUMBER::.BLKW	1	;НОМЕР КОМПИЛИРУЕМОЙ СТРОКИ
ADRHLT::.BLKW	1	;АДРЕС ОСТАНОВА ПРОГРАММЫ
TRFLAG::.BLKW	1	;ФЛАГ РЕЖИМА ТРАССИРОВКИ
AUTOON::.BLKW	1	;ФЛАГ РЕЖИМА AUTO
AUINC::	.BLKW	1	;AUTO ПРИРАЩЕНИЕ
CURLIN::.BLKW	1	;УКАЗАТЕЛЬ ТЕКУЩЕЙ СТРОКИ
III::	.BLKW	4	;РАБОЧИЕ ЯЧЕЙКИ ФУНКЦИИ RND
RNDSAV::.BLKW	4	;ТЕКУЩЕЕ ЗНАЧЕНИЕ RND
;MAXFIL::.BLKW	1	;maksimalxnoe koli~estwo fajlow dannyh
USRTAB::.BLKW	10.	;ТАБЛИЦА АДРЕССОВ USR
;APRL::	.BLKW	15	;tablica neqwnogo opredeleniq tipow
PNTSAV::.BLKW	1	;rabo~aq q~ejka {aga
PFACT::	.BLKW	2	;ukazatelx bloka parametrow dlq FN
TMP::	.BLKW	1	;ukazatelx wremennyh deskriptorow
SAVJMP::.BLKW	1	;adres koda wypolnqemoj programmy
LPTPOS::.BLKB	1	;s~et~ik golowki printera
	.EVEN
;
	.IF	EQ,MASINA-BK0010
FNKSTR::.BLKB	170.	;БУФЕР ТЕКСТОВ ФУНКЦИОНАЛЬНЫХ КЛАВИШ
$SCRT:	.BLKW	1	;rabo~aq peremennaq DRAW
	.IF	EQ,KONTYP-KLAS
COPPNT:	.BLKW	1	;ukazatelx ozu |krana dlq COPY
EKRHUK:	.BLKW	1	;adres dopoln. podpr.
	.ENDC
;
	.IFF
TABX::	.BLKB	1		;НО. ПОЗИЦИИ КУРСОРА
TABY::	.BLKB	1		;НО. СТРОКИ КУРСОРА
SIIKP::	.BLKB	1		;ПРИЗНАК ЗАПИСИ КОДА ИЗ КЛАВИАТУРЫ
RLPOZ::	.BLKB	1		;ПРИЗНАК РУС. ЛАТ.
SAUG::	.BLKW	4		;ХРАНЕНИЕ ВЕКТОРА ПРЕРЫВАНИЯ КЛАВИАТУРЫ
				;И КОМАНД ПЕРЕЗАПУСКА СИСТЕМЫ
	.IFTF
WRITE::	.BLKW	1	;adres drajwera ustrojstwa wne{nej pamqti
IODEV:: .BLKW	1	;НОМЕР ТЕКУЩЕГО ФАЙЛА
INK::	.BLKW	1	;БУФЕР INKEY
BUF::	.BLKB	400	;БУФЕР ВВОДА
BUFOUT::.BLKB	30	;БУФЕР ВЫВОДА
TEXT::	.BLKW	1	;ОБЛАСТЬ ПРОГРАММЫ ПОЛЗОВАТЕЛЯ
	;
	; ====================
	; ****ПУСК СИСТЕМЫ****
	; ====================
	;
	.GLOBL	MONIT,TRAP,CLRTXT,BC,CASCII,BLOCKN,FNDBUF,NEXDBL,NEXJMP
	.GLOBL	TOINT,$B93,$B95,ID,SI,DI,$DMUL$,SETST,CAS,JMP,DFNRET
	.GLOBL	LNUM
	.=MEMTYP
START::
	.IFT
	.IF	EQ,MEMTYP-ROM
	MOV	#100000,R1	;CHECK SUM
	MOV	#3,R3
4$:	MOV	#10000,R2
	CLR	R0
3$:	ADD	(R1)+,R0
	ADC	R0
	SOB	R2,3$
	CMP	R0,#177777
	BEQ	2$
	RETURN
2$:	SOB	R3,4$
	.ENDC
;
	.IFTF
	MOV	#STACK,SP
	.IFF
	MOV	AVPK,SAUG
	MOV	AVPK+2,SAUG+2
	MOV	0,SAUG+4
	MOV	2,SAUG+6
	.IFTF
	.IF	EQ,MEMTYP-ROM
	MOV	#MVIDEO,HIMEM
	.IFF
	MOV	#MEMTYP,HIMEM
	.ENDC
	CLR	TRFLAG
	CLRB	LPTPOS
	MOV	#200.,STRSIZ
	MOV	#AUINC,R0
	MOV	#12,(0)+
	MOV	#12,(0)+
	MOV	R0,R1
	MOV	#40101,(0)+
	MOV	#44122,(0)+
	MOV	#37502,(0)+
	MOV	#52125,(0)+
	.REPT	4
	MOV	(1)+,(0)+
	.ENDR
;	MOV	#1,(R0)+	;MAXFIL <- 1
	MOV	#10.,R1
1$:	MOV	#NOUSR,(0)+	;НЕОПРЕДЕЛЕННЫЕ ФУНКЦИИ USR
	SOB	R1,1$
.IFT
	MOV	#4,@#$SCRT	;SENAS DRAW
; 	MOV	#2000,@#$SCRT
;
.IFTF
;	MOV	#15,R1		;neqwnyj tip
;5$:	MOV	#2004,(R0)+
;	SOB	R1,5$
;
;	CLR	(R0)+		;STOPAR
	CLR	PNTSAV
	CALL	MIDM		;ИНИЦИАЛИЗАЦИЯ УСТРОЙСТВ
	CALL	CLRTXT
	.IFT
	CALL	KINIT		;ИНИЦИАЛИЗАЦИЯ КЛЮЧЕЙ
 .iftf
	br	setw
sum1::	.word	0
	.iff
init:	mov	#stack,sp
	jsr	r2,sepap
	call	midm
	br	setw
	.iftf
;
ctrlc:	tst	lygis
	bmi	break
	mov	#stack,sp
	mov	#12,r0
	call	isv
	jmp	monit+4
;
;
wstop::	.byte	12,363,364,357,360,12
;
break::	mov	#irps2-10,@#breakw
	clr	@#177660
	clr	iodev
.ift
	emt	34
	bit	#400,r0
	beq	1$
	mov	#225,r0
	emt	16
	call	isvss
.iftf
1$:	mov	#wstop,r1
	mov	#5,r2
	call	isveil
	tst	lygis
	bge	2$
	tst	savjmp
	beq	3$
	mov	savjmp,r4
	clr	savjmp
3$:	cmp	r4,strreg
	bhi	2$
	cmp	@sp,#nexdbl-20
	beq	10$
	cmp	@sp,#nexjmp+22
	beq	10$
	cmp	@sp,#jmp+2
	beq	10$
	cmp	@sp,#dfnret-14
	blo	11$
	cmp	@sp,#dfnret-2
	bhi	11$
	beq	10$
	tst	(4)+
10$:	tst	(4)+
11$:	call	lnum
	mov	-(4),adrhlt
	br	4$
2$:	mov	#12,r0
	call	isv
4$:	jsr	r2,sepap
   	clr	lygis
setw:	clr	@#timer+2		;УСТАНОВКА ВЕКТОРОВ ПРЕРЫВАНИЙ
	MOV	#IRPS2-10,@#TIMER	;TAIMERIS DARBUI SU PRINTERIU
	MOV	#IRPS2-10,@#BREAKW
	CLR	@#BREAKW+2
	MOV	#TRAP,TRAPW
	CLR	TRAPW+2
	MOV	#IRPS2-10,@#ILCODW
	CLR	@#ILCODW+2
.IFF
	MOV	#137,@#0
	MOV	#INIT,@#2
;
.IFT
.IF	EQ	KONTYP-KLAS
.IFTF
	CLR	@#IRPSRG+4
.IFT
	MOV	#IRPS1,@#IRPSW
	CLR	@#IRPSW+2
	BIS	#100,@#IRPSRG
	CLR	EKRHUK
.IFF
	CLR	@#IRPSRG
.ENDC
.IFTF
	MOV	#SYSERR,@#ILCODW
SYS1:	MOV	#BREAK,@#BREAKW
	JMP	STAR 		;ПЕРЕХОД НА БЭЙСИК-МОНИТОР
SYSERR:	TRAP	51.
;
;
	; =========================
	; ****БЛОК ВВОДА-ВЫВОДА****
	; =========================
	;
	; ------------------------------------
	; ####КОНСТАНТЫ БЛОКА ВВОДА-ВЫВОДА####
	; ------------------------------------
	;
	.IFF
	.IF	EQ,MASINA-MDS
	TIMER=177546	;АДРЕС CSR ТАЙМЕРА
	.ENDC
	ARSK=177560	;АДРЕС РСК
	ARDK=177562		;АДРЕС РДК
	ARSTV=177564		;АДРЕС РСТВ
	ARDTV=177566		;АДРЕС РДТВ
	AVPK=60		;АДРЕС ВЕКТОРА ПРЕР. КЛ.
;
	.IFT
	ARDK=177662		;adres rdk bk0010
	;
	; ---------------------------------------
	; ## ИНИЦИАЛИЗАЦИЯ БЛОКА ВВОДА-ВЫВОДА ###
	; ---------------------------------------
	;
MIDM:
.IF	EQ,MEMTYP-ROM
	RESET
	EMT	14
.IFF
	CLR	@#260
	EMT	34
	ASR	R0
	BCS	1$
.ENDC
 	MOV	#233,R0
	EMT	16
1$:	MOV	#236,R0
	EMT	16
	MOV	#BUF,R0
2$:	MOV	#5012,(0)+
	CMP	R0,#TEXT
	BLO	2$
	CLR	IODEV
	RETURN
;
KINIT:	MOV	#FNKSTR,R1
	MOV	#KEYTXT,R2
	CLR	R3
2$:	INC	R3
	MOV	R1,R4
	MOV	R3,R0
	EMT	12
	MOVB	@R2,R5
	INC	R5
1$:	MOVB	(R2)+,(R4)+
	SOB	R5,1$
	ADD	#17.,R1
	CMP	R3,#10.
	BLO	2$
	CALL	ISVSS
	RETURN
KEYTXT:	.ASCII	<6>/COLOR /<5>/AUTO /<5>/GOTO /<5>/LIST /<4>/RUN/<12>
	.ASCII	<12>/COLOR 1,0/<12><6>/CLOAD"/<5>/CONT/<12><2>/./<12>
	.ASCII	<5><14>/RUN/<12>
	.EVEN
;
ISVSS::	MOV	#236,R0
	EMT	16
	MOV	#FNKSTR,R3
	CLR	R1
4$:	MOV	R3,R5
	MOVB	(R5)+,R2
	MOV	#' ,R0
3$:	DEC	R2
	BMI	5$
	CMPB	#' ,(R5)+
	BGE	3$
	MOVB	-(R5),R0
5$:	EMT	22
	ADD	#17.,R3
	INC	R1
	CMP	R1,#10.
	BLT	4$
	MOVB	#' ,R0
	EMT	22
	INC	R1
	EMT	22
	RETURN
;
	.IFF
MIDM:
	.IF	EQ,MASINA-MDS
	CLR	TIMER
	.ENDC
	RESET
	MOV	#12,BUF+6
	MOV	#IVS,AVPK
	BIS	#100,ARSK
	CLR	IODEV
	RTS	PC
	;
	.IFTF
	;
	; -------------------
	; ## ЧТЕНИЕ СТРОКИ ##
	; -------------------
	;
	.IFT
IVEIL::	MOV	R0,-(SP)		;СОХРАНЕНИЕ РЕГИСТРОВ
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R5,-(SP)
SBRALL:	EMT	26
	MOV	R1,-(SP)
SBROS:	MOV	#BUF,R1		;АДРЕС СТРОКИ
	MOV	R1,R3
	MOV	#400,R2		;ДЛИНА СТРОКИ
	MOV	R2,-(SP)
PIJ::	EMT	6		;ЧТЕНИЕ ОЧЕРЕДНОГО СИМВОЛА
	MOVB	R0,R5
	BGE	5$
	CMPB	R0,#211
	BEQ	55$
.IF	EQ,MEMTYP-RAM
	CMPB	R0,#214
	BEQ	PIJ
.ENDC
	CMPB	R0,#240
	BGE	14$
	CMPB	R0,#233
	BGE	55$
	CMPB	R0,#231
	BNE	153$
	JMP	26$
153$:	CMPB	R0,#225
	BGT	PIJ
	BNE	12$
13$:	EMT	16
	EMT	6
	CMPB	R0,#225
	BNE	13$
	EMT	16
	CMP	(SP)+,(SP)+
	CALL	ISVSS
	BR	SBRALL
12$:	CMPB	R0,#220
	BNE	55$
	CALL	STEP
55$:	EMT	16
	CMPB	R5,#236
	BNE	PIJ
	CALL	ISVSS
	BR	PIJ
14$:	CMP	R1,R3		;ЗАПИСЬ ОЧЕРЕДНОГО СИМВОЛА
	BEQ	17$
	INC	R2
	DEC	R3
17$:	MOVB	R0,(R1)+
16$:	INC	R3
15$:				;wywod na |kran
.IF	EQ,KONTYP-KLAS
	CALL	ISV
.IFF
	EMT	16
.ENDC
	DEC	R2
	BNE	PIJ
47$:	TRAP	25.		;СЛИШКОМ ДЛИННАЯ СТРОКА
5$:	CMPB	R0,#177
	BNE	155$
	MOVB	#26,R0
	BR	26$
155$:	CMPB	R0,#3
	BNE	20$
	JMP	CTRLC
20$:	CMPB	R0,#40
	BGE	14$
	CMPB	R0,#7
	BEQ	55$
	CMPB	R0,#10
	BEQ	2$
	CMPB	R0,#12
	BEQ	3$
	CMPB	R0,#14
	BEQ	4$
	SUB	#23,R5
	BLT	PIJ
	ASL	R5
	ADD	R5,PC
	BR	21$
	BR	51$
	BR	PIJ
	BR	26$
	BR	27$
	BR	6$
	BR	7$
	BR	132$
	BR	133$
	BR	134$
	BR	135$
	BR	136$
	BR	137$
4$:	EMT	16		;ОЧИСТКА
	TST	(SP)+
	BR	SBROS
6$:	CMP	R2,@SP		;АНУЛИРОВАНИЕ СИМВОЛА
	BEQ	152$
	CMP	R1,R3
	BNE	1$
	DEC	R1
	DEC	R3
	INC	R2
	BR	151$
1$:	CMP	#BUF,R1
	BEQ	152$
	TSTB	-(R1)
151$:	EMT	16
152$:	MOVB	#26,R0
	BR	26$
2$:	CMP	#BUF,R1		;  <---
	BEQ	PIJ
	DEC	R1
	BR	55$
7$:	CMP	R1,R3		;  --->
	BEQ	11$
	INC	R1
	INC	R2
	BR	15$
11$:	MOVB	#40,(R1)+
	BR	16$
51$:	MOVB	#31,R0		;gorizontalxnaq tabulqciq
	CMP	R1,R3
	BLO	52$
	MOVB	#40,R0
	INC	R3
	DEC	R2
	BLE	47$
52$:	INC	R1
	EMT	16
	MOV	R1,-(SP)
	MOV	R2,-(SP)
	EMT	26
	MOV	R1,R5
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	BIT	#7,R5
	BNE	51$
	JMP	PIJ
21$:	CMP	R3,#BUF
	BNE	49$
	TST	LYGIS
	BMI	49$
	JMP	BC
49$:	MOVB	#10,R0			;wozwrat stroki
41$:	CMP	#BUF,R1
	BEQ	48$
	EMT	16
	DEC	R1
	BR	41$
26$:	CMP	R1,R3		;СМЫКАНИЕ СТРОКИ
	BEQ	48$
	BR	42$
3$:	CMP	R3,R1			;konec ~teniq stroki
	BEQ	53$
	CMP	-(SP),-(SP)
	CALL	PELING
40$:
.IF	EQ,KONTYP-KLAS
	CALL	ISV
.IFF
	EMT	16
.IFTF
	DEC	R5
	BGE	40$
	CMP	(SP)+,(SP)+
	BR	54$
53$:
.IFT
	CALL	ISV
.IFF
	EMT	16
.ENDC
54$:	MOVB	R0,(R3)+
	MOV	R3,R1
	CMP	(SP)+,(SP)+
	MOV	(SP)+,R5
	MOV	(SP)+,R3
	MOV	(SP)+,R2
	MOV	(SP)+,R0
	RTS	PC
27$:	CMP	R1,R3		;РАЗМЫКАНИЕ СТРОКИ
	BEQ	48$
	MOV	R3,R5
31$:	MOVB	-(R5),1(R5)
	CMP	R5,R1
	BHI	31$
	MOVB	#40,@R1
	BR	42$
132$:	JSR	R5,RSSE
	BR	142$
133$:	JSR	R5,RSSE
	BR	143$
134$:	JSR	R5,RSSE
	DEC	R1
	BR	142$
135$:	JSR	R5,RSSE
	INC	R1
	BR	142$
136$:	JSR	R5,RSSE
	INC	R1
	BR	143$
137$:	JSR	R5,RSSE
	DEC	R1
	BR	143$
43$:	EMT	16
	DEC	R5
	BLT	44$
	MOVB	#12,R0
	EMT	16
	MOVB	#231,R0
	BR	43$
44$:	MOV	R1,R3
	BR	33$
142$:	SUB	R0,R1
	CMP	R1,#BUF
	BLO	140$
	BR	141$
143$:	ADD	R0,R1
	CMP	R1,R3
	BLOS	141$
140$:	MOV	(SP)+,R1
	MOV	(SP)+,R0
	BR	48$
141$:	TST	(SP)+
	MOV	(SP)+,R0
	EMT	16
	BR	48$
34$:	EMT	16		;smykanie stroki
	DEC	R5
	BLT	45$
	MOVB	#12,R0
	EMT	16
	MOVB	#10,R0
	EMT	16
	ADD	2(SP),@SP
	MOVB	@(SP),R0
	EMT	16
	MOVB	#26,R0
	BR	34$
45$:	MOV	R1,R5
	INC	R5
30$:	MOVB	(R5)+,-2(R5)
	CMP	R5,R3
	BLO	30$
	INC	R2
	DEC	R3
33$:	MOV	R1,-(SP)
	MOV	R2,-(SP)
	MOVB	10(SP),R1
	MOVB	11(SP),R2
	EMT	24
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	ADD	#6,SP
48$:	JMP	PIJ
42$:	CLR	-(SP)
	CLR	-(SP)
	CALL	PELING
	MOV	#BUF,-(SP)
	SUB	10(SP),@SP
32$:	DEC	4(SP)
	BLT	37$
	ADD	2(SP),@SP
	BR	32$
37$:	MOV	R1,-(SP)
	MOV	R2,-(SP)
	EMT	26
	MOVB	R1,10(SP)
	MOVB	R2,11(SP)
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	BIT	#1,R0
	BEQ	34$
	TSTB	R0
	BMI	43$
	EMT	16
36$:	DEC	R5
	BLT	35$
	BNE	128$
	MOV	R1,-(SP)
	MOV	R2,-(SP)
	EMT	26
	CMP	R2,#27
	BNE	129$
	DECB	11(SP)
129$:	MOV	(SP)+,R2
	MOV	(SP)+,R1
128$:	ADD	2(SP),@SP
	MOVB	#12,R0
	EMT	16
	MOVB	#27,R0
	EMT	16
	MOVB	@(SP),R0
	EMT	16
	BR	36$
35$:	INC	R3
	DEC	R2
	BGT	33$
	TRAP	31
	;
PELING:	MOV	R0,-(SP)
	MOV	R1,-(SP)
	MOV	R3,-(SP)
	SUB	#BUF,R1
	SUB	#BUF,R3
	ADD	16(SP),R1
	ADD	16(SP),R3
	INC	R1
	CALL	SSE
	CLR	R5
9$:	SUB	R0,R3
	BLT	4$
	BEQ	6$
	INC	R5
	BR	9$
6$:	CMPB	#27,4(SP)
	BNE	4$
	INC	R5
4$:	SUB	R0,R1
	BLE	5$
	INC	12(SP)
	DEC	R5
	BR	4$
5$:	MOV	R0,10(SP)
	MOV	(SP)+,R3
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	RTS	PC
;
RSSE:	MOV	R0,(SP)
	MOV	R1,-(SP)
	MOV	R5,-(SP)
SSE:	EMT	34
	ROR	R0
	BCS	1$
	MOV	#100,R0
	RTS	PC
1$:	MOV	#40,R0
	RTS	PC
;
	.IFF
	;
	; ----------------------
	; ----------------------
	; ВЫХОД : КОД В R0 (МЛ. БАИТ)
	;
IVS::	CLR	ARSK
	MOVB	ARDK,R0
	INCB	SIIKP
	CALL	ALRIC
	RTI
	;
	; --------------------
	; ### ВЫВОД СИМВОЛА ##
	; --------------------
	; ВХОД : КОД В R0 (МЛ. БАИТ)
	;
	.IFT
ISV::	EMT	16
.IF	EQ,KONTYP-KLAS
	TST	EKRHUK
	BEQ	1$
	CALL	@EKRHUK
1$:
.ENDC
	RETURN
	.IFTF
ISVTO::	TST	IODEV
	BEQ	ISV
	MOV	#BUFOUT,R1
	MOV	#1,R2
	MOVB	R0,@R1
	.IFF
	JMP	ISVEIL
	;
CRLF:	MOV	#15,R0
	CMPB	TABY,#27
	BEQ	ISV
	INCB	TABY
;
ISV::	TSTB	ARSTV
	BPL	ISV
	MOVB	R0,ARDTV
	CMPB	R0,#12
	BEQ	CRLF
	CMPB	TABX,#117
	BNE	30$
	CMPB	TABY,#27
	BEQ	31$
	INCB	TABY
	BR	31$
30$:	INCB	TABX
	CMPB	R0,#15
	BNE	33$
31$:	CLRB	TABX
33$:	RTS	PC
	;
	; ---------------------
	; ### ЧТЕНИЕ СТРОКИ ###
	; ---------------------
	;
IVEIL::	MOV	R0,-(SP)		;СОХРАНЕНИЕ РЕГИСТРОВ
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R5,-(SP)
	MOVB	TABX,-(SP)
SBROS:	MOV	#BUF,R1		;АДРЕС СТРОКИ
	MOV	R1,R3
	MOV	#377,R2		;ДЛИНА СТРОКИ
	MOV	R2,-(SP)
PIJ::	CLRB	SIIKP		;РЕЗРЕШЕНИЕ ПРЕРЫВАНИЯ С КЛАВИАТУРЫ
	BIS	#100,ARSK
14$:	TSTB	SIIKP		;ЧТЕНИЕ ОЧЕРЕДНОГО СИМВОЛА
	BEQ	14$
	CMPB	R0,#40
	BGE	6$
	CMPB	R0,#11
	BNE	22$
	JMP	33$
22$:	CMPB	R0,#13
	BNE	21$
	JMP	15$
21$:	CMPB	R0,#3
	BNE	44$
	JMP	CTRLC
44$:	CMPB	R0,#14
	BEQ	1$
	CMPB	R0,#15
	BEQ	2$
	CMPB	R0,#16
	BEQ	3$
	CMPB	R0,#17
	BEQ	3$
	CMPB	R0,#23
	BEQ	27$
	CMPB	R0,#24
	BEQ	30$
	CMPB	R0,#25
	BEQ	8$
	CMPB	R0,#31
	BEQ	4$
	CMPB	R0,#32
	BEQ	5$
	CMPB	R0,#33
	BNE	PIJ
	BIS	#100,ARSK
	CALL	STEP
	BR	PIJ
6$:	CMPB	R0,#177
	BEQ	53$
	JMP	16$
53$:	CMP	R2,@SP		;АНУЛИРОВАНИЕ СИМВОЛА
	BEQ	PIJ
	CMP	R1,R3
	BNE	7$
	DEC	R1
	DEC	R3
	TST	(R2)+
	BR	11$
7$:	CMP	#BUF,R1
	BEQ	PIJ
	MOVB	#40,-(R1)
	INC	R2
11$:	MOVB	#32,R0
	DECB	TABX
	DECB	TABX
	CALL	ISV
	MOVB	#40,R0
	CALL	ISV
	MOVB	#32,R0
	BR	12$
1$:	CALL	ISV		;ОЧИСТКА
	CLRB	TABX
	CLRB	TABY
	TST	(SP)+
	BR	SBROS
2$:	MOVB	#12,R0		;КОНЕЦ ЧТЕНИЯ СТРОКИ
	MOV	R3,R1
	DEC	R2
	BEQ	24$
	MOVB	R0,(R1)+
	CALL	ISV		;ПО СИМВОЛУ ЗАВЕРШИТЕЛЮ
	BR	KCTSTR
3$:	DECB	TABX
	CALL	ISV		;РУС. ЛАТ.
	BR	PIJ
27$:	CMP	R1,R3		;РАЗМЫКАНИЕ СТРОКИ
	BEQ	PIJ
	MOV	R3,R5
31$:	MOVB	-(R5),1(R5)
	CMP	R5,R1
	BHI	31$
	MOVB	#40,@R1
	DECB	TABX
	BR	17$
30$:	CMP	R1,R3		;СМЫКАНИЕ СТРОКИ
	BEQ	18$
	MOV	R1,R5
	INC	R5
32$:	MOVB	(R5)+,-2(R5)
	CMP	R5,R3
	BLO	32$
	TST	(R2)+
	DEC	R3
	DECB	TABX
	CALL	KISV
	BR	23$
4$:	CMP	R1,R3		; --->
	BEQ	13$
	INC	R1
	INC	R2
	CALL	KISV
	BR	23$
13$:	MOVB	#40,R0
	MOVB	R0,(R1)+
	BR	17$
5$:	CMP	R2,@SP		; <---
	BEQ	18$
	CMP	#BUF,R1
	BEQ	18$
	DEC	R1
	INC	R2
12$:	DECB	TABX
	DECB	TABX
	CALL	KISV
	BR	23$
8$:	CMP	R3,#BUF		;wozwrat stroki
	BNE	180$
	JMP	BC
180$:	MOVB	#32,R0
9$:	CMP	R1,#BUF
	BEQ	23$
	DEC	TABX
	DEC	TABX
	CALL	KISV
	DEC	R1
	INC	R2
	BR	9$
15$:	DEC	TABX		;sbros konca stroki
	CALL	KISV
	MOV	R1,R3
26$:	JMP	PIJ
33$:	MOVB	#31,R0		;gorizontalxnaq tabulqciq
	CMP	R1,R3
	BLO	34$
	MOVB	#40,R0
	INC	R3
	DEC	R2
	BLE	24$
34$:	INC	R1
	CALL	KISV
	BITB	#111,TABX
	BNE	33$
	JMP	PIJ
16$:	CMP	R1,R3		;ЗАПИСЬ ОЧЕРЕДНОГО СИМВОЛА
	BEQ	25$
	INC	R2
	DEC	R3
25$:	MOVB	R0,(R1)+
17$:	INC	R3
	CALL	KISV
23$:	DEC	R2
	BEQ	24$
18$:	JMP	PIJ
24$:	CLRB	SIIKP
	BIS	#100,ARSK
	TRAP	31		;СЛИШКОМ ДЛИННАЯ СТРОКА
KCTSTR:	CMP	(SP)+,(SP)+
	MOV	(SP)+,R5
	MOV	(SP)+,R3
	MOV	(SP)+,R2
	MOV	(SP)+,R0
	CLRB	SIIKP
	BIS	#100,ARSK
	RTS	PC
KISV:	CALL	ISV		;ОТОБРАЖЕНИЕ ОЧЕРЕДНОГО СИМВОЛА
	TSTB	TABX
	BGE	21$
	DECB	TABY
	BLT	22$
	MOVB	#117,TABX
21$:	RTS	PC
22$:	MOVB	#27,TABY
	MOVB	#117,TABX
	RTS	PC
	;
	; --------------------------
	; ### ОТОБРАЖЕНИЕ СТРОКИ ###
	; --------------------------
	; ВХОД:	R1- АДРЕС СТРОКИ
	;		R2- ДЛИНА СТРОКИ (МЛ. БАИТ)
	;		  - СИМВОЛ ОГРАНИЧИТЕЛЬ (СТ. БАИТ)
	;
	.IFT
ISVETO::MOV	R3,-(SP)
	MOV	IODEV,R3
	BNE	LP
	MOV	(SP)+,R3
ISVEIL::
.IF	EQ,KONTYP-KLAS
	MOV	R1,-(SP)
	MOV	R2,-(SP)
.IFTF
	EMT	20
.IFT
	TST	EKRHUK
	BEQ	3$
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	MOV	R0,-(SP)
	MOV	R3,-(SP)
	MOV	R2,R3
	SWAB	R3
	BIC	#177400,R2
1$:	MOVB	(R1)+,R0
	CALL	@EKRHUK
	CMPB	R0,R3
	BEQ	2$
	SOB	R2,1$
2$:	MOV	(SP)+,R3
	MOV	(SP)+,R0
.ENDC
3$:	RETURN
;
LP::	BPL	FILOUT
	MOV	R0,-(SP)
	MOV	R5,-(SP)
	MOV	R2,R3
	SWAB	R3
	BIC	#177400,R2
	MOV	#ARPORT,R5
	MOVB	#23,R0
	CALL	LPISV
1$:	MOVB	(1)+,R0
	CMPB	R0,#37
	BLOS	2$
	CMPB	R0,#177
	BLO	3$
	CMPB	R0,#277
	BHI	3$
2$:	CMPB	R0,#10
	BEQ	6$
	CMPB	R0,#14
	BEQ	8$
	CMPB	R0,#15
	BEQ	8$
	CMPB	R0,#205
	BEQ	8$
	CMPB	R0,#12
	BNE	9$
	MOVB	#15,R0
	CALL	LPISV
	MOV	#12,R0
8$:	CLRB	LPTPOS
	BR	9$
6$:	DECB	LPTPOS
	BGT	9$
	MOVB	#77,LPTPOS
	BR	9$
3$:	INCB	LPTPOS
9$:	CALL	LPISV
	CMPB	R0,R3
	BEQ	5$
	SOB	R2,1$
5$:	MOV	(SP)+,R5
	MOV	(SP)+,R0
	MOV	(SP)+,R3
	RETURN
;
LPISV:	BIC	#177000,R0
	BIS	#400,R0
2$:	BIT	#400,@R5
	BEQ	2$
	MOV	R0,@R5
3$:	BIT	#400,@R5
	BNE	3$
	CLR	@R5
	CMPB	LPTPOS,#100
	BLE	1$
	CLRB	LPTPOS
1$:	RETURN
;
	.IFF
ISVETO::
ISVEIL::MOV	R0,-(SP)
	MOV	R3,-(SP)
	MOV	R2,R3		;ОТРАБОТКА ПРИЗНАКОВ КОНЦА СТРОКИ
	SWAB	R3
	CMPB	R3,#12
	BNE	11$
	MOVB	#15,R3
11$:	BIC	#177400,R2
	MOV	IODEV,R0
	BNE	LP
1$:	TSTB	@R1
	BPL	2$
	TSTB	RLPOZ		;КИРИЛИЦЫ
	BNE	4$
	INCB	RLPOZ
	MOVB	#16,R0
	CALL	ISV
	DECB	TABX
4$:	CLR	-(SP)
	MOVB	(R1)+,@SP
	SUB	#200,@SP
	MOVB	@SP,R0
	TST	(SP)+
	BR	3$
2$:	TSTB	RLPOZ
	BEQ	5$
	CLRB	RLPOZ
	MOVB	#17,R0
	CALL	ISV
	DECB	TABX
5$:	MOVB	(R1)+,R0
3$:	CALL	ISV
	CMPB	R0,R3
	BEQ	ISVG
	DEC	R2
	BNE	1$
ISVG:	CLRB	RLPOZ		;КОНЕЦ ВЫВОДА СТРОКИ
	MOVB	#17,R0
	CALL	ISV
	DECB	TABX
ENDLP:	MOV	(SP)+,R3
	MOV	(SP)+,R0
	RTS	PC
;
LP:	BPL	FILOUT
	MOVB	(R1)+,R0
10$:	TST	@#177514
	BMI	11$
	TSTB	@#177514
	BPL	10$
	MOVB	R0,@#177516
	CMPB	R0,#12
	BNE	15$
	MOV	#15,R0
	BR	10$
15$:	CMPB	R0,R3
	BEQ	2$
	DEC	R2
	BNE	LP
2$:	BR	ENDLP
11$:	TRAP	23
;
FILOUT:	CMP	(SP)+,(SP)+
	MOV	R0,R3
	.IFT
FILOUT:	TST	(SP)+
	.IFTF
	CALL	FNDBUF
	BITB	#1,(R0)+
	BNE	NOTOUT
	CLR	R3
	BISB	@R0,R3
	ADD	R0,R3
	ADD	#21,R3
COPOUT::DEC	R2
	BMI	NOSTEP
	MOVB	(R1)+,(R3)+
	INCB	@R0
	BNE	COPOUT
	MOV	R1,-(SP)
	DEC	R0
	CALL	CASCII
	CALL	CAS
	CALL	BLOCKN
	MOV	(SP)+,R1
	SUB	#400,R3
	INC	R0
	BR	COPOUT
NOSTEP:	RETURN
NOTOUT:	TRAP	52.
; 
.IFF
ALRIC:	CMPB	R0,#20
	BNE	1$
	.IF	GE,MASINA-SM4
	BIC	#0,177776
	.IFF
	MTPS	#0
	.ENDC
	TST	(SP)+
	JMP	BREAK
1$:	MOVB	@#ARDK,INK
	MOVB	#100,ARSK
	RTS	PC
;
INKEY::	MOV	#INK,-(SP)
	CLR	-(SP)
	TSTB	SIIKP
	BEQ	1$
	INC	@SP
	CLRB	SIIKP
1$:	RETURN

	.IFTF
	.GLOBL	VYKDYT,STAR,HALT,EILUTE,KODTST,AGAUD,IFGAUD
STEP:	TST	LYGIS
	BNE	NOSTEP
	CMP	R3,#BUF
	BNE	NOSTEP
	TST	ERRS
	BNE	1$
	ADD	#20,SP
	NEG	TRFLAG
	MOV	ADRHLT,R4
	BNE	TRIN
	MOV	FCB,R4
	TST	(R4)+
	BR	STP2
1$:	TRAP	17.
TRIN::	MOV	#IRPS2-10,@#BREAKW
      	CALL	AGAUD
STP2:	CLR	AUTOON
	CLR	NUMBER
	CMP	-(R4),-(R4)
	JSR	R0,SETST
	BR	50$
	JMP 	SYS1
50$:	CMP	(4)+,(4)+
	MOV	R4,R2
	MOV	R4,R0
	MOV	-(0),R0
	JSR	R5,KODTST
	BR	3$
	BR	4$
	CMPB	#40,@R0
	BNE	21$
	JSR	R5,NEXPAP
	MOV	R2,R4
21$:	SUB	#6,R4
	BR	32$
4$:	MOV	R0,R3
	INC	R3
41$:	CMPB	(3)+,#12
	BEQ	45$
42$:	JSR	R2,IFGAUD
	BR	45$
	BR	41$
    	CMP	#4,R5
	BNE	47$
	JSR	R5,NEXPAP
	BR	42$
47$:	CMP	#6,R5
	BEQ	42$
	MOV	R5,R0
48$:	TRAP	110
	BR	43$
	TRAP	111
	BR	44$
	BR	42$
44$:	JSR	R5,HCOR
	TRAP	112
	CMPB	#',,@R3
	BNE	42$
	INC	R3
	BR	48$
45$:	MOV	R2,R4
	BR	21$
43$:	CMP	#2,R0
	BNE	42$
	JSR	R5,REPA
	BR	42$
	BR	44$
3$:	CMPB	#74,@R0	;RESTORE
	BEQ	21$
	INC	R0
	MOV	R0,R3
	TRAP	110
	BR	31$
	TRAP	111
	BR	32$
	NOP
31$:	JSR	R5,REPA
	BR	37$
32$:	JSR	R5,HCOR
37$:	MOV	R2,R4
    	MOV	SP,PNTSAV
	CALL	EILUTE
	TST	TRFLAG
	BLE	2$
	MOVB	#133,R0
	CALL	ISV
	BIS	#20000,R2
	CALL	ISVEIL
	MOVB	#135,R0
	CALL	ISV
	BR	22$
2$:	CALL	ISVEIL
22$:	MOV	-(4),R4
	MOV	#BREAK,@#BREAKW
	JMP	VYKDYT
;
REPA:	TST	(SP)+
	CMP	LENT,LIMIT
	BEQ	1$
	MOV	@LIMIT,R4
	CALL	AGAUD
	CMP	-(4),-(4)
	TST	(5)+
1$:	MOV	R5,PC
;
HCOR:	TST	(SP)+
	JSR	R0,SETST
	BR	2$
	BR	1$
2$:	CMP	(4)+,(4)+
	CMP	R4,R2
	BEQ	1$
	CMP	R4,TABTOP
	BLO	1$
	CMP	-(4),-(4)
	MOV	@R4,R4
	MOV	R4,-(SP)
	MOV	@R4,-(SP)
	INC	NUMBER
	MOV	#STPEND,@R4
1$:	MOV	R5,PC
;
NEXPAP:	TST	AUTOON
	BEQ	1$
	RTS	R5
1$:	TST	(SP)+
	MOV	R5,AUTOON
	MOV	CIKL,R1
3$:	CMP	R1,LIMIT
	BHIS	2$
	MOV	2(1),R4
	CALL	AGAUD
	CMP	-(4),-(4)
	JSR	R5,HCOR
	MOV	@R1,R0
	ADD	#10,R1
	MOV	-(0),R0
	ROL	R0
	BMI	3$
	ADD	#14,R1
	BR	3$
2$:	MOV	AUTOON,PC
;
STPEND::MOV	#IRPS2-10,BREAKW
	JSR	R2,SEPAP
	TST	-(R4)
	TST	TRFLAG
	BLE	1$
	JMP	TRIN
1$:	MOV	R4,ADRHLT
	NEG	TRFLAG
	MOV	#BREAK,BREAKW
	JMP	STAR
;
SEPAP::	TST	(SP)+
	INC	ERRS
	TST	PNTSAV
	BEQ	5$
	MOV	PNTSAV,SP
2$:	DEC	NUMBER
	BLT	5$
	MOV	(SP)+,@(SP)+
	BR	2$
5$:	CLR	PNTSAV
	MOV	#STACK,SP
	CLR	ERRS
	CLR	NUMBER
	MOV	R2,PC
;
	.IFT
CURON::	EMT	34
	BIT	#40000,R0
	BEQ	C2
C1:	MOV	#232,R0
	CALL	ISV
C2:	RETURN
;
CUROFF::EMT	34
	BIT	#40000,R0
	BEQ	C1
	RETURN
;
INKEY::	MOV	#INK,-(SP)
	CLR	-(SP)
	TSTB	@#TSTKEY
	BEQ	1$
	EMT	6
	MOVB	R0,INK
	INC	@SP
1$:	JMP	@(R4)+
;
MAGNIT::
	EMT	36
	RETURN
;
.IF EQ,1
SIGNAL::MOV	R2,R0
	MOVB	#320,@#ASPORT
1$:	SOB	R0,1$
	MOV	R2,R0
	MOVB	#220,@#ASPORT
2$:	SOB	R0,2$
	SOB	R1,SIGNAL
	RETURN
.ENDC
;
USEROM::MOV	(4)+,R3
	MOV	R4,SAVJMP
	MOV	#1,R0
	MOV	#NOROM,@#BREAKW
NEXROM:	MOVB	R0,@#ASPORT+1
	ASL	R0
	CALL	@#AUSROM
	BCS	ERROM
	JSR	R0,STATUSQ
	JMP	@(R4)+
;
STATUSQ:TST	(SP)+
	MOV	#BREAK,@#BREAKW
	MOVB	#200,@#ASPORT+1
	CLRB	@#ASPORT+1
	MOV	SAVJMP,R4
	CLR	SAVJMP
	MOV	R0,PC
;
NOROM:	ADD	#6,SP
ERROM:	CMP	R0,#10
	BNE	NEXROM
	JSR	R0,STATUSQ
;
	.IFF
SIGNAL::MOV	#7,R0
	CALL	ISV
	RETURN
;
USEROM::
	.IFTF
NOUSR:	TRAP	5
;
ADDRMN::TRAP	106
	MOV	#14,R0
	CALL	ISV
;
	.IFT
	MOV	#1000,SP
	JMP	100274
;
	.IFF
	.IF	EQ,MASINA-MDS
	COM	TIMER
	.ENDC
	MOV	SAUG,AVPK
	MOV	SAUG+2,AVPK+2
	MOV	SAUG+4,0
	MOV	SAUG+6,2
	COM	ARSK
	JMP	0
;
	.IFTF
LOC::	TST	(SP)+
	BNE	1$
	CALL	CUROFF
	JMP	@(R4)+
1$:	CALL	CURON
	JMP	@(R4)+
;
ZONA::
	.IFT
	MOV	IODEV,R3
	BLT	3$
	BGT	4$
	EMT	26
	MOV	R1,-(SP)
	BR	41$
3$:	MOVB	LPTPOS,-(SP)
	BR	41$
4$:	CALL	FNDBUF
	MOVB	@R0,-(SP)
41$:	SUB	#20,@SP
	BGT	41$
	BEQ	42$
	NEG	@SP
	BR	SPC1
42$:	MOV	#20,@SP
	BR	SPC1
;
	.IFF
	MOV	#40,R0
1$:	CALL	ISV
	BITB	#17,TABX
	BNE	1$
2$:	JMP	@(R4)+
;
	.IFT
;SPC::	CALL	TIKR
;	BR	SPC1
TAB::	CALL	TIKR
	MOV	IODEV,R3
	BEQ	1$
	BGT	2$
	MOVB	LPTPOS,R1
	BR	3$
2$:	CALL	FNDBUF
	CLR	R1
	BISB	@R0,R1
	BR	3$
1$:	EMT	26
3$:	SUB	R1,@SP
SPC1:	MOV	#40,R0
1$:	DEC	@SP
	BLT	2$
	CALL	ISVTO
	BR	1$
2$:	TST	(SP)+
	JMP	@(4)+
;
TIKR:	TSTB	3(SP)
	BNE	NOUSR
	RETURN
;
AT::	CALL	TIKR
	MOV	(SP)+,R2
	TST	IODEV
	BNE	TAB
 	CALL	TIKR
	MOV	(SP)+,R1
	EMT	24
	JMP	@(R4)+
;
DLOCX::	CMP	(SP)+,(SP)+
SLOCX::	TST	(SP)+
ILOCX::	TST	(SP)+
LOCX::	EMT	26
	MOV	R1,-(SP)
	JMP	@(R4)+
;
DLOCY::	CMP	(SP)+,(SP)+
SLOCY::	TST	(SP)+
ILOCY::	TST	(SP)+
LOCY::	EMT	26
	MOV	R2,-(SP)
	JMP	@(R4)+
;
$KEY::	MOV	(SP)+,R0
	CMP	R0,#16.
	BLOS	1$
	MOV	#16.,R0
1$:	MOV	(SP)+,R2
	MOV	(SP)+,R3
	BLE	NOUSR
	CMP	R3,#10.
	BGT	NOUSR
	MOV	#FNKSTR-17.,R1
2$:	ADD	#17.,R1
	SOB	R3,2$
	MOVB	R0,(R1)+
	BLE	4$
3$:	MOVB	(R2)+,(R1)+
	SOB	R0,3$
4$:	CALL	ISVSS
	JMP	@(R4)+
;
.IF EQ,1
KEYISV::MOV	#FNKSTR,R3
4$:	MOV	R3,R1
	MOVB	(R1)+,R2
	BEQ	1$
3$:	MOVB	(R1)+,R0
	CMPB	#' ,R0
	BLE	2$
	MOVB	#' ,R0
2$:	CALL	ISV
	SOB	R2,3$
1$:	MOV	#12,R0
	CALL	ISV
	ADD	#17.,R3
	CMP	R3,#FNKSTR+170.
	BLO	4$
	JMP	@(R4)+
;
KEYON::
	CALL	ISVSS
	JMP	@(R4)+
KEYOFF::MOV	@#214,R2
	MOV	@#212,@#214
	MOV	#236,R0
	EMT	16
	MOV	R2,@#214
	JMP	@(R4)+
;
.ENDC
	.IFF
AT::	MOV	(SP)+,R2
1$:	SUB	#30,R2
	BPL	1$
	ADD	#30,R2
	MOV	#15,R0
	CLRB	TABX
2$:	CALL	ISV
	MOV	#35,R0
	DECB	TABX
	CMPB	R2,TABY
	BEQ	TAB
	BGT	3$
	INCB	TABY
	BR	2$
3$:	DECB	TABY
	DEC	R0
	BR	2$
TAB::	MOV	(SP)+,R1
1$:	SUB	#120,R1
	BPL	1$
	ADD	#120,R1
2$:	CMPB	R1,TABX
	BEQ	3$
	MOV	#40,R0
	CALL	ISV
	BR	2$
3$:	JMP	@(R4)+
;
IRPS::
MAGNIT::MOVB	#2,1(1)
CURON::
CUROFF::RETURN
;
RSP::	TST	-(SP)
SPFORM::JMP	@(R4)+
$PAINT::TST	(SP)+
$KEY::	CMP	(SP)+,(SP)+
	TST	(SP)+
KEYON::
KEYOFF::
KEYISV::JMP	@(R4)+
;
PUNKT::
VEKT::	CMP	(SP)+,(SP)+
COLR::
COLF::	TST	(SP)+
TRECIA::JMP	@(R4)+
;
;
;======================
;======================
;
;
	.IFT
	.IF	EQ,KONTYP-KLAS
IRPS1:
	.GLOBL	GRBCOL,ASCCOD,ATSTAB
;
	MOV	R0,-(SP)
	MOVB	@#IRPSRG,R0
	BIC	#1,R0
	CMP	R0,#24
	BHIS	1$
	JMP	@LEN(R0)
1$:	MOV	(SP)+,R0
	RTI
;
LEN:	.WORD	START
	.WORD	0
	.WORD	NET
	.WORD	CLOAD1
	.WORD	CSAVE1
	.WORD	GR
	.WORD	GR
	.WORD	COPY
	.WORD	GR
	.WORD	SHO
;
ST:	ADD	#6,SP
	JMP	@#0
;
CLOAD1:
	MOV	FCB,TABTOP
	MOV	FCB,CIKL
	MOV	FCB,LIMIT
	MOV	FCB,LENT
	CLR	STRREG
	MOV	#MAG,R0
	MOV	#3,(R0)+
	MOV	#TEXT,(R0)+
	MOV	FCB,@R0
	SUB	#TEXT,(R0)+
	CALL	CAS1
	MOV	#MAG+26,R3
	MOV	(R3)+,R2
	MOV	@R3,R0
	MOV	#TEXT,R3
	ADD	R3,R0
	SUB	R2,R3
	MOV	FCB,R2
	MOV	R2,R4
	SUB	-(R0),R4
5$:	MOV	-(R0),-(R2)
	MOV	-(R0),-(R2)
	ADD	R3,@R2
	MOV	-(R0),-(R2)
	CMP	R2,R4
	BHI	5$
	MOV	R0,ENDCOD
	MOV	R0,TXEND
	MOV	R2,LENT
	MOV	R2,LIMIT
	MOV	R2,CIKL
	MOV	R4,TABTOP
	BR	ST
;
CSAVE1:
	MOV	#MAG,R0
	MOV	#2,(R0)+
	MOV	#TEXT,(R0)+
	CALL	GRBCOL
	MOV	R1,R5
	MOV	TABTOP,R4
	MOV	R5,@R0
	SUB	#TEXT-2,(R0)+
	MOV	@PC,ERRS
7$:	MOV	(R4)+,(R5)+
	CMP	R4,FCB
	BLO	7$
	MOV	R4,@R5
	SUB	TABTOP,(R5)
	MOV	#MAG,R1
	ADD	(R5)+,4(R1)
	CALL	IRPS
	CALL	ATSTAB
	BR	ST
;
CAS1:	MOV	#MAG,R1
	CALL	IRPS
	RETURN
.IFTF
;TRP:	MOV	#IRPS2-10,@#BREAKW
;	MOVB	#4,@#MAG+1
;	BR	IRPS2
IRPS::
.IFT
	BIC	#100,@#IRPSRG
.IFTF
;	MOV	#TRP,@#BREAKW
	BIS	#100,@#177660
	MOV	R0,-(SP)
	MOV	R1,-(SP)
	MOV	R2,-(SP)
	MOV	R3,-(SP)
;	MOV	R4,-(SP)
	MOV	R5,-(SP)
;	MOV	SP,R4
	CLR	R5
	MOVB	@#IRPSRG+2,R0
	MOVB	(R1)+,R0
	CALL	ISVT
	CALL	IVT
	MOV	R1,R2
	ADD	#5,R2
	MOV	#12,R3
1$:	MOVB	(R2)+,R0
	CALL	ISVT
	SOB	R3,1$
	MOV	#4,R3
	CALL	IVT
	MOVB	R0,(R1)+
	BNE	IRPS2
	CMPB	-2(R1),#3
	BLO	6$		;BK->TT
	ADD	#6,R2
10$:	;CLR	@#MSTART	;TT->BK
	CALL	IVT
	MOVB	R0,(R2)+
	SOB	R3,10$
	MOV	-(R2),R3
;	MOV	R3,@#MLEN
	BEQ	IRPS2
	TST	@R1
	BEQ	11$
	MOV	@R1,R2
	BR	4$
11$:	MOV	-(R2),R2
	MOV	R2,@#MSTART
4$:	CALL	IVT
	MOVB	R0,(R2)+
	ADD	R0,R5
	SOB	R3,4$
	BR	3$
;
6$:	MOVB	(R1)+,R0
	CALL	ISVT
	SOB	R3,6$
	MOV	-(R1),R3
;	BEQ	IRPS2
	MOV	-(R1),R2
7$:	MOVB	(R2)+,R0
	CALL	ISVT
	ADD	R0,R5
	SOB	R3,7$
3$:	CALL	IVT
	CMPB	R0,R5
	BEQ	12$
	MOVB	#2,-(R1)
12$:	MOV	R5,R0
	CALL	ISVT
IRPS2::	MOV	#MAG+32,R2
	MOV	#MAG+6,R1
	MOV	#10,R3
16$:	MOV	(R1)+,(R2)+
	SOB	R3,16$
;	MOV	R4,SP
	MOV	(SP)+,R5
;	MOV	(SP)+,R4
	MOV	(SP)+,R3
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	CLR	@#177660
.IFT
	BIS	#100,@#IRPSRG
.IFTF
	RETURN
;
IVT:	TSTB	@#IRPSRG
	BPL	IVT
	MOVB	@#IRPSRG+2,R0
	RETURN
ISVT:	TSTB	@#IRPSRG+4
	BPL	ISVT
	MOVB	R0,@#IRPSRG+6
	RETURN
;
.IFT
NET:	BIS	#100,@#177660
SHO:	MOV	#TRTR,@#IRPSW
	MOV	#ISVT,EKRHUK
	BR	GR
TRTR:
	MOV	R0,-(SP)
	MOVB	@#IRPSRG+2,R0
	CMPB	R0,#6
	BEQ	BG
	MOVB	R0,@#104
	MOVB	R0,@#105
	MOV	@#TSTCHR,R0
	BEQ	GR
	CALL	@R0
GR:	MOV	(SP)+,R0
	RTI
BG:	
	CLR	@#IRPSRG+4
	MOV	#IRPS1,@#IRPSW
	BIC	#100,@#177660
	CLR	EKRHUK
	BR	GR
COPY:	
	MOV	#40000,COPPNT
	MOV	#CPTR,@#IRPSW+4
	BIS	#100,@#IRPSRG+4
	MOVB	@#177664,@#IRPSRG+6
	BR	SHO
CPTR:	CMP	COPPNT,#40000
	BLO	1$
	MOVB	@COPPNT,@#IRPSRG+6
2$:	INC	COPPNT
	CMP	COPPNT,#100000
	BLO	3$
	MOV	#37777,COPPNT
3$:	RTI
1$:	MOVB	@#177664,@#IRPSRG+6
	BR	2$
.ENDC
.IFT
;prodolvenie w drugom fajle!!!!
