	.TITLE	MONIT
	.ENABLE LC

	; ###################
	; #                 #
	; #   BASIC - VVU   #
	; #                 #
	; ###################

	; ИСПОЛНИТЕЛЬ: ВЦКП ВГУ
	; ВИЛЬНЮС,1987

	; ===========================
	; ****СИСТЕМНЫЕ КОНСТАНТЫ****
	; ===========================

	BREAKW==4	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ПО КЛАВИШЕ "СТОП"
	ILCODW=10
	TRAPW=34	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ПО ИНСТРУКЦИИ TRAP

.IF EQ,MASINA-MDS
	TIMER=177546	;АДРЕС CSR ТАЙМЕРА
.IFF
	TIMER==100	;АДРЕС ВЕКТОРА ПРЕРЫВАНИЙ ТАЙМЕРА
.ENDC

.IIF EQ,MASINA-BK0010	AUSROM==160000

.IF EQ,MASINA-BK0011
.IF EQ,MEMTYP-RAM
	PAG1==2
	PAG2==3
.IFF
	PAG1==11
	PAG2==10
.ENDC
	SYSPAG==7
	RAMPAG=1
	HS=6

	ID.==104000
	ID$==0
	INIT$==2
	STMT$==4
	DEV$==6
	OPFND$==10
.IFF
	HS=3
.ENDC

.IF EQ,MASINA-UK
	ABRKW=070074	;ВЕКТОР ОБРАБОТКИ "СТОП": 160170/2
	AMEM=176640	;РЕГИСТР АДРЕСА ДОСТУПА КО ВСЕЙ ПАМЯТИ
	DMEM=176642	;РЕГИСТР ДАННЫХ 
.ENDC

.IF NE,MASINA-UKM
	WR.==2
	RD.==3
	FR.==4
	NER.==1
	RER.==2
	MER.==3
	SER.==4
	FER.==5
	ADRW==2
	LENW==4
.IF EQ,MASINA-BK0011
	MAGSIZ==54
	NAMEW==6
	ADRR==30
	LENR==32
	NAMER==34
	PAGW==26
.IFF
	MAGSIZ==52
	NAMEW==6
	ADRR==26
	LENR==30
	NAMER==32
.ENDC
.IFF
	MAGSIZ==54	;30
	WR.==20
	RD.==10
	FR.==14		;30
	NER.==1
	MER.==2
	FER.==3
	RER.==10
	SER.==-1

	NAMEW==4	;10
	ADRW==24	;4
	LENW==26	;6
	NAMER==30	;10
	ADRR==50	;4
	LENR==52	;6
.ENDC

	; ========================
	; ****МАКРООПРЕДЕЛЕНИЯ****
	; ========================

.MACRO	.KOI87	STRING
	.IRPC	A,<STRING>
.IF LT,''A-100
	.BYTE	''A
.IFF
	.BYTE	200+''A
.ENDC
	.ENDM
.ENDM

.IF EQ,KLAS-UK
	NETON=1
	NETOFF=2
	RFRAME=3
	WFRAME=4
	RCANCL=5
	WCANCL=6
.MACRO	CALLMC	N
	MOV	@#IRPSW,R2
	ADD	-2*N(R2),R2
	CALL	@R2
.ENDM	CALLMC
.ENDC

.IIF EQ,MASINA-DISK	.MCALL	.SETTOP,.EXIT,.PRINT,.SERR,.SCCA

.IF EQ,MASINA-BK0011
.MACRO	.BJSR0
	EMT	54
.ENDM	.BJSR0
.ENDC

.MACRO	.BJMP	N
.IF EQ,MASINA-BK0011
	MOV	#PAG2,-(SP)
	MOV	#N,-(SP)
	EMT	56
.IFF
	JMP	N
.ENDC
.ENDM	.BJMP

.MACRO	.BJSR	N
.IF EQ,MASINA-BK0011
	MOV	#PAG2,-(SP)
	MOV	#N,-(SP)
	.BJSR0
.IFF
	CALL	N
.ENDC
.ENDM	.BJSR

.IF EQ,MASINA-BK0011
.MACRO	BRK$
	MOV	#STACK,SP
	MOV	#PAG1,-(SP)
	MOV	#BREAK,-(SP)
	EMT	56
.ENDM

.MACRO	ILCD$
	TRAP	51.
.ENDM

.MACRO	TRP$
	MOV	R0,-(SP)
	MOV	R2,-(SP)
	MOV	4(SP),R0
	MOV	-2(R0),R2
	BIC	#177400,R2
	SUB	#100,R2
	BLT	1$

	MOV	R0,-(SP)
	MOV	#PAG1,-(SP)
	ASL	R2
	ADD	#ATRAP,R2
	MOV	R2,-(SP)
	EMT	60
	MOV	R0,-(SP)
	MOV	2(SP),R0
	MOV	#PAG1,2(SP)
	MOV	6(SP),R2
	BR	2$

1$:	MOV	#PAG1,-(SP)
	MOV	#ERROR,-(SP)

2$:	.BJSR0
	MOV	R0,4(SP)
	MOV	(SP)+,R2
	MOV	(SP)+,R0
.ENDM	TRP$

.MACRO	RTI$
	RTI
.ENDM

.IF EQ,1
.MACRO	IOT$
	MOV	@#66,-(SP)
	MOV	R0,-(SP)

	MOV	#400+PAG2,R0
	EMT	52

	MOV	4(SP),R0
	BIC	#3,R0

	JSR	R5,SREG

	MOV	R0,R1
	MOV	@#VCTNMB,R0

1$:	BEQ	2$
	CALL	@#TSTVCT
	BCS	2$
	CMP	-2(R0),R1
	BEQ	3$
	MOV	@R0,R0
	BR	1$

3$:	MTPS	#0
	CALL	2(R0)
	JSR	R5,RREG
	MOV	(SP)+,R0
	MOV	@SP,@#ASPORT
	MOV	(SP)+,@#66
	RTI
2$:	TRAP	51.
.ENDM
.ENDC
.ENDC
	; ============================
	; ****СИСТЕМНЫЕ ПЕРЕМЕННЫЕ****
	; ============================

	.ASECT
	.=STACK

LYGIS::	.BLKW	1	;РЕЖИМ РАБОТЫ СИСТЕМЫ
TXEND::	.BLKW	1	;МЕТКА КОНЦА ТЕКСТА И НАЧАЛА ОБ'ЕКТНОГО КОДА
STRREG::.BLKW	1	;МЕТКА НАЧАЛА ОБЛАСТИ СТРОК
STRSIZ::.BLKW	1	;УКАЗАТЕЛЬ ДЛИНЫ ОБЛАСТИ СТРОК
FREBEG::.BLKW	1	;МЕТКА СВОБОДНОЙ ЧАСТИ ОБЛАСТИ СТРОК
FRELEN::.BLKW	1	;ТЕКУЩАЯ ДЛИНА СВОБОДНОЙ ЧАСТИ ОБЛАСТИ СТРОК
ENDCOD::.BLKW	1	;МЕТКА НАЧАЛА СВОБОДНОЙ ОБЛАСТИ
			;И КОНЦА ОБ'ЕКТНОГО КОДА
CIKL::	.BLKW	1	;МЕТКА НАЧАЛА СТЕКА ЦИКЛОВ
LIMIT::	.BLKW	1	;УКАЗАТЕЛЬ ВЕРШИНЫ СТЕКА ПОДПРОГРАММ
LENT::	.BLKW	1	;МЕТКА НАЧАЛА ТАБЛИЦЫ ИМЕН
TABTOP::.BLKW	1	;МЕТКА КОНЦА ТАБЛИЦ СВЯЗИ И ИМЕН
FCB::	.BLKW	1	;МЕТКА НАЧАЛА ОБЛАСТИ ФАЙЛОВ И ТАБЛИЦЫ СВЯЗИ
DFCB::	.BLKW	1	;МЕТКА КОНЦА СИСТЕМНОГО БУФЕРА
HIMEM::	.BLKW	1	;УКАЗАТЕЛЬ ВЕРХНЕГО АДРЕСА, ДОСТУПНОГО БЕЙСИКУ

ERRS::	.BLKW	1	;ПРИЗНАК ЗАПРЕТА ВЫПОЛНЕНИЯ ПРОГРАММЫ
TYPE::	.BLKW	1	;ТИП КОМПИЛИРУЕМОГО ВЫРАЖЕНИЯ
DATBEG::.BLKW	1	;УКАЗАТЕЛЬ НАЧАЛА БУФЕРА DATA
DATPTR::.BLKW	1	;ТЕКУЩИЙ УКАЗАТЕЛЬ БУФЕРА DATA
DATINP::.BLKW	1	;ФЛАГ READ/INPUT
NUMBER::.BLKW	1	;НОМЕР КОМПИЛИРУЕМОЙ СТРОКИ
ADRHLT::.BLKW	1	;АДРЕС ОСТАНОВА ПРОГРАММЫ
TRFLAG::.BLKW	1	;ФЛАГ РЕЖИМА ТРАССИРОВКИ
AUTOON::.BLKW	1	;ФЛАГ РЕЖИМА AUTO
AUINC::	.BLKW	1	;AUTO ПРИРАЩЕНИЕ
CURLIN::.BLKW	1	;УКАЗАТЕЛЬ ТЕКУЩЕЙ СТРОКИ
VCTNMB::.BLKW	1	;ЧИСЛО ЗАДЕЙСТВОВАНЫХ ON TRAP
;ONERFL::.BLKW	1	;ФЛАГ ON ERROR

.IF DF,SNG
III::	.BLKW	2	;РАБОЧИЕ ЯЧЕЙКИ ФУНКЦИИ RND
RNDSAV::.BLKW	2	;ТЕКУЩЕЕ ЗНАЧЕНИЕ RND
.IFF
III::	.BLKW	4
RNDSAV::.BLKW	4
.ENDC

;MAXFIL::.BLKW	1	;МАКСИМАЛЬНОЕ КОЛИЧЕСТВО ФАЙЛОВ ДАННЫХ

USRTAB::		;ТАБЛИЦА АДРЕССОВ USR
.IF EQ,MASINA-BK0011
	.BLKW	20.
.IFF
	.BLKW	10.
.ENDC

;APRL::	.BLKW	15	;ТАБЛИЦА НЕЯВНОГО ОПРЕДЕЛЕНИЯ ТИПОВ
PNTSAV::.BLKW	1	;РАБОЧАЯ ЯЧЕЙКА ШАГА
PFACT::	.BLKW	2	;УКАЗАТЕЛЬ БЛОКА ПАРАМЕТРОВ ДЛЯ FN
TMP::	.BLKW	1	;УКАЗАТЕЛЬ ВРЕМЕННЫХ ДЕСКРИПТОРОВ
SAVJMP::.BLKW	1	;АДРЕС КОДА ВЫПОЛНЯЕМОЙ ПРОГРАММЫ
$SCRT::	.BLKW	1	;РАБОЧАЯ ПЕРЕМЕННАЯ DRAW
LPTPOS::.BLKW	1	;СЧЕТЧИК ГОЛОВКИ ПРИНТЕРА
LPLCFL::.BLKW	1	;ПРИЗНАК РУССКОГО РЕГИСТРА ПРИНТЕРА

.IF EQ,MASINA-BK0010
FNKSTR::.BLKB	170.	;БУФЕР ТЕКСТОВ ФУНКЦИОНАЛЬНЫХ КЛАВИШ
.ENDC

.IF GT,MASINA-BK0010
LL::	.BLKW		; ДЛИНА СТРОКИ ЭКРАНА
MIDLE::	.BLKW		; СМЕЩЕНИЕ СТРОК К ЦЕНТРУ ЭКРАНА
ENDBUF::.BLKW		; КОНЕЦ ЭКРАННОГО БУФЕПА
KOM2P::	.BLKB		; ПРИЗНАК КОДОВ ИЗ 2-ОЙ КОМ.СИСТ.
SCRL::	.BLKB		; ПРИЗНАК "ПЕРЕМОТКИ" ЭКРАНА
CURS::	.BLKW		; МЕСТО КУРСОРА В БУФЕРЕ
TABX::	.BLKW		; МЕСТО 
TABY::	.BLKW		;       КУРСОРА НА ЭКРАНЕ
FIRST::	.BLKW
LAST::	.BLKW
RUSLAT::.BLKW
.ENDC

.IF EQ,MASINA-UK
GRPEN::	.BLKW
GRPAP::	.BLKW
CHRBOX::.BLKW
.ENDC

.IF EQ,<MASINA-BK0011>*<MASINA-DVK3>*<MASINA-VUMS>
$DRWX::	.BLKW	1
$DRWY::	.BLKW	1
CHAR0::	.BLKW	1
.ENDC

.IF EQ,<MASINA-BK0011>*<MASINA-DVK3>*<MASINA-VUMS>*<MASINA-UK>
CHAR::	.BLKW	1
PAPER::	.BLKW	1
CRTGRP::.BLKW	1
.ENDC


.IF EQ,MASINA-BK0011
VIRSCR::.BLKW	1
VIRSTA::.BLKW	1
VIREND::.BLKW	1
.ENDC

.IF GT,MASINA-BK0010
;SIIKP::	.BLKB	1		;ПРИЗНАК ЗАПИСИ КОДА ИЗ КЛАВИАТУРЫ
;RLPOZ::	.BLKB	1		;ПРИЗНАК РУС. ЛАТ.
.IF EQ,VM-2				;ХРАНЕНИЕ ВЕКТОРОВ ПРЕРЫВАНИЯ
SAUG::	.BLKW	14			;И КОМАНД ПЕРЕЗАПУСКА СИСТЕМЫ
.IFF
SAUG::	.BLKW	12
.ENDC
.ENDC

INK::	.BLKW	1			;БУФЕР INKEY
WRITE::	.BLKW	1			;АДРЕС ДРАЙВЕРА
IODEV:: .BLKW	1			;НОМЕР ТЕКУЩЕГО ФАЙЛА

.IF EQ,MASINA-UK
BRKFLG::.BLKW	1			;ФЛАГ РАЗРЕШЕНИЯ BREAK
.ENDC

.IF NE,MASINA-BK0010
PARAM::	.BLKW	5			;БЛОК ПАРАМЕТРОВ ДРАЙВЕРОВ
.IIF EQ,MASINA-BK0011	.BLKW	3
MAG::	.BLKW	MAGSIZ/2
.ENDC

.IF EQ,MASINA-DISK
INCNMB::.WORD	0
ASCFLG::.BYTE	0
ENDFLG::.BYTE	0
BLKCNT::.WORD	0
BUFCNT::.WORD	0
WSCCA:	.WORD	0
.ENDC

.IF EQ,MASINA-BK0011
BRKO::	BRK$
ILCDO::	ILCD$
TRPO::	TRP$
RTIO::	RTI$
.ENDC

;---------------------------------------------------------------
;        ВЕКТОРА ПЕРЕХВАТА НЕКОТОРЫХ СИСТЕМНЫХ ДЕЙСТВИЙ
;---------------------------------------------------------------
.IF DF,HOOKS
HOOK:
HSYS::	.BLKW	HS	;ВОЗВРАТ ИЗ БЕЙСИКА В СИСТЕМУ
HINIT::	.BLKW	HS	;ИНИЦИАЛИЗАЦИЯ ВСЕХ ДРАЙВЕРОВ
HINKEY::.BLKW	HS	;ВВОД СИМВОЛА С КЛАВИАТУРЫ БЕЗ ОЖИДАНИЯ
HGETCH::.BLKW	HS	;ВВОД СИМВОЛА С КЛАВИАТУРЫ С ОЖИДАНИЕМ
HGETLN::.BLKW	HS	;ВВОД СТРОКИ С КЛАВИАТУРЫ
HKEY::	.BLKW	HS	;ОПРЕДЕЛЕНИЕ ЗНАЧЕНИЙ ФУКЦИОНАЛЬНЫХ КЛАВИШЕЙ
HPUTCH::.BLKW	HS	;ВЫВОД СИМВОЛА НА ЭКРАН
HPUTLN::.BLKW	HS	;ВЫВОД СТРОКИ НА ЭКРАН
HEDIT::	.BLKW	HS	;РЕДАКТИРОВАНИЕ СТРОКИ
HLOCAT::.BLKW	HS	;УСТАНОВКА ПОЛОЖЕНИЯ КУРСОРА 
HLOOK::	.BLKW	HS	;ЧТЕНИЕ ПОЛОЖЕНИЯ КУРСОРА
HCURON::.BLKW	HS	;ПОКАЗ КУРСОРА
HCUROF::.BLKW	HS	;ГАШЕНИЕ КУРСОРА
HLPR::	.BLKW	HS	;ВЫВОД СТРОКИ В ПРИНТЕР
HLPOS::	.BLKW	HS	;ЧТЕНИЕ ПОЛОЖЕНИЯ ГОЛОВКИ ПРИНТЕРА
HCAS::	.BLKW	HS	;ВЫЗОВ ДРАЙВЕРА МАГНИТОФОНА
HMESS::	.BLKW	HS	;ВЫВОД СИСТЕМНЫХ СООБЩЕНИЙ: СТОП,В СТРОКЕ,OK
HERR::	.BLKW	HS	;ВЫВОД СООБЩЕНИЙ ОБ ОШИБКАХ
HLIST::	.BLKW	HS	;ВЫВОД СТРОК ЛИСТИНГА ПРИ LIST
HRUN::	.BLKW	HS	;ПЕРЕХОД НА ВЫПОЛНЕНИЕ ПРОГРАММЫ
HEND::	.BLKW	HS	;КОНЕЦ ВЫПОЛНЕНИЯ ПРОГРАММЫ
HCLEAR::.BLKW	HS	;ОПЕРАТОР CLEAR
HDFUSR::.BLKW	HS	;ОПЕРАТОР DEFUSR
HINTR::	.BLKW	HS	;ПЕРЕХВАТ НЕОБРАБАТЫВАЕМЫХ ПРЕРЫВАНИЙ
HPOKE::	.BLKW	HS	;ОПЕРАТОРЫ POKE И OUT СО СКРЫТЫМИ СТРАНИЦАМИ
ENDH:
.ENDC

;-------------------------------------------------------------------
.IF GT,MASINA-BK0010
EBUF::	.BLKB	30*120	;БУФЕР ДЛЯ ЭКРАННОГО РЕДАКТОРА
.ENDC
BUF::	.BLKB	400	;БУФЕР ВВОДА
BUFOUT::.BLKB	30	;БУФЕР ВЫВОДА
BEND::
.IF EQ,MASINA-DISK
BUFFIL::.BLKW	400
.ENDC

.IF EQ,MASINA-BK0010
.IF EQ,MEMTYP-ROM
TEXT::	.BLKW	1	;ОБЛАСТЬ ПРОГРАММЫ ПОЛЗОВАТЕЛЯ
.ENDC
.ENDC

.IF EQ,MASINA-BK0011
TEXT::
.ENDC

	; ====================
	; ****ПУСК СИСТЕМЫ****
	; ====================

.IF LE,MASINA-BK0010
.IFTF
.GLOBL	TEXT,MAGSIZ,MIDM,IRPSRG
.GLOBL	BC,CASCII,BLOCKN,FNDBUF,TRTOPL
.GLOBL	TOINT,SETST,CAS,NEXINT,NEXJMP,JMP,RTS,LNUM,ISVARD
.GLOBL	STEP,SEPAP,NOUSR,IRPSW
.IIF EQ,MASINA-DISK	.GLOBL	SYSPTR,USRLOC,JSW
.IIF EQ,VM-2	.GLOBL	FERRI
.IIF EQ,MASINA-UK	.GLOBL	BRUK
.IIF EQ,MASINA-DVK3	.GLOBL	GRC

.IFF
.GLOBL	FILTYP,AVPK,ISV1,IVS

.IFT
.GLOBL	LOOKXY,KINIT

.IFTF
.IF DF,SNG
.GLOBL	KOS,SING,IR,RI,MLR
.IFF
.GLOBL	$B93,$B95,ID,DI,SI
.ENDC

.IIF EQ,MASINA-UK	.GLOBL	MIG,KBON,NRCTR
.IIF NE,MASINA-BK0010	.GLOBL	SETCRT

.IF EQ,MASINA-BK0011
.GLOBL	ATRAP,ERROR,USRC
.IFF
.GLOBL	TRAP
.ENDC

.GLOBL	CURON,CUROFF
.GLOBL	ISV
.GLOBL	WSTOP,MONERR,AOPEND,AOPMON,AOPSUB,OPRNAM,AOPNAM
.GLOBL	STOPEN,NOSTOP,TSTVCT,RSTVCT

.IFF
.GLOBL	CURS
.IFT
.GLOBL	CALL

.IFTF
.GLOBL	ENDPRG
.GLOBL	ISVEIL,IVEIL
.GLOBL	RESTV,ISVETO,TMP,DFCB
.GLOBL	CLOALL,TABMOV,PFACT

.IF EQ,MASINA-BK0011
.GLOBL	PAG2,ENDFRE,STARP,OPFND,HALT
.IFF
.GLOBL	RWREN,ISVSS
.ENDC

.IFF
.GLOBL	INBF1,FIRST,LAST
.IFT
.IF NE,MASINA-BK0011
.GLOBL	PIJ
.ENDC
.IFTF
;
;----------------------------
;
	.ASECT
.IF EQ,MASINA-BK0011
.=100000
.IFF
.IIF EQ,MEMTYP-ROM	.=MEMTYP
.ENDC

START::

.IF EQ,MASINA-BK0010
.IF EQ,1		;MEMTYP-ROM
	MOV	#100000,R1	;CHECK SUM
	MOV	#3,R3
4$:	MOV	#10000,R2
	CLR	R0
3$:	ADD	(R1)+,R0
	ADC	R0
	SOB	R2,3$
	CMP	R0,#177777
	BEQ	2$
	RETURN
2$:	SOB	R3,4$
.ENDC
.ENDC

.IFTF
	MOV	#STACK,SP
.IFF
	MOV	#SAUG,R0
	CLR	R1
	MOV	#6,R2
6$:	MOV	(R1)+,(R0)+
	SOB	R2,6$
	MOV	@#AVPK,(R0)+
	MOV	@#AVPK+2,(R0)+

.IF EQ,VM-2
	MOV	@#244,(0)+
	MOV	@#246,(0)+
.ENDC

.IF EQ,MASINA-UK
	CLR	@#BRKFLG		;ЗАПРЕТИТЬ BREAK
	MOV	#ABRKW,@#AMEM
	MOV	@#DMEM,(0)+
	MOV	#BRUK,@#DMEM
	INC	@#AMEM
	MOV	@#DMEM,(0)+
	MOV	#600,@#DMEM
.ENDC
	CLR	RUSLAT

.IFTF
.IF EQ,MASINA-BK0011
	MOV	#100000,@#HIMEM
.IFF
.IF	EQ,MEMTYP-ROM
	MOV	#MVIDEO,HIMEM
.IFF
.IF EQ,MASINA-DISK
	MOV	@#SYSPTR,R0
	MOV	USRLOC(R0),R0
	MOV	R0,HIMEM
	TST	-(R0)
	.SETTOP	R0
	.SERR
	.SCCA	#PARAM,#WSCCA
	BIS	#10000,@#JSW
	CLR	@#ASCFLG
.IFF
.IF EQ,MASINA-BK0010
	MOV	#70000,@#HIMEM
.IFF
	MOV	#MEMTYP,HIMEM
.ENDC
.ENDC
.ENDC
.ENDC

	CLR	TRFLAG
	CLR	LPTPOS
	MOV	#200.,STRSIZ
	MOV	#AUINC,R0
	MOV	#12,(0)+
	MOV	#12,(0)+
	CLR	(R0)+
;	CLR	(R0)+
	MOV	R0,R1

.IF DF,SNG
	MOV	#40101,(0)+
	MOV	#27076,(0)+
	MOV	(1)+,(0)+
	MOV	(1)+,(0)+
.IFF
	MOV	#40101,(0)+
	MOV	#44122,(0)+
	MOV	#37502,(0)+
	MOV	#52125,(0)+
.REPT	4
	MOV	(1)+,(0)+
.ENDR
.ENDC

;	MOV	#1,(R0)+	;MAXFIL <- 1
	MOV	#10.,R1
1$:
.IIF EQ,MASINA-BK0011	MOV	#PAG2,(0)+
	MOV	#NOUSR,(0)+	;НЕОПРЕДЕЛЕННЫЕ ФУНКЦИИ USR
	SOB	R1,1$

;	MOV	#15,R1		;НЕЯВНЫЙ ТИП
;.IF DF,SNG
;5$:	MOV	#1002,(R0)+
;.IFF
;5$:	MOV	#2004,(R0)+
;.ENDC
;	SOB	R1,5$
;
;	CLR	(R0)+		;STOPAR
	CLR	PNTSAV

.IF DF,HOOKS
	MOV	#HOOK,R0
	MOV	#<ENDH-HOOK>/2,R1
7$:	MOV	#207,(0)+	;RETURN FROM HOOK
	SOB	R1,7$
.ENDC

.IF EQ,MASINA-BK0011
	MOV	#TRBEG$,R0
	MOV	#<TREND$-TRBEG$>/2,R1
	MOV	#BRKO,R2
8$:	MOV	(R0)+,(R2)+
	SOB	R1,8$
	BR	TREND$
TRBEG$:	BRK$
	ILCD$
	TRP$
	RTI$
TREND$:
.ENDC

	CALL	MIDM			;ИНИЦИАЛИЗАЦИЯ УСТРОЙСТВ
	CLR	@#VCTNMB
	CALL	CLRTXT

.IF EQ,MASINA-BK0011			;ИНИЦИАЛИЗАЦИЯ СТРАНИЦ РАСШИРЕНИЙ
	MOV	#100000+INIT$,R5	;АДРЕС ВЫЗОВА В СТРАНИЦЕ
	MOV	#2,R1			;2 СТРАНИЦЫ ROM: 13 И 12
	MOV	#11,R2
1$:	.BJSR	USRC
	BCS	11$
	SOB	R1,1$			;НЕ ВСЕ ИНИЦИАЛИЗИРОВАНЫ
11$:	MOV	#3,R1			;3 СТРАНИЦЫ RAM: 4, 3, 2
	MOV	#1,R2
2$:	.BJSR	USRC
	BCS	22$
	SOB	R1,2$

22$:	MOV	#140000,@#VIRSTA
	MOV	#3,R1
23$:	MOV	R1,-(SP)
	INC	@SP
	MOV	#100000+ID$,-(SP)
	EMT	60
	CMP	R0,#ID.
	BEQ	24$
	SUB	#40000,@#VIRSTA
	SOB	R1,23$
24$:	MOV	@#VIRSTA,@#VIREND
.ENDC

.IFT
	CALL	KINIT

.IFTF
TOSETW:	JMP	SETW

.IFT
SUM1::	.WORD	0

.IFF
INIT:	MOV	#STACK,SP
	JSR	R2,SEPAP
	CALL	MIDM
	MOV	#14,R0
	CALL	ISV
	BR	TOSETW

.IFTF
CTRLC::	TST	LYGIS
	BMI	BREAK
	MOV	#STACK,SP
	MOV	#12,R0
	CALL	ISV
	JMP	STAR

.IFTF
ADDRMN::TRAP	106
	CALL	WRESET
.IIF DF,HOOKS	CALL	HSYS
	MOV	#14,R0
	CALL	ISV
	CALL	CURON
.IIF NE,MASINA-BK0010	.BJSR	SETCRT
.IIF EQ,MASINA-DVK3	CLR	@#GRC

.IFT
.IF EQ,MASINA-BK0011
	EMT	0
.IFF
	MOV	#1000,SP
	JMP	@#100274
.ENDC

.IFF
.IF EQ,MASINA-MDS
	COM	@#TIMER
.ENDC
	MOV	#SAUG,R0
	CLR	R1
	MOV	#6,R2
1$:	MOV	(R0)+,(R1)+
	SOB	R2,1$
	MOV	(R0)+,@#AVPK
	MOV	(R0)+,@#AVPK+2
.IF EQ,VM-2
	MOV	(0)+,@#244
	MOV	(0)+,@#246
.ENDC

.IF EQ,MASINA-UK
	MOV	#ABRKW,@#AMEM
	MOV	@#SAUG+20+<<VM-1>*4>,@#DMEM
	INC	@#AMEM
	MOV	@#SAUG+22+<<VM-1>*4>,@#DMEM
.ENDC
.IF EQ,MASINA-DISK
	.PRINT	#LC1
	.EXIT
.IFF
	HALT
.ENDC
.IFTF
.IF	NE,MASINA-BK0011
BRKO::
.ENDC
BREAK::
	CALL	NOSTOP
	MOV	#STACK,SP
.IIF EQ,MASINA-BK0011	CALL	PAGSET

.IF EQ,KLAS-UK
.IF EQ,MASINA-NET
	MOV	#PARAM+10,R1	;ОТКЛЮЧЕНИЕ ОТ СЕТИ
	CLR	-(R1)
	CLR	-(R1)
	MOV	#NRCTR,-(R1)
	CLR	-(R1)
	CALLMC	NETON
	MOV	R1,R0
	CALLMC	WFRAME
	BCC	5$
	MOV	R1,R0
	CALLMC	RCANCL
	BCC	5$
	MOV	R1,R0
	CALLMC	WCANCL
	BCS	6$
5$:	TST	@R1
	BPL	5$
6$:	CALLMC	NETOFF
7$:
.ENDC
.ENDC

	CLR	@#IODEV

.IFF
	MOV	FIRST,LAST
	MOVB	RUSLAT+1,RUSLAT

.IF	EQ,MASINA-UK
	TST	CRTGRP
	BEQ	13$
	MOV	#15,R1
	CLR	R0
12$:	CALL	ISV1
	SOB	R1,12$
	CALL	KBON
13$:
.ENDC

.IFT
.IF NE,MASINA-BK0011
	EMT	34
	BIT	#400,R0
	BEQ	1$
	MOV	#225,R0
	CALL	ISV
1$:	CALL	ISVSS
.ENDC

.IFTF
.IIF NE,MASINA-BK0010 	.BJSR	SETCRT

	MOV	#WSTOP,R1
	MOV	#5,R2
.IIF DF,HOOKS	CALL	HMESS
	CALL	ISVEIL
	TST	LYGIS
	BGE	2$
	TST	SAVJMP
	BEQ	3$
	MOV	SAVJMP,R4
	CLR	SAVJMP
3$:	CMP	R4,STRREG
	BHI	2$
	CMP	@SP,#NEXINT+24
	BEQ	10$
	CMP	@SP,#NEXJMP+22
	BEQ	10$
	CMP	@SP,#JMP+2
	BEQ	10$
	CMP	@SP,#RTS+14
	BLO	11$
	CMP	@SP,#RTS+26
	BHI	11$
	BEQ	10$
	TST	(4)+
10$:	TST	(4)+
11$:	CALL	LNUM
	MOV	-(4),ADRHLT
.IIF DF,HOOKS 	CALL	HEND
	BR	4$
2$:	MOV	#12,R0
	CALL	ISV
4$:	JSR	R2,SEPAP
	CALL	TRTOPL
   	CLR	LYGIS

SETW::					;УСТАНОВКА ВЕКТОРОВ ПРЕРЫВНИЙ
	CALL	NOSTOP

.IF EQ,MASINA-BK0011
	MOV	#TRPO,@#TRAPW
	CALL	PAGSET
.IFF
	MOV	#TRAP,@#TRAPW
.ENDC
	CLR	@#TRAPW+2

	MOV	#ILCODW,R0
	.BJSR	TSTVCT
	BCC	1$
	MOV	#ILCDO,@R0
	CLR	@#ILCODW+2
1$:
.IFF
	MOV	#137,@#0
	MOV	#INIT,@#2

	MOV	#BREAKW,R0
	.BJSR	TSTVCT
	BCC	2$
	MOV	#BRKO,@R0
	CLR	@#BREAKW+2
2$:
	MOV	#AVPK,R0
3$:	.BJSR	RSTVCT
	BCC	3$
	MOV	#IVS,@R0
	MOV	#340,@#AVPK+2
.IFTF

.IF EQ,VM-2
	MOV	#244,R0
	.BJSR	TSTVCT
	BCC	4$
	MOV	#FERRI,@R0
	CLR	@#246
4$:
.ENDC
	CALL	STOPEN
	BR	STAR		;ПЕРЕХОД НА БЕЙСИК МОНИТОР

MONIT::	CLR	ADRHLT

	; ======================================
	; ***БЛОК ВВОДА ДИРЕКТИВ ПОЛЬЗОВАТЕЛЯ***
	; ======================================

STAR::
AUTOCL:	CLR	AUTOON
	CLR	LYGIS		;ПРИЗНАК РАБОТЫ МОНИТОРА
.IIF NE,MASINA-BK0010	.BJSR	SETCRT
	MOV	#OK,R1		;ВЫВОД OK
	MOV	#3,R2
.IIF DF,HOOKS	CALL	HMESS
	CALL	ISVEIL
	CALL	CURON
PRGLIN:	TST	AUTOON
	BNE	AUNEXT
NEXTL:	CALL	IVEIL		;ВВОД СТРОКИ
	;
	; --------------------------
	; ...АНАЛИЗ СТРОКИ...
	; --------------------------
	;
ANALIZ::
.IFF
	CALL	INBF1
	BCC	NEXTL
.IFTF
	MOV	#BUF,R3
.IFT
	CMPB	@R3,#'.
	BNE	2$
	INC	R3
	JMP	EDIT
.IFTF
2$:	JSR	R1,ASCCOD
	BR	COMAND
	BR	PRGLIN
;
AUNEXT:	MOV	CURLIN,R5
	ADD	AUINC,R5
	BCS	AUTOCL
	JMP	EDAUIN
;
COMAND: CALL	CUROFF
	MOV	SP,R0
	CMP	R5,#AOPMON-AOPSUB
	BLT	1$
.IF EQ,MASINA-BK0011
	MOV	R0,-(SP)
	MOV	#PAG2,-(SP)
	MOV	@SP,-(SP)
	MOV	#AOPSUB,-(SP)
	ADD	R5,@SP
	EMT	60
	MOV	R0,-(SP)
	MOV	2(SP),R0
	MOV	(SP)+,@SP
	JMP	@(SP)+
.IFF
	JMP	@AOPSUB(R5)
.ENDC
1$:	JMP	STATM

.IF EQ,MASINA-BK0011
PAGSET::MOV	#RAMPAG,R0
	EMT	52
	RETURN
.ENDC

.IFTF
.IF EQ,MASINA-UK
TSTBRK::
.ENDC

.IF NE,MASINA-BK0011
RTIO::	RTI
ILCDO:	TRAP	51.
.ENDC

	; =============================
	; ***ОБЛАСТЬ ДАННЫХ МОНИТОРА***
	; =============================

BASVU:	.BYTE	14			;НАЧАЛЬНОЕ СООБЩЕНИЕ
	.KOI87	< БЕЙСИК >
.IIF EQ,MASINA-UK	.KOI87	<УК-НЦ>
.IIF EQ,MASINA-DVK3	.KOI87	<ДВК-3>
.IIF EQ,MASINA-VUMS	.KOI87	<ДВК + ЦДР>
.IIF EQ,MASINA-DVK2	.KOI87	<ДВК>
.IIF EQ,MASINA-DVK2B	.KOI87	<ДВК СЕТЕВОЙ>
.IFT
	.KOI87	<БК001>
.IF EQ,MASINA-BK0010
	.BYTE	'0
.IFF
	.BYTE	'1
.ENDC
.IFTF
	.BYTE	12
	.KOI87	< ВИЛЬНЮС, 1988.03.14>
.IF EQ,<MASINA-DVK2>*<MASINA-DVK2B>*<MASINA-VUMS>*<MASINA-DVK3>
	.KOI87	< (ОПЫТ.)>
.ENDC
	.BYTE	12
.IF EQ,MASINA-BK0010
.IIF NE,MEMTYP-RAM	.BYTE	12
.IFF
	.BYTE	12
.ENDC

WSTOP::
	.BYTE	12
	.KOI87	<СTOP>
	.BYTE	12

ATLINE: .KOI87	< W STROKE >

OK:
	.ASCII	/OК/<12>
.IF EQ,MASINA-DISK
LC1:	.BYTE	33,'Y,'7,' ,200
.ENDC
.EVEN

;--------------------------------
;

.IF EQ,MASINA-BK0011
ENDST::
.IFF
ENDPRG::.BJSR	CLOALL
.IFTF
	INC	LYGIS
	BNE	END1
	JSR	R2,SEPAP
	CLR	ADRHLT
	CALL	TRTOPL
END1:
.IF DF,HOOKS
	CALL	HEND
.ENDC
	JMP	STAR
;
.IFF
HALT::
.IFT
HALTST::
.ENDC
.IF NE,MASINA-BK0010
	.BJSR	SETCRT
.ENDC
	MOV	R4,SAVJMP
	MOV	#WSTOP,R1
	MOV	#5,R2
.IF DF,HOOKS
	CALL	HMESS
.ENDC
	CALL	ISVEIL
	CMP	R4,STRREG
	BHI	3$
	MOV	R4,ADRHLT
2$:	JSR	R2,SEPAP
	CALL	TRTOPL
	CALL	LNUM
	CMP	-(R4),-(R4)
	JSR	R0,SETST
	BR	1$
	CLR	ADRHLT
1$:	CLR	SAVJMP
	BR	END1
3$:	CLR	SAVJMP
.IF DF,HOOKS
	CALL	HEND
.ENDC
	JMP	MONERR
;
TRTOPL::TST	TRFLAG
	BPL	1$
	NEG	TRFLAG
1$:	RETURN
;
LNUM::	CALL	AGAUD
	MOV	2(0),R5
ATLIN1::MOV	#ATLINE,R1
	MOV	#12,R2
.IF DF,HOOKS
	CALL	HMESS
.ENDC
	CALL	ISVEIL
	MOV	R5,R2
	TRAP	114
	MOV	#12,R0
	CALL	ISV
	RETURN
;
AGAUD:: MOV	TABTOP,R0
3$:	CMP	(R0)+,R4
	BLO	4$
	CMP	(R0)+,(R0)+
	CMP	R0,FCB
	BLO	3$
	TST	(R0)+
4$:	MOV	R0,R4
	RETURN
;
.IF EQ,MASINA-BK0011
ASCCD0::JSR	R1,ASCCOD
	SEC
	RETURN
.ENDC
ASCCOD::
	MOV	#BUF,R3
	TRAP	110
	BR	10$		;БЕЗ НОМЕРА
	CALL	CLRCOD
	MOV	R5,CURLIN
	TRAP	111
	BR	5$
	CMPB	@R3,#12
	BEQ	7$
	MOV	TABTOP,R0
	MOV	R0,R2
	SUB	#6,R2
	CMP	R2,TXEND
	BLOS	MEMFUL
	MOV	R2,TABTOP
	MOV	R2,LENT
	MOV	R2,LIMIT
	MOV	R2,CIKL
17$:	MOV	(R0)+,(R2)+
	CMP	R0,R4
	BLOS	17$
	MOV	R5,@R4
5$:	MOV	R4,R5
10$:	TRAP	112
; ОПОЗНАНИЕ ОПЕРАТОРА
	CMPB	@R3,#12
	BNE	16$
	TST	R5
	BEQ	7$
	CALL	DEL
	BR	7$
16$:	MOV	R5,R2
.IF EQ,MASINA-BK0011
	.BJSR	OPFND
.IFF
	TRAP	113
	.WORD	OPRNAM
	.BYTE	2
	.BYTE	AOPEND-AOPSUB-2
	CLR	R5
.ENDC
25$:	TST	R2
	BEQ	8$
	MOV	TXEND,R0
	SUB	R3,@SP
	ADD	R0,@SP
	CMP	@SP,TABTOP
	BHIS	1$
	MOV	R0,-(R2)
	MOVB	R5,(R0)+
2$:	MOVB	(R3)+,(R0)
	CMPB	(R0)+,#12	;BUTINA PAGAL 12 DEL LOAD
	BNE	2$
	MOV	R0,TXEND
	INC	R0
	BIC	#1,R0
	MOV	R0,ENDCOD
7$:	TST	(R1)+
8$:	RTS	R1
;
1$:	MOV	R2,R4
	CALL	DEL
	TRAP	7
;
DEL:	MOV	R4,R0
DELINT:	TST	(R4)+
	CMP	-(R0),-(R0)
20$:	CMP	R0,TABTOP
	BLOS	21$
	MOV	-(R0),-(R4)
	BR	20$
21$:	MOV	R4,TABTOP
	RETURN
;
CLRBUF::MOV	HIMEM,FCB
;	MOV	MAXFIL,R1
;	BEQ	2$
1$:	SUB	#422,FCB
	CLR	@FCB
;	SOB	R1,1$
2$:	MOV	FCB,DFCB
	SUB	#400,FCB
.IFF
	MOV	FCB,FIRST
	MOV	FCB,LAST
.IFTF
	RETURN
MEMFUL:	TRAP	7
;
CLRTXT::CALL	CLRBUF
	MOV	#BASVU,R1	;ВЫВОД НАЧАЛЬНОГО СООБЩЕНИЯ
	MOV	#WSTOP-BASVU,R2
.IF DF,HOOKS
	CALL	HMESS
.ENDC
	CALL	ISVEIL
CLRTX1::MOV	FCB,TABTOP	;
	MOV	#TEXT,TXEND
	CLR	PFACT
;
CLRCOD::CALL	WRESET
;	CLR	ONERFL
.IIF EQ,MASINA-BK0011	MOV	@#VIRSTA,@#VIREND
	MOV	TABTOP,LENT
	MOV	TABTOP,LIMIT
	MOV	TABTOP,CIKL
	MOV	TXEND,ENDCOD
	INC	ENDCOD
	BIC	#1,ENDCOD
	MOV	@PC,ERRS	;ЗАПРЕТИТЬ ВЫПОЛНЕНИЕ РАБОЧЕЙ ПРОГРАММ
	CLR	STRREG
	RETURN
;
WRESET::MOV	R0,-(SP)
	MOV	R1,-(SP)
	CALL	NOSTOP
	MOV	#VCTNMB,R0
2$:	MOV	@R0,R0
	BEQ	3$
	.BJSR	TSTVCT
	BCS	1$
	MOV	@R0,R1
	MOV	-(R1),@R0
	TST	-(R1)
	MOV	R1,R0
	BR	2$
3$:	CLR	@#VCTNMB
	CALL	STOPEN
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	RETURN
1$:	CLR	@#VCTNMB
	TRAP	51.

STRMEM:	MOV	ENDCOD,R1
	MOV	R1,FREBEG
	ADD	STRSIZ,R1
	MOV	STRSIZ,FRELEN
	CMP	R1,LENT
	BHI	MEMFUL
	MOV	R1,ENDCOD
	MOV	FREBEG,STRREG
NOSTEP:	RETURN
;
STEP::	TST	LYGIS
	BNE	NOSTEP
.IFF
	MOV	R1,CURS
.IFTF
	TST	ERRS
	BNE	1$
	MOV	#STACK,SP
	NEG	TRFLAG
	MOV	ADRHLT,R4
	BNE	TRIN
	CALL	NOSTOP
	MOV	FCB,R4
	TST	(R4)+
	BR	STP2
1$:	TRAP	17.
TRIN::
	CALL	NOSTOP
      	CALL	AGAUD
STP2:	CLR	AUTOON
	CLR	NUMBER
	CMP	-(R4),-(R4)
	JSR	R0,SETST
	BR	50$
	CALL	STOPEN
	JMP 	STAR
50$:	CMP	(4)+,(4)+
	MOV	R4,R2
	MOV	R4,R0
	MOV	-(0),R0
	JSR	R5,KODTST
	BR	3$
	BR	4$
	CMPB	#40,@R0
	BNE	21$
	JSR	R5,NEXPAP
	MOV	R2,R4
21$:	SUB	#6,R4
	BR	32$
4$:	MOV	R0,R3
	INC	R3
41$:	CMPB	(3)+,#12
	BEQ	45$
42$:	JSR	R2,IFGAUD
	BR	45$
	BR	41$
    	CMP	#4,R5
	BNE	47$
	JSR	R5,NEXPAP
	BR	42$
47$:	CMP	#6,R5
	BEQ	42$
	MOV	R5,R0
48$:	TRAP	110
	BR	43$
	TRAP	111
	BR	44$
	BR	42$
44$:	JSR	R5,HCOR
	TRAP	112
	CMPB	#',,@R3
	BNE	42$
	INC	R3
	BR	48$
45$:	MOV	R2,R4
	BR	21$
43$:	CMP	#2,R0
	BNE	42$
	JSR	R5,REPA
	BR	42$
	BR	44$
3$:	CMPB	#74,@R0	;RESTORE
	BEQ	21$
	INC	R0
	MOV	R0,R3
	TRAP	110
	BR	31$
	TRAP	111
	BR	32$
	NOP
31$:	JSR	R5,REPA
	BR	37$
32$:	JSR	R5,HCOR
37$:	MOV	R2,R4
    	MOV	SP,PNTSAV
	CALL	EILUTE
	TST	TRFLAG
	BLE	2$
	MOVB	#133,R0
	CALL	ISV
	BIS	#20000,R2
	CALL	ISVEIL
	MOVB	#135,R0
	CALL	ISV
	BR	22$
2$:
	MOV	#12,R0
	CALL	ISV
	CALL	ISVEIL
22$:	MOV	-(4),R4
	CALL	STOPEN
	JMP	VYKDYT
;
REPA:	TST	(SP)+
	CMP	LENT,LIMIT
	BEQ	1$
	MOV	@LIMIT,R4
	CALL	AGAUD
	CMP	-(4),-(4)
	TST	(5)+
1$:	MOV	R5,PC
;
HCOR:	TST	(SP)+
	JSR	R0,SETST
	BR	2$
	BR	1$
2$:	CMP	(4)+,(4)+
	CMP	R4,R2
	BEQ	1$
	CMP	R4,TABTOP
	BLO	1$
	CMP	-(4),-(4)
	MOV	@R4,R4
	MOV	R4,-(SP)
	MOV	@R4,-(SP)
	INC	NUMBER
	MOV	#STPEND,@R4
1$:	MOV	R5,PC
;
NEXPAP:	TST	AUTOON
	BEQ	1$
	RTS	R5
1$:	TST	(SP)+
	MOV	R5,AUTOON
	MOV	CIKL,R1
3$:	CMP	R1,LIMIT
	BHIS	2$
	MOV	2(1),R4
	CALL	AGAUD
	CMP	-(4),-(4)
	JSR	R5,HCOR
	MOV	@R1,R0
	ADD	#10,R1
	MOV	-(0),R0
	ROL	R0
;
.IF DF,SNG
;
	BCS	3$
.IFF
	BMI	3$
.ENDC
	ADD	#14,R1
	BR	3$
2$:	MOV	AUTOON,PC
;
STPEND::
	CALL	NOSTOP
	JSR	R2,SEPAP
	TST	-(R4)
	TST	TRFLAG
	BLE	1$
	JMP	TRIN
1$:	MOV	R4,ADRHLT
	NEG	TRFLAG
	CALL	STOPEN
.IF DF,HOOKS
	CALL	HEND
.ENDC
	JMP	STAR
;
SEPAP::	TST	(SP)+
	INC	ERRS
	TST	PNTSAV
	BEQ	5$
	MOV	PNTSAV,SP
2$:	DEC	NUMBER
	BLT	5$
	MOV	(SP)+,@(SP)+
	BR	2$
5$:	CLR	PNTSAV
	MOV	#STACK,SP
	CLR	ERRS
	CLR	NUMBER
	MOV	R2,PC

;
NEW::	TRAP	106
	MOV	#STACK,SP
	.BJSR	CLOALL
	CALL	CLRTXT

.IIF NE,MASINA-BK0011	CALL	ISVSS

	JMP	MONIT
;
LLIST::	MOV	#-1,IODEV
;
LIST::	MOV	R5,-(SP)
	CALL	LARG		; PIRMAS ARGUMENTAS - R4, ANTRAS - R0
;	CLR	-(SP)
	INC	LYGIS
	CMP	R4,R0
	BLO	LS66	;PRIE PUSLAPIAVIMO LS6
LS1:	MOV	R4,-(SP)
LS2:	MOV	R0,-(SP)
	CALL	CUROFF		
;	CLR	-(SP)
LS3:	CALL	EILUTE
;	ADD	R2,@SP
;	CMP	@SP,#1340
;	BGT	LS7
LS4:	MOV	#5000,R2
	MOV	2(R4),CURLIN
.IF DF,HOOKS
	CALL	HLIST
.ENDC
	CALL	ISVETO
;11$:	BIT	#37,@SP 
;	BEQ	8$
;	INC	@SP
;	BR	11$
8$:	CMP	-(R4),-(R4)
	CMP	R4,@SP	;2(SP) PRIE PUSLAPIAVIMO
	BHIS	LS3
;	TST	IODEV
;	BEQ	LS8
	CLR	IODEV
LS6:	CMP	(SP)+,(SP)+	;TST PRIE  PUSLAPIAVIMO
;	BNE	LS6
LS66:	MOV	#5007,@#BUF
	JMP	STAR
;LS7:	TST	IODEV
;	BMI	LS4
;	TST	(R4)+
;LS8:	MOV	R4,@SP
;LS9:	CALL	CURON
;	CALL	IVEIL
;	CMPB	BUF,#12
;	BEQ	3$
;	CLR	LYGIS
;44$:	TST	(SP)+
;	BNE	44$
;	JMP	ANALIZ
;3$:	CMP	(SP),2(SP)
;	BLO	LS9
;	MOV	(SP)+,R4
;	MOV	(SP)+,R0
;	BR	LS1
;LISTBC:	MOV	6(SP),R4
;	BEQ	LS9
;	TST	(SP)+
;	MOV	(SP)+,R0
;	TST	(SP)+
;	BR	LS2
;
LARG:	CLR	-(SP)		;PIRMAS ARGUMENTAS
	CALL	ARGUM		;ПРИЗНАК НАЛИЧИЯ ПЕРВОГО АРГУМЕНТА
	BEQ	1$
	INC	(SP)
	TRAP	111
	BR	11$
	TST	4(SP)
	BEQ	111$
	NEG	(SP)
11$:	MOV	R4,-(SP)	; (SP)-PIRMOS EILUTES ADRESAS RYSIU LENT.
	CMPB	#'-,(R3)	;AR YRA MINUSAS ?
	BNE	4$
	INC	R3
	CALL	ARGUM		;ANTRAS ARGUMENTAS
	BEQ	3$
	TRAP	111
	BR	2$
	TST	6(SP)
	BEQ	111$
	TST	(R4)+		;ARGUMENTAS NURODYTAS KLAIDINGAI -
31$:	CMP	(R4)+,(R4)+	;PASTUMIAM RODYKLE RYSIU LENTELEJE
2$:	MOV	R4,R0
	MOV	(SP)+,R4
	TST	(SP)+
	MOV	(SP)+,(SP)
	TRAP	106
	RETURN
111$:	TRAP	8.
;
1$:	MOV	FCB,R4		;PIRMO ARGUMENTO NERA => JIS = PIRMOS EILUTES
	TST	-(R4)
	BR	11$
4$:	TST	2(SP)
	BGT	2$
	BLT	31$
3$:	MOV	TABTOP,R4
	BR	31$
;
ARGUM:	TRAP	112
	CMPB	#'.,(R3)
	BNE	2$
	MOV	CURLIN,R5
	INC	R3
	RETURN
2$:	TRAP	110
	CLR	R4
	RETURN
;
EILUTE::MOV	@R4,R5
	CALL	IUNPCK
	MOV	R5,R3
	MOV	-(R4),R5
	MOVB	(R5)+,R1
	BEQ	1$
.IF EQ,MASINA-BK0011
	ADD	#AOPNAM,R1
	MOV	#PAG2,-(SP)
	MOV	R1,-(SP)
	EMT	60
	MOV	R0,R1
.IFF
	MOV	AOPNAM(R1),R1
.IFTF
	MOV	#40,R0
.IFT
	CALL	INBUF1
.IFF
	CALL	INBUF
.ENDC
1$:	MOV	R5,R1
	MOV	#12,R0
	CALL	INBUF
	MOV	#BUF,R1
	RETURN
;
STATM:	MOV	#400,LYGIS
	TST	STRREG
	BNE	5$
	CALL	STRMEM
5$:	MOV	ENDCOD,R1
	CLR	TMP
	MOV	#-1,NUMBER
.IF EQ,MASINA-BK0011
	MOV	R0,-(SP)
	MOV	#PAG2,-(SP)
	MOV	@SP,-(SP)
	MOV	#AOPSUB,-(SP)
	ADD	R5,@SP
	EMT	60
	MOV	R0,-(SP)
	.BJSR0
	MOV	(SP)+,R0
	MOV	#STARP,(R1)+
.IFF
	CALL	@AOPSUB(R5)
	MOV	#STAR,(R1)+
.ENDC
	MOV	ENDCOD,R4
	CMP	R1,CIKL
	BHIS	MEMORY
	NEG	LYGIS
	JMP	VYK1
;
RUN::	MOV	FCB,R2
	TRAP	110
	MOV	-(R2),R5
	TRAP	106
	MOV	R5,-(SP)
	.BJSR	CLOALL
	MOV	(SP)+,R5
LRUN::	CMP	TABTOP,FCB
	BEQ	KL1
	TRAP	111
	BR	1$
	TRAP	8.
1$:	JSR	R0,SETST
	BR	4$
	BR	KL1
4$:	CALL	CURON
	MOV	#DATBEG,DATPTR
	CLR	AUTOON
	CLR	DATBEG
	CALL	CLRCOD
	MOV	#1,LYGIS
	MOV	R4,ADRHLT
	CALL	GRBCOL
	MOV	TABTOP,R0
3$:	CLR	(R0)+
	CMP	(R0)+,(R0)+
	CMP	R0,FCB
	BLO	3$
	CLR	PFACT
	MOV	#STACK,SP
COMPIL:	CMP	R0,TABTOP
	BLOS	ENDTXT
	MOV	-(R0),NUMBER
	MOV	-(R0),R3
	TST	-(R0)
	MOV	R0,R5
1$:	MOV	@R5,R2
	MOV	R1,@R5
	MOV	R2,R5
	BNE	1$
	MOVB	(R3)+,R2
	CLR	TMP
	CMP	R2,#AOPMON-AOPSUB
	BGE	KL12
.IF EQ,MASINA-BK0011
	MOV	R0,-(SP)
	MOV	#PAG2,-(SP)
	MOV	@SP,-(SP)
	MOV	#AOPSUB,-(SP)
	ADD	R2,@SP
	EMT	60
	MOV	R0,-(SP)
	MOV	4(SP),R0
	.BJSR0
	MOV	(SP)+,R0
.IFF
	CALL	@AOPSUB(R2)
.ENDC
	CMP	R1,LENT
	BLOS	COMPIL
MEMORY:	CLR	LYGIS
2$:	CMP	R1,TABTOP
	BLOS	TXTSV
	ADD	#6,TABTOP
	BR	2$
KL1:	JMP	STAR
KL2::	TRAP	2
KL12::	TRAP	12.
SET1:	CMPB	@-(4),#50	;REM
	BEQ	1$
	CMPB	@0(4),#2	;'
	BEQ	1$
	CMPB	@0(4),#100	;DATA
	BNE	SET2
1$:	CMP	-(4),-(4)
SETST::	CMP	R4,TABTOP
	BHIS	SET1
	TST	(0)+
SET2:	TST	-(4)
	RTS	R0
TXTSV:	CALL	CLRCOD
	TRAP	7
ENDTXT:	CMP	-2(R1),#ENDPRG
	BEQ	2$
	CMP	-2(R1),#HALT
	BEQ	2$
	MOV	#ENDPRG,(R1)+
2$:	MOV	DATBEG,R0
	BEQ	11$
	MOV	R1,DATBEG
1$:	MOV	2(0),R3
	INC	R3
	MOV	@R0,R2
	MOV	R1,@R0
13$:	CMP	LENT,R1
	BLOS	MEMORY
	CMPB	@R3,#12
	BEQ	3$
	MOVB	(3)+,(1)+
	BR	13$
3$:	CMP	R0,DATPTR
	BEQ	4$
	MOVB	#54,(1)+
	MOV	R2,R0
	BR	1$
4$:	MOVB	@R3,(1)+
11$:	MOV	DATBEG,DATPTR
	MOV	AUTOON,R0
	BEQ	10$
7$:	MOV	@R0,R4
5$:	CMPB	@(4),#100	;DATA
	BEQ	6$
	SUB	#6,R4
	CMP	R4,TABTOP
	BHI	5$
	CLR	@R0
	BR	8$
6$:	MOV	-(4),@R0
8$:	MOV	-(0),R4
	MOV	#RESTV,@R0
	MOV	R4,R0
	BNE	7$
10$:	MOV	R1,ENDCOD
	CALL	STRMEM
	MOV	@ADRHLT,R4
	DEC	LYGIS
VYK2:	CLR	ADRHLT
	CLR	ERRS
	CLR	AUTOON
	CLR	NUMBER
	TST	TRFLAG
	BEQ	VYKDYT
	JMP	TRIN
VYKDYT::CLR	SAVJMP
	MOV	#-1,LYGIS
.IF DF,HOOKS
	CALL	HRUN
.ENDC
	CALL	CUROFF
VYK1:	CLR	INK
;
.IF EQ,MASINA-BK0011
	.BJMP	ENDFRE
.IFF
	JMP	@(R4)+
.ENDC
;
PRGFUL:	TRAP	17.
;
CONT::	TRAP	106
	TST	ERRS
	BNE	PRGFUL
	MOV	ADRHLT,R4
	BEQ	KL11
.IF EQ,MASINA-BK0011
	MOV	@#CRTGRP,R0
	EMT	74
.ENDC
	BR	VYK2
;
BC::	ADD	#16,SP
;	TST	LYGIS
;	BGT	1$
	CLR	R2
	MOV	#BUF,R3
	MOV	R3,R1
	BR	E4
;1$:	JMP	LISTBC

DELETE::CLR	-(SP)
	CALL	LARG
	CALL	CLRCOD
	CALL	DELINT
KL11:	JMP	STAR

EDAUIN:	TRAP	111
	BR	E6
	CALL	IUNPCK
.IF EQ,MASINA-BK0011
	ADD	R1,R2
	CLRB	@R2
	BR	EDEND
.IFF
	CALL	ISVEIL
	MOV	R1,R2
	SUB	#BUF,R2
	MOV	R1,R3
	BR	EDEND
;
.IFTF
EDIT:	CLR	R2
	MOV	CURLIN,R0
	TRAP	110
	MOV	R0,R5
E0:	TRAP	106
	TRAP	111
	BR	E6
	TRAP	8.
E6:	CALL	IUNPCK
	MOV	-(R4),R4
	MOV	R5,R3
	MOVB	(R4)+,R1
	BEQ	1$
.IFT
	ADD	#AOPNAM,R1
	MOV	#PAG2,-(SP)
	MOV	R1,-(SP)
	EMT	60
	MOV	R0,R1
.IFF
	MOV	AOPNAM(R1),R1
.IFTF
	MOV	#40,R0
.IFT
	CALL	INBUF1
.IFF
	CALL	INBUF
.IFTF
1$:	MOV	R4,R1
E4:	MOV	#12,R0
	CALL	INBUF
.IFT
	CLRB	-(3)
.IFTF
	MOV	#BUF,R1
.IFF
	DEC	R3
	DEC	R2
	BEQ	5$
	MOV	R2,R0
	CALL	ISVEIL
	MOV	R0,R2
5$:	MOV	R3,R1
EDEND:	MOV	#ANALIZ,-(SP)
.IFT
EDEND:	MOV	R1,-(SP)
	MOV	#400,-(SP)

.IIF DF,HOOKS	CALL	HEDIT

	EMT	66
2$:	TSTB	(R1)+
	BNE	2$
	MOVB	#12,-(R1)
	JMP	ANALIZ
.ENDC
.IFF
	JMP	IVEIL
.IFT
.IF NE,MASINA-BK0011
	MOV	R0,-(SP)
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R5,-(SP)
	CLR	-(SP)
	MOV	#420,-(SP)
	SUB	@SP,R2
	NEG	R2
.IF DF,HOOKS
	CALL	HEDIT
.ENDC
	JMP	PIJ
.ENDC
.IFTF
.IF EQ,MASINA-BK0011
INBUF1:	MOV	R0,-(SP)
1$:	MOV	#PAG2,-(SP)
	MOV	R1,-(SP)
	BIC	#1,@SP
	EMT	60
	BIT	#1,R1
	BEQ	2$
	SWAB	R0
2$:	MOVB	R0,(R3)+
	INC	R2
	INC	R1
	CMPB	R0,@SP
	BNE	1$
	MOV	(SP)+,R0
	RETURN
.ENDC
INBUF:	MOVB	(1),(3)+
	INC	R2
	CMPB	(1)+,R0
	BNE	INBUF
	RETURN
AUTO::	CALL	CURON
	CLR	R2
	CMPB	#',,@R3
	BEQ	2$
	MOV	#12,R2
	CALL	ARGUM
	BEQ	2$
	MOV	R5,R2
2$:	TRAP	112
	MOV	#12,R5
	CMPB	#',,@R3
	BNE	4$
3$:	MOV	AUINC,R0
	INC	R3
	TRAP	110
	MOV	R0,R5
	TST	R5
	BEQ	ILCALL
4$:	TRAP	106
	MOV	@PC,AUTOON
	MOV	R2,CURLIN
	MOV	R5,AUINC
	MOV	R2,R5
	BR	EDAUIN
;
ISGUNP::MOV	#BUFOUT,R3
	MOVB	#40,(R3)+
	TST	R5
	BPL	1$
	MOVB	#'-,-1(R3)
	NEG	R5
1$:	CALL	I1
	DEC	R1
	INC	R2
	RETURN
;
IUNPCK::
	MOV	#BUF,R3
I1::	MOV	R3,-(SP)
	ADD	#5,R3
	MOVB	#40,(R3)
1$:	MOV	#5,R2
	CLR	R1
2$:	ASL	R2
	BCS	4$
	CMP	R2,R5
	BLOS	2$
3$:	CMP	R2,#12
	BEQ	5$
	CLC
4$:	ROR	R2
	ASL	R1
	CMP	R2,R5
	BHI	3$
	SUB	R2,R5
	INC	R1
	BR	3$
5$:	ADD	#60,R5
	MOVB	R5,-(R3)
	MOV	R1,R5
	BNE	1$
	MOV	@SP,R5
	MOV	R5,R1
	CLR	R2
	ADD	#6,@SP
6$:	MOVB	(R3)+,(R5)+
	INC	R2
	CMP	R3,@SP
	BNE	6$
	TST	(SP)+
	RETURN
;
GRBCOL::CLR	R4
	MOV	#TEXT,R1
	CALL	NOSTOP
1$:	MOV	#-1,R3
	MOV	FCB,R2
	CMP	-(R2),-(R2)
2$:	CMP	@R2,R4
	BLOS	3$
	MOV	@R2,R3
	MOV	R2,R5
3$:	CMP	-(R2),-(R2)
	CMP	R2,TABTOP
	BLO	4$
	CMP	-(R2),R3
	BLO	2$
	BR	3$
4$:	CMP	R3,#-1
	BEQ	6$
	MOV	R1,@R5
	MOV	R3,R4
	MOVB	(R3)+,(R1)+
5$:	MOVB	(R3)+,@R1
	CMPB	(R1)+,#12
	BNE	5$
	BR	1$
6$:	MOV	R1,TXEND
	INC	R1
	BIC	#1,R1
	MOV	R1,ENDCOD
	CALL	STOPEN
	RETURN
;
ILCALL:	TRAP	5
;
RENUM::	CALL	CLRCOD
	MOV	R3,R0
	CALL	GRBCOL
	MOV	R0,R3
	CLR	R2
	MOV	#12,R0
	MOV	#12,-(SP)
	CMPB	#',,@R3
	BEQ	1$
	CALL	ARGUM
	BEQ	3$
	MOV	R5,@SP
	TRAP	112
	CMPB	#',,@R3
	BNE	3$
1$:	INC	R3		;2 ARG. TIKRINIMAS
	TRAP	112
	CMPB	#',,@R3
	BEQ	5$		;2 ARG. PRALEISTAS
	CALL	ARGUM
	BEQ	3$
	MOV	R5,R2
	TRAP	112
	CMPB	#',,@R3
	BNE	3$
5$:	INC	R3
	TRAP	110
	BR	3$
	MOV	R5,R0
	BEQ	ILCALL
3$:	TRAP	106
;	ARGUMENTAI ISANALIZUOTI
;	@SP /NEW/,  R2/OLD/,  R0/INC/
	MOV	R2,R5	;НАЙТИ ПЕРВУЮ СТРОКУ
	TRAP	111
	BR	8$
	CMP	R4,TABTOP
	BHIS	8$
	JMP	STAR	;ПЕРВОЙ НЕТ - КОНЕЦ
;---В ТРЕТЬЕМ СЛОВЕ ПОМЕСТИТЬ НОВЫЕ НОМЕРА
8$:	MOV	R4,R2	;ЗАПОМНИТЬ АДРЕС НАЧАЛА
	MOV	R4,R1
	TST	-(R1)
9$:	MOV	@SP,-(R1)	;ТРЕТЬЕ СЛОВО = НОВЫЙ НОМЕР
	MOV	@R1,R5
	TRAP	111
	BR	13$
	BR	14$
13$:	CMP	R4,R2
	BHI	ILCALL		;ТАКАЯ СТРОКА ЕСТЬ ВЫШЕ
14$:	CMP	R1,TABTOP
	BLOS	10$
	ADD	R0,@SP
	BCS	ILCALL		;ПРЕВЫШЕНИЕ 65535
	CMP	-(R1),-(R1)
	BR	9$
10$:	TST	(SP)+
;---ПЕРЕНУМЕРОВАТЬ В ТЕКСТЕ
38$:	CMP	R1,FCB
	BEQ	30$		;TEKSTO ANALIZE BAIGTA
	TST	(R1)+
	MOV	@R1,R0
	JSR	R5,KODTST
	BR	12$		;ПРОСТЫЕ ССЫЛКИ (GOTO И Т.П.)
	BR	24$		;СЛОЖНЫЕ ССЫЛКИ
15$:	CMP	(R1)+,(R1)+	;НЕТ ССЫЛОК - ИДТИ ДАЛЬШЕ
	BR	38$
12$:	INC	R0
	MOV	R0,R3
	TRAP	110
	BR	15$
	CLR	-(SP)
	CALL	RENPAP
	BR	23$
	BR	23$
24$:	CLR	-(SP)
	MOV	R0,R3
20$:	CMPB	(3)+,#12
	BEQ	23$
22$:	JSR	R2,IFGAUD
	BR	23$
	BR	20$
29$:	MOV	R3,R0
	TRAP	110
	BR	22$
	CALL	RENPAP
	BR	28$
28$:	TRAP	112
	CMPB	#',,@R3
	BNE	22$
	INC	R3
	BR	29$
23$:	CLR	(SP)+
	BR	15$
;---ПЕРЕНУМЕРАЦИЯ В ТАБЛИЦЕ СВЯЗИ
30$:	TST	(R2)+
31$:	MOV	R2,R0		;SIUNCIAM NEW NR. OF LINE
	CMP	-(R2),-(R2)
	MOV	-(R2),-(R0)
	CMP	R2,TABTOP
	BNE	31$
;	SUTVARKOM RL MONOTONISKUMA
;	R0/@MAX EIL NR
	CLR	R3
36$:	MOV	R0,R4
33$:	ADD	#6,R4
	CMP	R4,FCB
	BHI	34$		;BAIGTA
35$:	CMP	(R0)+,(R4)+
	BLO	32$
	CMP	R4,FCB
	BEQ	34$
	CMP	-(R0),-(R4)
	MOV	R4,R0
	BR	33$
32$:	TST	R3
	BNE	37$
	MOV	R4,R3
37$:	MOV	-(R4),-(SP)
	MOV	-(R4),-(SP)
	MOV	-(R4),-(SP)
	SUB	#6,R0
	MOV	(R0)+,(R4)+
	MOV	(R0)+,(R4)+
	MOV	(R0)+,(R4)+
	MOV	R0,R4
	SUB	#6,R0
	MOV	(SP)+,(R0)+
	MOV	(SP)+,(R0)+
	MOV	(SP)+,(R0)+
	SUB	#10,R0
	TST	-(R4)
	CMP	R0,TABTOP
	BHI	35$
	MOV	R3,R0
	TST	-(R0)
	CLR	R3
	BR	36$
34$:	JMP	STAR
RENPAP:	MOV	R2,-(SP)
	MOV	R1,-(SP)
	MOV	R3,-(SP)
	TRAP	112
	SUB	R0,@SP
	INC	@SP		;R3/SK. ILGIS +1/
	TRAP	111
	BR	3$
	CMP	R1,R2
	BHI	1$
	MOV	-2(R1),NUMBER
	BR	2$
1$:	MOV	2(R1),NUMBER
2$:	MOV	#200,LYGIS
	TRAP	10
22$:	TST	(SP)+
	BR	21$
3$:	CMP	R4,R2
	BHI	22$
	MOV	-4(R4),R5
	CALL	IUNPCK
	SUB	R2,@SP
	BLT	8$
4$:	DEC	R2
	BEQ	5$		;EIL. NR. SUVESTI
	MOVB	(R1)+,(R0)+
	BR	4$
5$:	DEC	@SP
	BLT	6$
	MOVB	#40,(R0)+
	BR	5$
6$:	MOV	R0,@SP
7$:	MOV	(SP)+,R3
21$:	MOV	(SP)+,R1
	MOV	(SP)+,R2
	CLR	LYGIS
	RTS	PC
8$:	TST	10(SP)
	BNE	13$
	MOV	R0,R4
9$:	CMPB	(R4)+,#12
	BNE	9$
	SUB	@2(SP),R4
	SUB	@SP,R4
	ADD	TXEND,R4
	CMP	TABTOP,R4
	BHIS	15$		;OK
	SUB	TXEND,R4		;NE OK
	MOV	R4,-(SP)
	MOV	R3,-(SP)
	CALL	GRBCOL
	MOV	(SP)+,R3
	MOV	(SP)+,R4
	ADD	TXEND,R4
	CMP	TABTOP,R4
	BHI	15$		;GERAI
10$:	CMP	2(SP),4(SP)
	BHI	11$
	MOV	-2(R1),NUMBER
	BR	12$
11$:	MOV	2(R1),NUMBER
12$:	MOV	#200,LYGIS
	TRAP	7
	ADD	#2,6(SP)
	BR	22$
13$:	MOV	@SP,R2
	NEG	R2
	ADD	TXEND,R2
	CMP	TABTOP,R2
	BLO	10$		;NETILPS
	MOV	TXEND,R4
14$:	MOVB	-(R4),-(R2)
	CMP	R4,R0
	BNE	14$
	MOV	R0,R1
	SUB	@SP,R1
	BR	17$
15$:	MOV	@2(SP),R1
	MOV	TXEND,R4
	MOV	R4,@2(SP)
16$:	MOVB	(R1)+,(R4)+
	CMP	R1,R0
	BNE	16$
17$:	MOV	#BUF,R2
18$:	CMPB	@R2,#40
	BEQ	19$
	MOVB	(R2)+,(R4)+
	INC	R1
	BR	18$
19$:	ADD	@SP,R1
	MOV	R4,@SP
20$:	MOVB	@R1,(R4)+
	CMPB	(R1)+,#12
	BNE	20$
	MOV	R4,TXEND
	INC	10(SP)
	BR	7$
;
KODTST::CMPB	#20,@R0	;GOSUB
	BEQ	12$
	CMPB	#22,@R0	;GOTO
	BEQ	12$
	CMPB	#52,@R0	;RETURN
	BEQ	12$
	CMPB	#74,@R0	;RESTORE
	BEQ	12$
	TST	(5)+
	CMPB	#24,@R0	;IF
	BEQ	12$
	CMPB	#72,@R0	;ON
	BEQ	12$
	TST	(5)+
12$:	RTS	R5
;
IFGAUD::TRAP	102
	BR	25$
	BR	21$		;RAIDE
	BR	20$
25$:	CMPB	@R3,#47
	BEQ	23$
	CMPB	@R3,#42
	BNE	20$
26$:	INC	R3
	CMPB	@R3,#12
	BEQ	23$
	CMPB	@R3,#42
	BNE	26$
	BR	20$
21$:	TRAP	113
	.WORD	RWREN
	.BYTE	2
	.BYTE	16
	BR	20$
	TST	R5
	BEQ	23$
	TST	(2)+
20$:	TST	(2)+
23$:	RTS	R2
;
.IF EQ,MASINA-BK0011
RWREN:	.ASCII	/REM /
	.ASCII	/RETURN /
	.ASCII	/NEXT /
	.ASCII	/RESTORE /
RWON:	.ASCII	/GOSUB /
RWIF:	.ASCII	/GOTO /
	.ASCII	/THEN /
	.ASCII	/ELSE /
	.EVEN
.ENDC
;
;MAXFL:	CMPB	(R3)+,#'=
;	BNE	1$
;	TRAP	110
;1$:	TRAP	2
;	TRAP	106
;	CMP	R5,#1
;	BHI	2$
;	MOV	R5,-(SP)
;	.BJSR	CLOALL
;	CLR	R2
;	MOV	#422,R4
;	MOV	MAXFIL,R1
;	SUB	@SP,R1
;	BEQ	3$
;	BPL	4$
;	NEG	R4
;	NEG	R1
;4$:	ADD	R4,R2
;	SOB	R1,4$
;	.BJSR	TABMOV
;	MOV	(SP)+,MAXFIL
;	CALL	CLRBUF
;3$:	CALL	CLRCOD
;	JMP	STAR
;2$:	TRAP	5
.ENDC
	.END	START
