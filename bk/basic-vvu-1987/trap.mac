.TITLE	TRAPLIB
.ENABLE	LC

.MACRO	.BJSR	N
.IF EQ,MASINA-BK0011
	MOV	#PAG2,-(SP)
	MOV	#N,-(SP)
	EMT	54
.IFF
	CALL	N
.ENDC
.ENDM
;
.GLOBL	IFRE,$FRE,SLOCX,ILOCX,SLOCY,ILOCY
.GLOBL	FRE,SFRE,PEEK,LEN,INP,LOCX,LOCY,EOF,INSTR
.GLOBL	ASC,VAL,SUM,SKIRT,SAND,POINT,INSTR2
.GLOBL	LPOS,ILPOS,SLPOS
.GLOBL	TMP,PI,$SQUEE
.GLOBL	ABS,IABS,ISGN
.GLOBL	ISVEIL,NUMBER,ATLIN1,LNUM,ISV
.GLOBL	SEPAP,SVAR,IVAR,SCONST,ICONST
.GLOBL	INEG,NEG,ADRHLT,AFUN,STOPEN,NOSTOP
.GLOBL	TABTOP,LENT,SETW,LYGIS,ERRS,SETST,DATINP,SAVJMP,CLRCOD
.GLOBL	INDEX,ARRAY,ARR10,$USR,ISTR,SSTR,BIN,OCT,HEX
.GLOBL	SXVAR,IXVAR,$XVAR,SCMP,ICMP,$CMP,NOT,ADLOG,ADDRBR
.GLOBL	MOD,IDIV,BUFOUT,TRTOPL
.GLOBL	FCB,INKEY,NEWSTR,CURLIN,STRREG,I1,TYPE,CIKL,STACK,LIMIT
.GLOBL	STR,STR2,IODEV,CONCAT,CHR,ALLSM,ALLSK	;SIGNAL
.GLOBL	FNST,FNCALL,SPAR,IPAR,APAR,PFACT			;APRL
.GLOBL	EQ,GT,LT,GE,LE,NE
.GLOBL	IMP,EQV,XOR,OR,AND
.IF DF,SNG
.GLOBL	ADR,SUB,DVR,MLR
.GLOBL	RAN,ROTG,ROTI,RI,IR,SGN
.GLOBL	SQRG,SING,KOS,TANG,ATANG,EXPG,ALNG,INT,SVD1
.IFF
.GLOBL	DFRE,DLOCX,DLOCY,DLPOS,$DADD$,$DSUB$,$DDIV$,$DMUL$
.GLOBL	$B91,$B93,$B95,$TAN,$B97,$B99,$B103,$INT,$FIX,DSGN,SSGN
.GLOBL	SI,IS,DI,ID,SD,DS,DVAR,DCONST,DSTR,DXVAR,DCMP,POW,DPAR,RAN
.ENDC
.IF LE,MASINA-BK0010
.GLOBL	BRKO,RTIO
.ENDC
.IF EQ,MASINA-BK0011
.GLOBL	PAG2,PEEK1,INP1,CISLO1,VIRPTR,IXVIR,SXVIR,$XVIR,PAGSET
.IIF NDF,SNG	.GLOBL	DXVIR
.IFF
.GLOBL	CISLO,ATCONV
.ENDC
.IF DF,HOOKS
.GLOBL	HERR,HEND
.ENDC
.IF NE,MASINA-BK0010
.GLOBL	SETCRT
.ENDC

;
.IF NE,MASINA-BK0011
TRAP::	MOV	R0,-(SP)
	MOV	R2,-(SP)
	MOV	4(SP),R0
	MOV	-2(R0),R2
	BIC	#177400,R2
	SUB	#100,R2
	BLT	ERROR
	ASL	R2
	CALL	@ATRAP(R2)
	MOV	R0,4(SP)
TREND:	MOV	(SP)+,R2
	MOV	(SP)+,R0
	RTI
.ENDC
;
ERROR::	CLR	IODEV
	MOV	R2,R5
	ADD	#100,R5
.IIF EQ,MASINA-BK0011	CALL	PAGSET
.IIF NE,MASINA-BK0010	.BJSR	SETCRT
.IF NE,MESSAG
;	MOV	#400,R1
;	MOV	#24,R2
;	CALL	SIGNAL
	MOV	#ERR,R1
	MOV	#10,R2
.IIF DF,HOOKS	CALL	HERR
	CALL	ISVEIL
;	MOV	#400,R1
;	MOV	#37,R2
;	CALL	SIGNAL
	MOV	R5,R2
	TRAP	114	;KEICIA R1,R3,R5,(R4 NE)
.IFF
	CALL	MSTYPE
.ENDC
	TST	LYGIS
	BEQ	MONERR
	BMI	RUNERR
	TSTB	LYGIS
	BEQ	MONERR
	CALL	CLRCOD
	MOV	NUMBER,R5
	MOV	R5,CURLIN
	CALL	ATLIN1
	TSTB	LYGIS
	BMI	TREND
	BR	COMERR
RUNERR:	CMPB	R2,#13.
	BNE	10$
	TST	DATINP
	BEQ	GETERR
10$:	TST	SAVJMP
	BEQ	1$
	MOV	SAVJMP,R4
	CLR	SAVJMP
1$:	CMP	R4,STRREG
	BHI	MONERR
	JSR	R2,SEPAP
	CALL	NOSTOP
	CALL	LNUM
	MOV	R2,CURLIN
	CALL	TRTOPL
	CMP	-(4),-(4)
	JSR	R0,SETST
	BR	2$
	CLR	ADRHLT
	BR	3$
2$:	MOV	@R4,ADRHLT
3$:	CLR	LYGIS
	CALL	STOPEN
.IF DF,HOOKS
	CALL	HEND
.ENDC
	BR	COMERR
MONERR::MOV	#12,R0
	CALL	ISV
COMERR:	MOV	#STACK,SP
COMER2:	JMP	SETW
GETERR:	MOV	#12,R0
	CALL	ISV
.IF NE,MASINA-BK0011
	BR	TREND
.IFF
TREND:	RETURN
.ENDC
;
ERR:	.BYTE	'О+200,'[+200,'I+200,'B+200,'K+200,'A+200,' ,7
	.EVEN
;
ATRAP::	.WORD	T100
	.WORD	T101
	.WORD	T102
	.WORD	T103
	.WORD	T104
	.WORD	T105
	.WORD	T106
	.WORD	T107
	.WORD	T110
	.WORD	T111
	.WORD	T112
	.WORD	T113
	.WORD	T114
	.WORD	T115
	.WORD	T116
	.WORD	T117
	.WORD	T120
	.WORD	T121
	.WORD	T122
	.WORD	T123
	.WORD	T124
	.WORD	T125
	.WORD	T126
	.WORD	T127
	.WORD	T130
;
RWMOD:	.ASCII	/MOD /
RWLOG:	.ASCII	/NOT IMP EQV XOR OR AND /
FUN:	.ASCII	/PI POINT INP PEEK /
	.ASCII	/RND SQR SIN COS TAN ATN /
	.ASCII	/EXP LOG INT FIX ABS SGN /
.IF DF,SNG
	.ASCII	/CSNG CINT /
.IFF
	.ASCII	/CINT CDBL CSNG /
.ENDC
	.ASCII	/EOF CSRLIN POS LPOS FRE LEN ASC VAL /
 	.ASCII	/INSTR VARPTR /
	.ASCII	/USR FN /
SFUN:	.ASCII	/INKEY MID STRING STR CHR BIN OCT HEX /
RELAT:	.BYTE	'=,'>,'<,0
PLMI::	.BYTE	'+,'-,0
.IF NE,MASINA-BK0011
PRSPEC::.BYTE	'"
	.BYTE	'&,'.,'(,0
.ENDC
DGDL:	.BYTE	'/,'*,0
.IF DF,SNG
TYPCHR::.BYTE	'$,'!,'%,0
.IFF
TYPCHR::.BYTE	'$,'!,'#,'%,0
.ENDC
	.EVEN
;
ADDRBR::.WORD	EQ,GT,LT,GE,LE,NE,0
;
ADLOG::	.WORD	IMP,EQV,XOR,OR,AND
;
T100:
	CMP	SP,#500
	BLO	5$
	MOV	#-1,R5
1$:	MOV	R5,-(SP)
	CALL	SANT
	TRAP	113
	.WORD	RWLOG+4
	.BYTE	3,10
	BR	4$
	CALL	CINT
2$:	CMP	R5,@SP
	BGT	1$
	MOV	(SP)+,R2
	MOV	ADLOG(R2),(R1)+
	BR	2$
3$:	CALL	CINT
	MOV	ADLOG(R2),(R1)+
4$:	MOV	(SP)+,R2
	BGE	3$
	RETURN
5$:	TRAP	16.
;
SANT:	TRAP	112
	CLR	-(SP)
	TRAP	113
	.WORD	RWLOG,3
	INC	@SP
	MOV	TMP,-(SP)
	TRAP	115
	BR	1$
	CALL	LYGOP
	BCS	10$
	TRAP	115
10$:	TRAP	13.
	MOV	(SP)+,TMP
	MOV	#$CMP,(R1)+
	BR	7$
1$:	TST	(SP)+
	CALL	SUDAT
2$:	CALL	LYGOP
	BMI	8$
	MOV	R2,-(SP)
.IF DF,SNG
	MOV	TYPE,-(SP)
	CALL	CSNG
	MOV	R1,-(SP)
	CALL	SUDAT
	MOV	(SP)+,R4
	TST	(SP)+
	BEQ	4$
	TST	TYPE
	BEQ	5$
	CALL	SUSP
	MOV	#ICMP,(R1)+
	BR	6$
4$:	CALL	CSNG
5$:	MOV	#SCMP,(R1)+
6$:	MOV	(SP)+,R2
7$:	MOV	#1,TYPE
.IFF
	MOV	R1,-(SP)
	MOV	TYPE,-(SP)
	CALL	SUDAT
	MOVB	TYPE,R5
	CMPB	R5,@SP
	BEQ	5$
	BHI	4$
	MOV	@SP,TYPE
	MOV	R1,R4
	TST	(R4)+
	MOV	R4,@SP
3$:	MOV	-(R1),-(R4)
	CMP	R1,2(SP)
	BHI	3$
	INC	R5
	ASL	R5
	CALL	@ATCONV(R5)
	MOV	@SP,R1
	BR	6$
4$:	MOVB	@SP,R5
	INC	R5
	ASL	R5
	CALL	@ATCONV(R5)
	BR	6$
5$:	INC	R5
	ASL	R5
6$:	MOV	ACMP(R5),(R1)+
	CMP	(SP)+,(SP)+
	MOV	(SP)+,R2
7$:	MOV	#377,TYPE
.IFTF
	MOV	ADDRBR (R2),(R1)+
	BR	2$
8$:	TST	(SP)+
	BNE	9$
	CALL	CINT
	MOV	#NOT,(R1)+
9$:	RETURN
;
.IFF
ACMP:	.WORD	ICMP,	DCMP,	SCMP
.IFTF
;
LYGOP:	TRAP	101
	.WORD	RELAT
	BR	1$
	MOV	R4,R2
	TRAP	101
	.WORD	RELAT
	RETURN
	CMP	R4,R2
	BEQ	2$
	CMP	(R4)+,(R4)+
	ADD	R4,R2
	RETURN
1$:	COM	R4
	RETURN
2$:	TRAP	2
;
SUDAT:	CALL	LIEK
3$:	TRAP	101
	.WORD	PLMI
	RETURN
	MOV	TYPE,-(SP)
	MOV	R4,-(SP)
.IFF
	CALL	CDBL
.IFT
	CALL	CSNG
.IFTF
	MOV	R1,-(SP)
	CALL	LIEK
	MOV	(SP)+,R4
	MOV	(SP)+,R2
.IFF
	TSTB	TYPE
	BPL	1$
	TSTB	@SP
	BPL	1$
.IFT
	TST	(SP)+
	BEQ	1$
	TST	TYPE
	BEQ	2$
.IFTF
	CALL	SUSP
	CMP	(R2)+,(R2)+
	BR	2$
.IFF
1$:	CALL	CDBL
.IFT
1$:	CALL	CSNG
.IFTF
2$:	MOV	APLMI(R2),(R1)+
.IFF
	TST	(SP)+
.IFTF
	BR	3$
;
LIEK:	CALL	SDAL
1$:	TRAP	113
	.WORD	RWMOD
	.WORD	3
	RETURN
	CALL	CINT
	CALL	SDAL
	CALL	CINT
	MOV	#MOD,(R1)+
	BR	1$
;
SDAL:	CALL	DAUG
1$:	TRAP	112
	CMPB	@R3,#'\
	BNE	D3
	INC	R3
	CALL	CINT
	CALL	DAUG1
	CALL	CINT
	MOV	#IDIV,(R1)+
	BR	1$
;
DAUG:	CLR	-(SP)
	TRAP	101
	.WORD	PLMI
	BR	DAUG2
	MOV	R4,@SP
	BR	DAUG2
DAUG1:	CLR	-(SP)
DAUG2:	CALL	LAIPS
	TST	(SP)+
	BEQ	D2
.IFF
	TSTB	TYPE
	BPL	D1
.IFT
	TST	TYPE
	BEQ	D1
.IFTF
	MOV	#INEG,(R1)+
	BR	D2
D1:	MOV	#NEG,(R1)+
D2:	TRAP	101
	.WORD	DGDL
D3:	RETURN
	MOV	TYPE,-(SP)
	MOV	R4,-(SP)
.IFT
	CALL	CSNG
.IFF
	CALL	CDBL
.IFTF
	MOV	R1,-(SP)
	CALL	LAIPS
	MOV	(SP)+,R4
	MOV	(SP)+,R2
	BEQ	1$
.IFF
	TSTB	TYPE
	BPL	1$
	TSTB	@SP
	BPL	1$
.IFT
	TST	@SP
	BEQ	1$
	TST	TYPE
	BEQ	2$
.IFTF
	CALL	SUSP
	TST	(R2)+
	BR	2$
.IFT
1$:	CALL	CSNG
.IFF
1$:	CALL	CDBL
.IFTF
2$:	MOV	ADGDL(R2),(R1)+
	TST	(SP)+
	BR	D2
;
LAIPS:	CALL	OPER
1$:	TRAP	112
	CMPB	(R3),#'^
	BNE	D3
	INC	R3
.IFF
	CALL	CDBL
	MOV	#$B103,(R1)+	; LOG
	MOV	R1,-(SP)
	CALL	OPER
	MOV	(SP)+,R4
	TSTB	TYPE
	BMI	2$
	CALL	CDBL
	MOV	#$DMUL$,(R1)+
	MOV	#$B99,(R1)+	;EXP
	BR	1$
2$:	CALL	SUSP
	MOV	#POW,(R1)+
.IFT
	CALL	CSNG
	CALL	OPER
	MOV	TYPE,R5
	ASL	R5
	MOV	APOW(R5),(R1)+
.IFTF
	CLR	TYPE
	BR	1$
;
.IFT
APOW:	.WORD	ROTG,ROTI
.IFTF
;
SUSP:	MOV	R4,R5
	TST	-(R5)
3$:	MOV	(R4)+,(R5)+
	CMP	R4,R1
	BLO	3$
	TST	-(R1)
	RETURN
;
OPER:
.IF EQ,MASINA-BK0011
.IF DF,SNG
	CMP	-(SP),-(SP)
.IFF
	SUB	#10,SP
.IFTF
	MOV	SP,R2
	.BJSR	CISLO1
	ADD	R2,PC
	BR	3$
	BR	2$
	TST	R4
	BEQ	71$
.IFF
	BPL	70$
	CMP	(SP)+,(SP)+
70$:
.ENDC
	CMP	(SP)+,(SP)+
71$:
.IFF
	CALL	CISLO
	TRAP	24.	;SPEC
	BR	3$	;RAIDE
	BR	2$	; (
.ENDC
	MOV	R4,TYPE
.IFT
	BNE	13$
	MOV	#SCONST,(R1)+
	MOV	2(SP),(R1)+
	MOV	(SP)+,(R1)+
	TST	(SP)+
.IFF
	BMI	13$
	BEQ	11$
	MOV	#SCONST,(R1)+
	CMP	(R1)+,(R1)+
	MOV	R1,R4
	BR	12$
11$:	MOV	#DCONST,(R1)+
	ADD	#10,R1
	MOV	R1,R4
	MOV	(SP)+,-(R4)
	MOV	(SP)+,-(R4)
12$:	MOV	(SP)+,-(R4)
	MOV	(SP)+,-(R4)
.IFTF
	RETURN
13$:	MOV	#ICONST,(R1)+
	MOV	R5,(R1)+
.IFF
	CLRB	TYPE+1
.IFTF
	RETURN
2$:
.IF EQ,MASINA-BK0011
.IF DF,SNG
	CMP	(SP)+,(SP)+
.IFF
	ADD	#10,SP
.ENDC
.IFTF
	JMP	OP2
3$:
.IFT
.IF DF,SNG
	CMP	(SP)+,(SP)+
.IFF
	ADD	#10,SP
.ENDC
.ENDC
	TRAP	105
	BR	31$
	TRAP	13.
	TRAP	13.
	CLR	R4
	TRAP	121
	TRAP	103
30$:	RETURN
31$:
.IFT
	CMP	R5,#66	;-2 BE INSTR
.IFF
	CMP	R5,#70
.IFTF
	BLT	4$
	BEQ	34$	;TIK SU VARPTR
	MOV	TMP,-(SP)
	CALL	USR	;USR IR FN
	BMI	32$
	MOV	(SP)+,TMP
	RETURN
32$:	TRAP	13.
;
;----------TIK SU VARPTR------------
33$:	TRAP	5
34$:	TRAP	127	;(
	CLR	R4
	TRAP	107
	NOP
	TST	R5
	BEQ	33$
.IFT
	MOV	#1,TYPE
.IFF
	MOV	#377,TYPE
.IFTF
	TSTB	R5
.IF NE,MASINA-BK0011
	BMI	36$
.IFF
	BPL	35$
	BIT	#100,R5
	BEQ	36$
	MOV	#VIRPTR,(1)+
	BR	36$
35$:
.ENDC
	MOV	#ICONST,(1)+
	MOV	R4,(1)+
36$:	JMP	OP3
;------------------------------------
;
4$:	MOV	R5,-(SP)
	BEQ	6$
.IFF
	CMP	R5,#46		; SURISTA SU FUNKCIJOMIS
	BLT	5$
	CMP	R5,#60		; SURISTA SU FUNKCIJOMIS
	BGE	40$
	DECB	TYPE
	CMP	R5,#46
	BEQ	6$
	CMPB	@R3,#'(
	BNE	6$
	SUB	#61,R5
	ASL	R5
	ADD	R5,@SP
40$:	ADD	#32,@SP
	TRAP	127
	CMP	@SP,#100		;SURISTA SU FUNKCIJOMIS
	BGT	42$
41$:	TRAP	100
	MOVB	TYPE,R5
	INC	R5
	SUB	R5,TYPE
	ASL	R5
	ADD	@SP,R5
	BR	46$
42$:	MOV	TMP,-(SP)
	TRAP	122
	BR	47$
	MOV	(SP)+,TMP
	CLR	TYPE
	CMP	@SP,#116
	BEQ	45$
;
;-------TIK SU INSTR-------
	BLT	44$
	TRAP	126
	TRAP	115
43$:	TRAP	13.
;---------------------------
44$:	DECB	TYPE
45$:	MOV	@SP,R5
46$:	TRAP	130
	BR	6$
47$:	TST	(SP)+
	ADD	#2,@SP
	CMP	@SP,#104
	BEQ	41$
	CMP	@SP,#122
.IFT
	CMP	R5,#44
	BLT	5$
	CMP	R5,#56
	BGE	40$
	INC	TYPE
	CMP	R5,#44
	BEQ	6$
	CMPB	@R3,#'(
	BNE	6$
	SUB	#60,R5
	ADD	R5,@SP
40$:	ADD	#22,@SP
	TRAP	127	;(
	CMP	@SP,#70
	BGT	42$
41$:	TRAP	100
	MOV	TYPE,R5
	MOV	#1,TYPE
	ASL	R5
	ADD	@SP,R5
	BR	46$
42$:	MOV	TMP,-(SP)
	TRAP	122
	BR	47$
	MOV	(SP)+,TMP
	CLR	TYPE
	CMP	@SP,#104
	BEQ	45$
;
;---------TIK SU INSTR--------
	BLT	44$
	TRAP	126
	TRAP	115
43$:	TRAP	13.
	CLR	TYPE
;------------------------------
44$:	INC	TYPE
45$:	MOV	@SP,R5
46$:	TRAP	130	;)
	BR	6$
47$:	TST	(SP)+
	ADD	#2,@SP
	CMP	@SP,#74
	BEQ	41$
	CMP	@SP,#110
.IFTF
	BNE	43$	;BE INSTR 32$
;
;----------TIK SU INSTR-----------
	TRAP	125
	TRAP	126
	BR	42$
;---------------------------------
48$:	TRAP	124
.IF EQ,MASINA-BK0011
	CMP	@SP,#2
	BEQ	45$
55$:	TRAP	116
	BR	45$
	SUB	#10,@SP
	CALL	CINT
.IFTF
	BR	45$
49$:	TRAP	125
.IFT
	BR	55$
.IFF
	BR	45$
.ENDC
5$:	TRAP	127
	CMP	R5,#6
	BLT	48$
	BEQ	49$
	TRAP	100
	TRAP	130
	MOV	(SP),R5
	CMP	R5,#26
	BLE	52$
.IFF
	MOVB	TYPE,R4
	INC	R4
	ASL	R4
	CMP	R5,#36
	BGT	7$
	BEQ	51$
	CMP	R5,#34
	BNE	50$
	TST	R4
	BNE	6$
51$:	TST	(R4)+
	ADD	R4,R5
	BR	6$
50$:	TST	R4
	BEQ	60$
52$:	CALL	CDBL
.IFT
	MOV	TYPE,R4
	ASL	R4
	CMP	R5,#36
	BGT	7$	;CSNG,CINT
	BNE	51$
	TST	(R4)+	;SGN
50$:	ADD	R4,R5
	BR	6$
51$:	CMP	R5,#34
	BEQ	50$	;ABS
	TST	R4	;INT,FIX
	BNE	60$
52$:	CALL	CSNG
.ENDC
6$:	MOV	AFUN(R5),(R1)+
60$:	TST	(SP)+
	RETURN
7$:	SUB	#40,R5
	CALL	@ATCONV(R5)
	BR	60$
OP2:	TRAP	100
T130:
OP3:	CMPB	(R3)+,#')
	BEQ	SKLUZ
OP4:	TRAP	2
;
T127:
	TRAP	112
	CMPB	(R3)+,#'(
	BNE	OP4
SKLUZ:	TRAP	112
	RETURN
;
.IF DF,SNG
CINT::	TST	TYPE
	BNE	1$
	MOV	#RI,(R1)+
	INC	TYPE
1$:	RETURN
;
CSNG::	TST	TYPE
	BEQ	1$
	MOV	#IR,(R1)+
	CLR	TYPE
1$:	RETURN
;
.IFF
CINT::	MOVB	TYPE,R4
	BLT	1$
	ASL	R4
	MOV	ACINT(R4),(R1)+
	MOV	#377,TYPE
1$:	RETURN
;
CSNG::	MOVB	TYPE,R4
	BGT	1$
	INC	R4
	ASL	R4
	MOV	ACSNG(R4),(R1)+
	MOV	#1,TYPE
1$:	RETURN
;
CDBL::	MOVB	TYPE,R4
	BEQ	1$
	INC	R4
	MOV	ACDBL(R4),(R1)+
	CLR	TYPE
1$:	RETURN
;
ACDBL:	.WORD	ID,SD
ACSNG:	.WORD	IS,DS
ACINT:	.WORD	DI,SI
.ENDC
;
T101:	TRAP	112
	MOV	(R0)+,R2
	MOV	#-2,R4
1$:	TSTB	@R2
	BEQ	2$
	ADD	#2,R4
	CMPB	(R2)+,@R3
	BNE	1$
	INC	R3
	TST	(R0)+
2$:	RETURN
;
MZ:	BICB	#40,@R3
T102:
	MOVB	@R3,R4
	CMPB	R4,#172
	BHI	2$
	CMPB	R4,#140
	BHI	MZ
	CMPB	R4,#132
	BHI	2$
	CMPB	R4,#100
	BHI	1$
	CMPB	R4,#71
	BHI	2$
	CMPB	R4,#60
	BLO	2$
	TST	(R0)+
1$:	TST	(R0)+
2$:	RETURN
;
T103:
	TRAP	112
	CLR	R2
	CMPB	@R3,#'(
.IF NE,MASINA-BK0011
	BNE	2$
.IFF
	BEQ	10$
	CMPB	@R3,#'[
	BNE	2$
	BISB	#100,R5
10$:
.IFTF
	TST	(R4)+
	INC	R3
	MOV	TYPE,-(SP)
	MOV	R5,-(SP)
	MOV	R4,-(SP)
	TRAP	125
6$:	TRAP	116
	BR	1$
	INC	R2
	CALL	CINT
	BR	6$
1$:	MOV	(SP)+,R4
	MOV	(SP)+,R5
	MOV	(SP)+,TYPE
.IFF
	BIT	#100,R5
	BEQ	17$
	CMPB	(R3)+,#']
	BEQ	8$
	TRAP	2
17$:
.ENDC
	TRAP	130
	BR	8$
2$:	TST	R4
	BEQ	7$
8$:	INC	R2
	BIS	#200,R5
.IF DF,SNG
	BIC	#100000,R5
.IFF
	DECB	TYPE+1
.IFTF
	BR	4$
7$:	TST	R5
.IFF
	BMI	4$
	TSTB	TYPE
	BEQ	5$
.IFTF
	BPL	4$
.IFF
	TST	(R4)+
.IFTF
5$:	CMP	(R4)+,(R4)+
4$:	MOV	AKINPP(R4),(R1)+
	TRAP	104
	MOV	R4,(R1)+
	TSTB	R5
	BPL	3$
	MOV	R2,(R1)+
.IFF
	MOV	#6,R2
	INCB	TYPE+1
	BMI	9$
	MOVB	TYPE,R2
.IFT
	MOV	TYPE,R2
.IFTF
	INC	R2
	ASL	R2
9$:
.IF EQ,MASINA-BK0011
	BIT	#100,R5
	BEQ	19$
.IF DF,SNG
	ADD	#6,R2
.IFF
	ADD	#10,R2
.ENDC
19$:
.ENDC
	MOV	AXKINP(R2),(R1)+
3$:	RETURN
;
AKINPP:	.WORD	SVAR,	INDEX
.IFF
	.WORD			DVAR
.IFTF
	.WORD				IVAR
	.WORD	ARR10,	ARRAY
;
.IFF
AXKINP:	.WORD	IXVAR,DXVAR,SXVAR,$XVAR
.IF EQ,MASINA-BK0011
	.WORD	IXVIR,DXVIR,SXVIR,$XVIR
ATCONV:	.WORD	CINT,CDBL,CSNG
.ENDC
.IFT
AXKINP:	.WORD	$XVAR,SXVAR,IXVAR
.IF EQ,MASINA-BK0011
	.WORD	$XVIR,SXVIR,IXVIR
ATCONV:	.WORD	CSNG,CINT
.ENDC
.ENDC
;
T104:	MOV	LENT,R4
1$:	CMP	R4,TABTOP
	BHIS	2$
.IF DF,SNG
	CMP	R5,@R4
	BEQ	3$
	TST	(R4)+
	BMI	11$
.IFF
	MOV	(R4)+,R2
	CMP	R5,R2
	BEQ	3$
	TSTB	R2
	BMI	10$
	ROL	R2
	BCS	10$
	BMI	11$
	ROL	R2
	BMI	10$
	CMP	(R4)+,(R4)+
.IFTF
10$:	TST	(R4)+
11$:	TST	(R4)+
	BR	1$
2$:	TST	LYGIS
	BMI	50$
	MOV	LENT,R4
.IFT
	CMP	-(4),-(4)
	TST	R5
	BMI	20$
	TST	-(4)
.IFF
	SUB	#6,R4
	MOV	TYPE,R2
	BMI	20$
	TSTB	R2
	BEQ	22$
	BGT	20$
	TST	(R4)+
	BR	20$
22$:	CMP	-(R4),-(R4)
.IFTF
20$:	CMP	R4,R1
	BLOS	4$
	MOV	R4,LENT
	MOV	R4,LIMIT
	MOV	R4,CIKL
.IFF
	MOV	R5,(R4)+
.IFTF
	MOV	R4,R2
.IFF
	TST	TYPE+1
	BMI	30$
	TSTB	TYPE
.IFT
	MOV	R5,(2)+
.IFTF
	BMI	33$
.IFF
	BNE	30$
	CLR	(R2)+
	CLR	(R2)+
.IFTF
30$:	CLR	(R2)+
33$:	CLR	(R2)+
	TSTB	R5
	BPL	3$
	MOVB	#2,-(R2)
.IFT
	TST	TYPE
	BGT	3$
	BMI	35$
	INCB	@R2
35$:	INCB	@R2
3$:	TST	(4)+
	RETURN
.IFF
	TST	R5
	BMI	40$
	TSTB	TYPE
	BMI	3$
	BNE	35$
	ASLB	@R2
35$:	ASLB	@R2
3$:	RETURN
40$:	INCB	@R2
	RETURN
.IFTF
50$:	CLR	R4
	RETURN
4$:	TRAP	7
;
T105:	MOV	R3,-(SP)
	CLR	TYPE
	TRAP	102
	TRAP	5
	BR	10$
	TRAP	5
10$:	TRAP	113
	.WORD	FUN
.IFT
	.BYTE	6,112		;110,106
.IFF
	.BYTE	6,114		;КОЛИЧЕСТВО ФУНКЦИЙ
.IFTF
	BR	2$
	TST	(SP)+
.IFT
	CMP	R5,#74		;72,70
.IFF
	CMP	R5,#76		;КОЛИЧЕСТВО ЧИСЛОВЫХ ФУНКЦИЙ
.IFTF
	BLT	1$
.IFT
	SUB	#74,R5		;72,70
.IFF
	SUB	#76,R5		;ПЕРЕКОДИРОВАТЬ С НУЛЯ
.IFTF
	CMPB	(R3)+,#'$
	BEQ	6$
	TRAP	13.
2$:	MOV	(SP)+,R3
	CMP	(R0)+,(R0)+
	CLR	R5		;MOV	#20000,R5	TRACE
	BISB	(R3)+,R5
	BIC	#100,R5
	SWAB	R5
	TRAP	102
	BR	4$
	NOP			;CLRB	R5		TRACE
	BISB	(R3),R5
	BIC	#100,R5
3$:	INC	R3
	TRAP	102
	BR	4$
	BR	3$
	BR	3$
4$:	TRAP	101
	.WORD	TYPCHR
	BR	6$	;8$	SU DEFTYP
9$:	ADD	R4,PC
	BR	5$	;$
.IFF
	BR	7$
.IFTF
	BR	6$
.IFT
	BIS	#140000,R5	;%
.IFF
	BIS	#40000,R5
	DECB	TYPE
	BR	6$
7$:	BIS	#20000,R5
.IFTF
	INC	TYPE
6$:	TST	(R0)+
1$:	RETURN
;8$:	MOV	R5,R4
;	CLRB	R4
;	SWAB	R4
;	MOVB	APRL-1(R4),R4
;	BR	9$
.IFT
5$:	BIS	#20000,R5
	DEC	TYPE
	RETURN
.IFF
5$:	BIS	#160000,R5
	COMB	TYPE+1
	RETURN
;
.IFT
APLMI::	.WORD	ADR,SUB,SUM,SKIRT
ADGDL::	.WORD	DVR,MLR,SAND
.IF EQ,MASINA-BK0011
	.WORD	INP1,PEEK1
.ENDC
AFUN::	.WORD	PI,POINT,INP,PEEK,RAN,SQRG,SING,KOS,TANG,ATANG,EXPG
	.WORD	ALNG,INT,SVD1
	.WORD	ABS,IABS,SGN,ISGN
	.WORD	EOF,LOCY,LOCX,LPOS,FRE
	.WORD	SLOCY,ILOCY,SLOCX,ILOCX
	.WORD	SLPOS,ILPOS,$FRE,SFRE,IFRE
.IFF
APLMI::	.WORD	$DADD$,$DSUB$,SUM,SKIRT
ADGDL::	.WORD	$DDIV$,$DMUL$,SAND
.IF EQ,MASINA-BK0011
	.WORD	INP1,PEEK1
.ENDC
AFUN::	.WORD	PI,POINT,INP,PEEK,RAN,$B91,$B93,$B95,$TAN,$B97,$B99
	.WORD	$B103,$INT,$FIX
	.WORD	ABS,IABS,ISGN,DSGN,SSGN
	.WORD	EOF,LOCY,LOCX,LPOS,FRE
	.WORD	ILOCY,DLOCY,SLOCY,ILOCX,DLOCX,SLOCX
	.WORD	ILPOS,DLPOS,SLPOS,$FRE,IFRE,DFRE,SFRE
.ENDC
	.WORD	LEN,ASC,VAL,INSTR2,INSTR
;
T106:
	TRAP	112
.IF EQ,MASINA-BK0011
	TST	R2
.IFF
	TST	4(SP)
.ENDC
	BEQ	2$
	CMPB	@R3,#12
	BEQ	2$
	CMPB	@R3,#''
	BNE	1$
2$:	RETURN
1$:	TRAP	2
;
T107:
	MOV	R4,R2
	TRAP	112
	TRAP	102
	TRAP	2
	BR	2$
3$:	TRAP	2
1$:	SUB	#2,R5
	BNE	3$
	RETURN
2$:	TRAP	105
	TRAP	2
	BR	1$
	TST	-(R0)
	TST	(R0)+
	MOV	R2,R4
	TRAP	121
	BR	5$
	CMP	-(1),-(1)
	MOV	#APAR,(1)+
	MOV	-6(4),(1)+
	NEGB	R5
	RETURN
5$:	TRAP	103
	TSTB	R5
	BMI	4$
	TST	-(R1)
4$:	TST	-(R1)
	RETURN
;
T110:	CLR	R5
	TRAP	112
	TRAP	102
	RETURN
	RETURN
1$:	SUB	#60,R4
	INC	R3
	ASL	R5
	ADD	R5,R4
	ASL	R5
	ASL	R5
	ADD	R4,R5
	BCS	2$
	TRAP	102
	BR	3$
	BR	3$
	CMP	R5,#14631
	BLOS	1$
2$:
.IF EQ,MASINA-BK0011
	MOV	16(SP),R4	;+4
.IFF
	MOV	16(SP),R4	;12?!
.ENDC
	TRAP	6
3$:	TST	(R0)+
	RETURN
;
T111:
	MOV	FCB,R4
	TST	-(R4)
1$:	CMP	R4,TABTOP
	BLO	2$
	CMP	R5,(R4)
	BEQ	3$
	BLO	2$
	SUB	#6,R4
	BR	1$
2$:	TST	(R0)+
3$:	RETURN
;
T112:	CMPB	(R3)+,#40
	BEQ	T112
	DEC	R3
	RETURN
;
T113:	MOV	R1,-(SP)
	MOV	R3,-(SP)
	MOV	#-1,-(SP)
	CLR	R1
	CLR	R5
	MOV	(R0)+,R2
	TRAP	102
	TRAP	112
	BR	1$
	BR	14$
1$:	CLRB	@SP
2$:	CMPB	(R3),(R2)+
	BNE	3$
	INC	R3
	INCB	@SP
	CMPB	@R2,#40
	BEQ	6$
	TRAP	102
	BR	3$
	BR	2$
3$:	DECB	@SP
	BLE	4$
	CMP	R1,R3
	BHI	4$
	BEQ	7$
	MOV	R3,R1
	MOVB	R5,1(SP)
4$:	MOV	2(SP),R3
	CMPB	R5,1(R0)
	BEQ	11$
5$:	CMPB	(R2)+,#40
	BNE	5$
	TST	(R5)+
	BR	1$
7$:	MOV	#-1,@SP
	BR	4$
11$:	MOVB	1(SP),R5
	BMI	14$
	CMPB	@R0,#3
	BGE	14$
	MOV	R1,R3
6$:	TRAP	112
	TST	(R0)+
14$:	TST	(R0)+
	CMP	(SP)+,(SP)+
	MOV	(SP)+,R1
	RETURN
;
T114:	MOV	#BUFOUT,R3
	CALL	I1
	CALL	ISVEIL
	RETURN
;
T115:
	CMP	SP,#500
	BHIS	2$
	TRAP	16.
2$:	TRAP	112
	CLR	R2
	MOV	R3,-(SP)
	TRAP	102
	BR	1$
	BR	6$
	BR	NOT$
1$:	TRAP	120
	BR	NOT$
	BR	S10
6$:	TRAP	105
	BR	SIMUSR
	BR	SIMFUN
	BR	8$
	BR	NOT$
8$:	CLR	R4
	CALL	TSTSQZ
	TST	-(R1)
	TRAP	121
	TRAP	103
S10:	TRAP	112
	TST	(SP)+
	CMPB	(R3),#'+
	BNE	2$
	TST	R2
	BGT	3$
4$:	INC	R2
	BEQ	4$
	CALL	TSTSQZ
3$:	INC	R3
	TRAP	115
	TRAP	13.
	TST	R4
	BGT	1$
	MOV	#NEWSTR,(R1)+
1$:	MOV	#CONCAT,(R1)+
2$:	TST	(R0)+
	MOV	R2,R4
	RETURN

TSTSQZ::TST	TMP
	BNE	1$
	MOV	#$SQUEE,(R1)+
	INC	TMP
1$:	MOV	#NEWSTR,(R1)+
	RETURN

SIMUSR:
.IF DF,SNG
	CMP	R5,#70	;USR$
.IFF
	CMP	R5,#72	;USR$
.IFTF
	BLT	NOT$
	MOV	R1,-(SP)
	CALL	USR
	BMI	S2
	MOV	(SP)+,R1
NOT$:	MOV	(SP)+,R3
	RETURN
S2:	TST	(SP)+
	INC	R2
	BR	S10
;
SIMFUN:	ASR	R5	;MID$
	BEQ	10$
	TRAP	127
	DEC	R5
	BNE	S30
	TRAP	115
	TRAP	13.
	CALL	MIDEND
	DEC	R2
	BR	S10
;
10$:	MOV	#INKEY,(R1)+	;INKEY$
	BR	S10
;
S20:	DEC	R5	;CHR$,BIN$,OCT$,HEX$
	ASL	R5
	MOV	R5,-(SP)
21$:	TRAP	125
	MOV	(SP)+,R5
	MOV	ASFINT(R5),(R1)+
	BR	S33
;
S30:	DEC	R5	;STRING$
	BNE	S40
	TRAP	125
	TRAP	126
	CALL	TSTSQZ
	TST	-(R1)
	TRAP	122
	BR	31$
	MOV	#ALLSM,(R1)+
	BR	S32
31$:	TRAP	125
	MOV	#ALLSK,(R1)+
S32:	INC	R2
S33:	TRAP	130
	BR	S10
;
S40:	DEC	R5	;STR$
	BNE	S20
	TRAP	100
.IFF
	MOVB	TYPE,R4
	INC	R4
.IFT
	MOV	TYPE,R4
.IFTF
	ASL	R4
	MOV	ASTR(R4),(R1)+
	BR	S33
;
.IFF
ASTR:	.WORD	ISTR,	DSTR,	SSTR
.IFT
ASTR:	.WORD	SSTR,	ISTR
.IFTF
ASFINT:	.WORD	CHR,	BIN,	OCT,	HEX
;
LMID::	TRAP	127
	CLR	R4
	TRAP	107
	BR	10$
	TRAP	13.
10$:	TST	(R1)+
	TSTB	R5
	BMI	MIDEND
	TST	(R1)+
MIDEND:	MOV	R5,-(SP)
	TRAP	126
	TRAP	125
	TRAP	116
	BR	11$
	CALL	CINT
	MOV	#STR,(R1)+
	BR	12$
11$:	MOV	#STR2,(R1)+
12$:	TRAP	130
	MOV	(SP)+,R5
	RETURN
;
USR:	TRAP	112
	CLR	R2
	TRAP	102
	BR	1$
	BR	4$
	MOVB	(R3)+,R2
	SUB	#'0,R2
	ASL	R2
.IFT
1$:	CMP	R5,#72	;-2(4) VARPTR,INSTR
.IFF
1$:	CMP	R5,#74
.IFTF
	BEQ	SYNERR
	TRAP	127
	TRAP	122
	BR	2$
	TST	R4
	BGT	3$
	CALL	TSTSQZ
	BR	3$
2$:	TRAP	100
3$:	MOV	#$USR,(R1)+
	MOV	R2,(R1)+
	TRAP	130
	MOV	TYPE,(R1)+
	RETURN
.IFT
4$:	CMP	R5,#72	;-2(4)
.IFF
4$:	CMP	R5,#74
.IFTF
	BNE	SYNERR
	TRAP	105
	TRAP	2
	TRAP	2
	NOP
.IFT
	BIS	#100,R5
	BIC	#100000,R5
.IFF
	BIS	#100100,R5
.IFTF
	MOV	TYPE,-(SP)
	MOV	#-1,TYPE
	TRAP	104
	MOV	R4,-(SP)
	CALL	TSTSQZ
	MOV	#FNST,-2(1)
	TRAP	112
	CMPB	@R3,#'(
	BNE	7$
40$:	INC	R3
	TRAP	122
	BR	41$
.IFT
	MOV	#-1,-(SP)
.IFF
	MOV	#177400,-(SP)
.ENDC
	TST	R4
	BGT	5$
	MOV	#NEWSTR,(R1)+
	BR	5$
41$:	TRAP	100
	MOV	TYPE,-(SP)
5$:	INC	R2
	CMPB	@R3,#',
	BEQ	40$
6$:	TRAP	130
7$:	MOV	#FNCALL,(1)+
	MOV	R1,R4
	TST	(1)+
	MOV	R2,(1)+
	BEQ	9$
8$:	MOV	(SP)+,(R1)+
	SOB	R2,8$
9$:	MOV	(SP)+,@R4
	MOV	(SP)+,TYPE
	RETURN
;
T116:
	TRAP	112
	CMPB	@R3,#',
	BNE	2$
	MOV	R3,-(SP)
	INC	R3
	TRAP	112
	CMPB	@R3,#',
	BEQ	1$
	CMPB	@R3,#12
	BEQ	1$
	TRAP	100
	TST	(R0)+
	TST	(SP)+
	RETURN
1$:	MOV	(SP)+,R3
2$:	RETURN
;
T117:	CLR	R2
1$:	TRAP	102
	BR	3$
	BR	4$
2$:	INC	R3
	BR	1$
3$:	CMPB	R4,#'.
	BNE	4$
	INC	R2
	BR	2$
4$:	DEC	R2
	BGT	SYNERR
	RETURN
SYNERR:	TRAP	2
;
T120:
	CMPB	@R3,#'"
	BNE	1$
3$:	INC	R3
	MOV	#SCONST,(R1)+
	MOV	R3,(R1)+
	CLR	R4
4$:	CMPB	@R3,#12
	BEQ	5$
	CMPB	(R3)+,#'"
	BEQ	5$
	INC	R4
	BR	4$
5$:	MOV	R4,(R1)+
	TST	(R0)+
.IF DF,SNG
	MOV	#-1,TYPE
.IFF
	MOV	#177400,TYPE
.ENDC
1$:	RETURN
;
T121:
	MOV	PFACT,R2
	BEQ	4$
1$:	TST	(2)+
	BEQ	4$
	TST	(2)+
	CMP	(2)+,R5
	BNE	1$
	TST	TYPE
.IF DF,SNG
	BLE	3$
.IFF
	BMI	3$
	TSTB	TYPE
	BEQ	2$
	BPL	3$
	TST	(4)+
.IFTF
2$:	TST	(4)+
3$:	MOV	PARMOV(R4),(1)+
	MOV	(2),(1)+
	MOV	R2,R4
	TST	(0)+
4$:	RETURN
;
.IFF
PARMOV:	.WORD	SPAR,DPAR,IPAR
.IFT
PARMOV:	.WORD	SPAR,IPAR
.ENDC
;
T122:
	MOV	R3,-(SP)
	TRAP	115
	BR	1$
	TRAP	101
	.WORD	RELAT
	BR	2$
1$:	MOV	(SP)+,R3
	RETURN
2$:	CMP	(SP)+,(R0)+
	RETURN
;
T123:
	TRAP	125
	TRAP	126
T124:
	TRAP	125
	TRAP	126
T125:
	TRAP	100
	CALL	CINT
	RETURN
;
T126:
	TRAP	112
	CMPB	(R3)+,#',
	BNE	1$
	TRAP	112
	RETURN
1$:	TRAP	2
;
.IF EQ,MESSAG
;
.MACRO	.KOI8	STRING
	.IRPC	A,<STRING>
	.BYTE	200+''A
	.ENDM
.ENDM
;
.MACRO	.KOI87	STRING
	.IRPC	A,<STRING>
.IF LT,''A-100
	.BYTE	''A
.IFF
	.BYTE	200+''A
.ENDC
	.ENDM
	.BYTE	7
.ENDM
;
MSTYPE:	MOV	R5,-(SP)
	MOV	#MESS,R1
	MOV	#3400,R2
	CMP	R5,#26.
	BLT	1$
	CMP	R5,#59.
	BGT	2$
	CMP	R5,#52.
	BLT	2$
	SUB	#26.,R5
1$:
	CMPB	(1)+,#7
	BNE	1$
	SOB	R5,1$
2$:
.IF DF,HOOKS
	CALL	HERR
.ENDC
	CALL	ISVEIL
	MOV	(SP)+,R2
	RETURN	
;
MESS:	.KOI87	<О[IBKA>
;
	.ASCII	/NEXT /
	.KOI8	<BEZ>
	.ASCII	/ FOR/<7>
;
	.KOI87	<СINTAKSI^ESKAQ O[IBKA>
;
	.ASCII	/RETURN /
	.KOI8	<BEZ>
	.ASCII	/ GOSUB/<7>
;
	.KOI8	<КON^ILISX>
	.ASCII	/ DATA/<7>
;
	.KOI87	<НEPRAWILXNYJ WYZOW FUNKCII>
	.KOI87	<ПEREPOLNENIE>
	.KOI87	<НET BOLX[E PAMQTI>
	.KOI87	<НEOPREDELENNYJ NOMER STROKI>
	.KOI87	<НESOOTWETSTWIE INDEKSA>
	.KOI87	<ПOWTORNOE OPREDELENIE MASSIWA> ;ПОВТОРНОЕ ОПРЕДЕЛЕНИЕ МАССИВА
	.KOI87	<ДELENIE NA NOLX> ;ДЕЛЕНИЕ НА НОЛЬ/
	.KOI87	<НEWOZMOVNO WYPOLNITX> ;НЕВОЗМОЖНО ВЫПОЛНИТЬ/
	.KOI87	<О[IBKA TIPOW> ;ОШИБКА ТИПОВ/
	.KOI87	<КON^ILASX PAMQTX STROK> ;КОНЧИЛАСЬ ПАМЯТЬ СТРОК/
	.KOI87	<СLI[KOM DLINNAQ STROKA> ;СЛИШКОМ ДЛИННАЯ СТРОКА/
	.KOI87	<СLI[KOM SLOVNOE WYRAVENIE> 
	.KOI87	<ПRODOLVENIE NEWOZMOVNO> ;ПРОДОЛЖЕНИЕ НЕВОЗМОЖНО/
	.KOI87	<НEOPREDELENNAQ FUNKCIQ> ;НЕОПРЕДЕЛЕННАЯ ФУНКЦИЯ/
	.KOI87	<О[IBKA WWODA/WYWODA> ;ОШИБКА ВВОДА-ВЫВОДА/
.REPT 4
	.BYTE	7
.ENDR
	.KOI87	<ОTSUTSTWUET OPERAND> ;ОТСУТСТВУЕТ ОПЕРАНД/
	.KOI87	<ПEREPOLNENIE BUFERA WWODA> ;ПЕРЕПОЛНЕНИЕ БУФЕРА ВВОДА/
	.KOI87	<НEDOPUSTIMOE OBRA]ENIE K FAJLU>;НЕДОПУСТИМОЕ ОБРАЩЕНИЕ K ФАЙЛУ/
	.KOI87	<ФAJL NE NAJDEN>
	.KOI87	<ФAJL UVE OTKRYT> ;ФАЙЛ УЖЕ ОТКРЫТ/
	.KOI87	<КONEC FAJLA> ;КОНЕЦ ФАЙЛА/
	.KOI87	<НEPRAWILXNOE IMQ FAJLA> ;НЕПРАВИЛЬНОЕ ИМЯ ФАЙЛА/
	.KOI87	<СTROKA BEZ NOMERA> ;СТРОКА БЕЗ НОМЕРА>
	.BYTE	7
	.KOI87	<ФAJL NE OTKRYT>
	.EVEN
.ENDC
;
	.END
