.SBTTL	EXECUTIONS KERN EXPANTION

.IIF EQ,MASINA-DISK	.GLOBL	SYSPTR,USRLOC,ASCFLG

.IF EQ,MASINA-BK0011
.MACRO	.BJSR0
	EMT	54
.ENDM

.MACRO	.BJMP
	EMT	56
.ENDM
.ENDC

.MACRO	.BJSR	ADR
.IF EQ,MASINA-BK0011
	MOV	#PAG1,-(SP)
	MOV	#ADR,-(SP)
	.BJSR0
.IFF
	CALL	ADR
.ENDC
.ENDM

.GLOBL	BRKO,ISVEIL,DAUG,ENDPRG
.GLOBL	TXEND,LYGIS,STRREG,STRSIZ,CURON,CUROFF,STACK
.GLOBL	TRFLAG,TRIN,LIMIT,SAVJMP
.GLOBL	ADRHLT,STAR,ENDCOD,LENT,CIKL,IVEIL
.GLOBL	ISVTO,ISVETO,FCB,EGO,ISGUNP,HIMEM
.GLOBL	NAMEW,NAMER,RD.,WR.,FR.,ADRR,ADRW,LENW,FER.,NER.,MER.,RER.,SER.
.GLOBL	BUF,PACK,TABTOP,BUFOUT,INK,FREBEG,FRELEN,NUMBER
.GLOBL	USRTAB,DATINP,DATPTR,IODEV,MAG			;MAXFIL
.GLOBL	WRITE,COPOUT,CISLO
.GLOBL	EILUTE,LRUN,CLRTX1,ASCCOD,TEXT,ERRS,GRBCOL
.GLOBL	CLRBUF,CLRCOD,LPTPOS,PFACT			;SIGNAL
.GLOBL	VCTNMB						;ONERFL

.IF EQ,MASINA-BK0010
.GLOBL	MSTART,ISVSS,FNKSTR,AUSROM,ASPORT,TSTKEY
.ENDC

.IF EQ,MASINA-BK0011
.GLOBL	PNTSAV,PAG1,ENDST,HALTST,T113
.GLOBL	ID.,ID$,STMT$,ASCCD0,ERRBYT
.GLOBL	VIRSCR,VIREND,PAGW
.IFF
.GLOBL	ISV,SEPAP
.ENDC

.IF DF,SNG
.GLOBL	ADR,RI,IR
.IFF
.GLOBL	$DADD$,ID,SI,IS,SD,DCMP,DGO
.ENDC

.IF DF,HOOKS
	.GLOBL	HINKEY,HKEY,HPUTCH,HPUTLN,HLOCAT,HLOOK,HLPOS
	.GLOBL	HCLEAR,HDFUSR,HPOKE
.ENDC

.IIF EQ,MASINA-DISK	.GLOBL	RT11
.IIF EQ,MASINA-NET	.GLOBL	IRPS
.IIF EQ,MASINA-CASET	.GLOBL	MAGNIT

.GLOBL	NOSTOP,STOPEN
.IF LE,MASINA-BK0010
.IFF
.GLOBL	TABX,TABY,INBF0,TSTBRK,AVPK,FIRST,LAST,IVS,ISV1
.GLOBL	NIM,LN,LL,EBUF,CURS,LOK,CRTGRP,GRPCLS
.GLOBL	SETWDT,MIDLE

.IFT
.GLOBL	BREAKW,RTIO
.IFTF

.IFF
INKEY::	MOV	#INK,-(SP)
	CLR	-(SP)
.IIF DF,HOOKS	CALL	HINKEY
	CMP	FIRST,LAST
	BEQ	1$
	CALL	NIM
	INC	@SP
	MOVB	R0,INK
1$:	JMP	@(R4)+
.IFT
.IF EQ,MASINA-BK0011
INKEY::	MOV	#INK,-(SP)
	CLR	-(SP)
.IF DF,HOOKS
	CALL	HINKEY
.ENDC
	EMT	102
	BCS	1$
	INC	@SP
	MOVB	R0,@#INK
1$:	JMP	@(R4)+
;
.IFF
INKEY::	MOV	#INK,-(SP)
	CLR	-(SP)
.IF DF,HOOKS
	CALL	HINKEY
.ENDC
	TSTB	@#TSTKEY
	BEQ	1$
	EMT	6
	MOV	R0,@#INK
	INC	@SP
1$:	JMP	@(R4)+
.ENDC
;
.IFTF
CHR::	BIT	#177400,@SP
	BNE	A5
	MOVB	@SP,INK+1
	MOV	#INK+1,@SP
	MOV	#1,-(SP)
	JMP	@(R4)+
;
STR2::	MOV	2(SP),R0
	BR	STR1
;
STR::	MOV	(SP)+,R0
	BLT	A5
STR1:	MOV	(SP)+,R1
	BLE	A5
	DEC	R1
	ADD	R1,2(SP)
	SUB	R1,@SP
	BLE	A5
	CMP	R0,@SP
	BGE	1$
	MOV	R0,@SP
1$:	JMP	@(R4)+
;
ALLSK::	MOV	(SP)+,R5
	BIT	#177400,R5
	BEQ	STRING
A5:	TRAP	5
;
ALLSM::	TST	(SP)+
	BEQ	A5
	MOVB	@(SP)+,R5
STRING:	MOV	(SP),R1
	BIT	#177400,R1
	BNE	A5
	CALL	TSTFRE
	SUB	R1,FRELEN
	MOV	FREBEG,R2
	MOV	R2,@SP
	MOV	R1,-(SP)
	BEQ	2$
1$:	MOVB	R5,(R2)+
	SOB	R1,1$
	MOV	R2,FREBEG
2$:	JMP	@(R4)+
;
CONCAT::MOV	(SP)+,R0
	MOV	(SP)+,R1
	MOV	@SP,R2
	ADD	R0,@SP
	BIT	#177400,@SP
	BNE	2$
	ADD	2(SP),R2
	TST	R0
	BEQ	3$
1$:	MOVB	(R1)+,(R2)+
	SOB	R0,1$
3$:	SUB	FREBEG,R2
	ADD	R2,FREBEG
	SUB	R2,FRELEN
	JMP	@(R4)+
2$:	TRAP	15.
;
LEN::	MOV	(SP)+,@SP
	JMP	@(R4)+
;
.IF EQ,0
INSTR::	MOV	(SP)+,R0
	MOV	(SP)+,R1
	MOV	(SP)+,R2
	BEQ	I3
	MOV	(SP)+,R3
	MOV	(SP)+,R5
	BLE	A5
	CMP	R5,#255.
	BHI	A5
	DEC	R5
I0:	MOV	R2,-(SP)
	SUB	R5,R2
	BLE	A5
	ADD	R3,R5
	MOV	R0,-(SP)
	BEQ	I6
I1:	MOV	R1,-(SP)
1$:	CMPB	(R5)+,(R1)
	BEQ	I5
	SOB	R2,1$
I2:	TST	(SP)+
I3:	TST	(SP)+
I4:	CLR	@SP
	JMP	@(R4)+
I5:	CMP	R2,R0
	BLT	I2
	MOV	R5,-(SP)
	DEC	R5
8$:	CMPB	(R5)+,(R1)+
	BNE	I7
	SOB	R0,8$
	CMP	(SP)+,(SP)+
I6:	TST	(SP)+
	DEC	R2
	SUB	R2,@SP
	JMP	@(R4)+
I7:	MOV	(SP)+,R5
	MOV	(SP)+,R1
	MOV	(SP),R0
	DEC	R2
	BR	I1
;
INSTR2::MOV	(SP)+,R0
	MOV	(SP)+,R1
	MOV	(SP)+,R2
	BEQ	I4
	MOV	(SP)+,R3
	CLR	R5
	BR	I0
.ENDC
;
ASC::	TST	(SP)+
	BEQ	A5
	CLR	R0
	BISB	@(SP)+,R0
	MOV	R0,-(SP)
	JMP	@(R4)+
;
VAL::	MOV	R4,SAVJMP
	CALL	PACK
	MOV	SAVJMP,R4
	CLR	SAVJMP
.IF DF,SNG
.IFF
	TST	(SP)+
.IFTF
	JMP	@(R4)+
;
SSTR::	CALL	EGO
	BR	STRE
;
.IFF
DSTR::	CALL	DGO
	BR	STRE
;
.ENDC
ISTR::	MOV	(SP)+,R5
	.BJSR	ISGUNP
	MOV	R1,-(SP)
	MOV	R2,-(SP)
STRE:	DEC	@SP
	JMP	@(R4)+
;
OCT::	CLR	R0
	MOV	#BUFOUT+6,R1
	MOV	(SP)+,R5
1$:	MOV	R5,R2
	BIC	#177770,R2
	ADD	#'0,R2
	MOVB	R2,-(R1)
	INC	R0
	CLC
	ROR	R5
	ASR	R5
	ASR	R5
	BNE	1$
	MOV	R1,-(SP)
	MOV	R0,-(SP)
	JMP	@(R4)+
;
BIN::	CLR	R0
	MOV	#BUFOUT+16,R1
	MOV	(SP)+,R5
1$:	MOV	R5,R2
	BIC	#177776,R2
	ADD	#000060,R2
	MOVB	R2,-(R1)
	INC	R0
	CLC
	ROR	R5
	BNE	1$
	MOV	R1,-(SP)
	MOV	R0,-(SP)
	JMP	@(R4)+
;
HEX::	CLR	R0
	MOV	#BUFOUT+4,R1
	MOV	(SP)+,R5
3$:	MOV	R5,R2
	BIC	#177760,R2
	CMP	R2,#9.
	BLE	1$
	ADD	#000067,R2
	BR	2$
1$:	ADD	#000060,R2
2$:	MOVB	R2,-(R1)
	INC	R0
	CLC
	ROR	R5
	ASR	R5
	ASR	R5
	ASR	R5
	BNE	3$
	MOV	R1,-(SP)
	MOV	R0,-(SP)
	JMP	@(R4)+
;
NEWSTR::MOV	(SP),R1
	CALL	TSTFRE
	SUB	R1,FRELEN
	MOV	2(SP),R0
	MOV	FREBEG,R2
	MOV	R2,2(SP)
	ADD	R1,FREBEG
1$:	DEC	R1
	BMI	2$
	MOVB	(R0)+,(R2)+
	BR	1$
2$:	JMP	@(R4)+
;
TSTFRE::CMP	R1,FRELEN
;	BLOS	1$
;	MOV	R1,-(SP)
;	CALL	STRSQU
;	MOV	(SP)+,R1
;	CMP	R1,FRELEN
	BHI	2$
1$:	RETURN
2$:	TRAP	14.
;
.IIF NDF,SNG	DFRE::	CMP	(SP)+,(SP)+
;
SFRE::	TST	(SP)+
IFRE::	TST	(SP)+
FRE::	MOV	CIKL,-(SP)
	SUB	ENDCOD,@SP
ENDFRE::JMP	@(R4)+
;
$FRE::	TST	(SP)+
	CALL	STRSQU
	MOV	FRELEN,@SP
	JMP	@(R4)+
;
$SQUEE::CALL	STRSQU
	JMP	@(R4)+
;
;-----------------------
;СБОРКА МУСОРА СИМВ. ОБЛ.
;-----------------------
STRSQU::MOV	R4,SAVJMP	;R3 - НОВЫЙ FREBEG
	MOV	R5,-(SP)	;R0 - НАЧАЛО ПЕРЕМЕЩАЕМОГО МАС.
	MOV	STRREG,R3	;R2 - УКАЗ. В ТАБЛ. ИМЕН
	MOV	R3,-(SP)	;@SP - КОНЕЦ STRREG
	ADD	STRSIZ,@SP
40$:	MOV	@SP,R0
	TST	-(SP)
	MOV	LENT,R2
8$:	CMP	R2,TABTOP
	BHIS	58$
	JSR	R4,FIND
	BR	8$
	BR	1$
	TST	(R2)+
	BEQ	7$
	CMP	@R2,R0
	BHIS	7$
	CMP	@R2,R3
	BLO	7$
	MOV	@R2,R0
	MOV	R2,@SP
7$:	TST	(R2)+
	BR	8$
;
1$:	CALL	SSQ1
38$:	CALL	SSQ2
	CMP	R4,R0
	BHIS	39$
	CMP	R4,R3
	BLO	39$
	MOV	R4,R0
	MOV	R5,@SP
39$:	CMP	R5,R1
	BLO	38$
	CMP	(R2)+,(R2)+
	BR	8$
;
58$:	MOV	(SP)+,R2
	CMP	R0,@SP
	BEQ	22$
	CMP	R2,LENT
	BLOS	11$
;
	MOV	R3,@R2
	MOV	-(R2),R1
	BR	3$
;
11$:	SWAB	R3
	MOVB	R3,-(R2)
	SWAB	R3
	MOVB	R3,-(R2)
	CLR	R1
	BISB	-(R2),R1
3$:	CALL	PERK
	BR	40$
;
22$:	MOV	R3,FREBEG
	SUB	R3,@SP
	MOV	(SP)+,FRELEN
	MOV	(SP)+,R5
	MOV	SAVJMP,R4
	CLR	SAVJMP
	RETURN
PERK:	CMP	R0,R3		;R0 - СТАРЫЙ
	BEQ	3$		;R3 - НОВЫЙ
1$:	MOVB	(R0)+,(R3)+	;R1 - ДЛИНА
	SOB	R1,1$
	RETURN
3$:	ADD	R1,R3
	RETURN
MASREG::MOV	R0,-(SP)	;R2 -> ИМЯ+2
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	(R2)+,R3
	CLR	R0
	BISB	(R2)+,R0
	MOVB	@R2,R1
1$:	MOV	(R3)+,R2
.IF EQ,VM-1
	CALL	DAUG
.IFF
	MUL	R2,R1
.ENDC
	SOB	R0,1$
	INC	R1
	ASR	R1
	ASL	R1		;R1<- ДЛИНА МАС. БЕЗ ИНД.
	MOV	(SP)+,R3
	MOV	(SP)+,R2
	MOV	(SP)+,R0
	RETURN
;
SSQ1:	CALL	MASREG
	ADD	(R2)+,R1
	CLR	R5
	BISB	@R2,R5
	ASL	R5
	ADD	R5,R1
	ADD	-(R2),R5
	RETURN
SSQ2:	CLR	R4
	TSTB	(R5)+
	BEQ	1$
	BISB	(R5)+,R4
	SWAB	R4
	BISB	(R5)+,R4
	SWAB	R4
	RETURN
1$:	CMPB	(R5)+,(R5)+
	RETURN
FIND:				;ВХОД:	R2-> ИМЯ
.IF DF,SNG			;	R4= АДРЕС ВОЗВРАТА
	BIT	#20000,@R2	;ВЫХОД:	+0	НЕСИМВ.	R2->СЛЕД.ИМЯ
	BEQ	1$		;	+2	СИМВ.МАС. R2->ИМЯ+2
	TSTB	@R2		;	+4	СИМВ.ПЕР. --//---
	BPL	2$
	TST	2(2)
.IF EQ,MASINA-BK0011
	BEQ	1$
	BIT	#100,@R2
	BEQ	3$
.IFF
	BNE	3$
.ENDC
1$:	CALL	PRAL
	RTS	R4
2$:	TST	(4)+
3$:	TST	(4)+
	TST	(2)+
	RTS	R4
.IFF				;	R1 ПОРТИТ
	TSTB	@R2
	BPL	6$
	TST	(2)+
	BPL	3$
	TST	@R2
	BEQ	3$
.IF EQ,MASINA-BK0011
	BIT	#100,-2(R2)
	BNE	3$
.ENDC
	BR	1$
6$:	MOV	(R2)+,R1
	BMI	5$
	ASL	R1
	BMI	2$
	ASL	R1
	BMI	3$
	CMP	(R2)+,(R2)+
3$:	TST	(R2)+
2$:	TST	(R2)+
	RTS	R4
5$:	TST	(R4)+
1$:	TST	(R4)+
	RTS	R4
;
.IFT
PRAL:	TST	(R2)+
	BMI	8$
	TST	(R2)+
8$:	TST	(R2)+
	RETURN
.ENDC
;
CLRSTR::MOV	@SP,R0
	BLT	C5
	INC	R0
	BIC	#1,R0
	MOV	R0,@SP
	SUB	STRSIZ,R0	;
	MOV	R0,R1		;АНАЛИЗ I-ГО АРГУМЕНТА
	ADD	ENDCOD,R0	;
	CMP	R0,LENT
	BHI	CLRERR
	MOV	(SP)+,STRSIZ
	BR	CLR1
CLRERR:	TRAP	7
CLR::	CLR	R1
;.............УСТАНОВКА ОБЛАСТИ ДЛЯ СТРОК..........
CLR1:	MOV	STRREG,R0
	.BJSR	NOSTOP
	MOV	R0,FREBEG
	MOV	STRSIZ,FRELEN
	MOV	LENT,LIMIT
	MOV	LENT,CIKL
	ADD	R1,ENDCOD
;.........................СДВИГ МАССИВОВ...........
	ADD	STRSIZ,R0
	MOV	ENDCOD,R3
	MOV	R1,R5
	BEQ	1$
	NEG	R1
	BMI	20$
	ADD	R0,R1
10$:	MOV	(R1)+,(R0)+
	CMP	R0,R3
	BLO	10$
	BR	1$
20$:	ADD	R3,R1
22$:	MOV	-(R1),-(R3)
	CMP	R3,R0
	BHI	22$
;...................ОЧИСТКА ПЕРЕМЕННЫХ.............
1$:	MOV	LENT,R2
7$:	CMP	R2,TABTOP
	BHIS	8$
	TSTB	@R2
	BPL	6$
.IF EQ,MASINA-BK0011
	BIT	#100,(R2)+
	BNE	77$
.IFF
	TST	(R2)+
.ENDC
	TST	(R2)+
	BEQ	77$
	ADD	R5,-(R2)
	CALL	MASREG
	MOV	(R2)+,R0
	CLR	R3
	BISB	@R2,R3
	ASL	R3
	ADD	R3,R0
	ASR	R1
2$:	CLR	(R0)+
	SOB	R1,2$
77$:	TST	(R2)+
	BR	7$
6$:	MOV	(R2)+,R0
.IF NDF,SNG
	BMI	5$
	ASL	R0
.IFF
	BMI	4$
.IFT
	ASL	R0
	BMI	5$
	CLR	(R2)+
	CLR	(R2)+
.ENDC
5$:	CLR	(R2)+
4$:	CLR	(R2)+
3$:	BR	7$
8$:
	.BJSR	STOPEN
	CMP	R4,STRREG
	BLO	9$
	BR	S1T
9$:	JMP	@(R4)+
C5:	TRAP	5
;
;---------------------------------------------
CLRMEM::
.IF DF,HOOKS
	CALL	HCLEAR
.ENDC
.IF EQ,MASINA-DISK
	MOV	@#SYSPTR,R0
	CMP	@SP,USRLOC(R0)
.IFF
.IF EQ,MASINA-BK0011
	CMP	@SP,#100000
.IFF
.IF EQ,MASINA-BK0010
	CMP	@SP,#70000
.IFF
	CMP	@SP,#MEMTYP
.ENDC
.ENDC
.ENDC
	BHI	C5
	CMP	@SP,#TEXT
	BLOS	C5
	.BJSR	NOSTOP
	CALL	CLOALL
	MOV	(SP)+,R2
	ASR	R2
	ASL	R2
	SUB	HIMEM,R2
	CALL	TABMOV
	ADD	R2,HIMEM
	.BJSR	CLRBUF
	.BJSR	CLRCOD
	MOV	(SP)+,R0
	INC	R0
	BIC	#1,R0
	MOV	R0,STRSIZ
	.BJSR	STOPEN
S1T:
.IF EQ,MASINA-BK0011
	JMP	STARP
.IFF
	JMP	STAR
.ENDC
;
TABMOV::MOV	TABTOP,R0
	ADD	R2,R0
	CMP	R0,TXEND
	BLO	CLRERR
	MOV	FCB,R1
	SUB	TABTOP,R1
	BEQ	4$
	ASR	R1
	TST	R2
	BMI	2$
	MOV	FCB,R3
	MOV	R3,R0
	ADD	R2,R0
3$:	MOV	-(R3),-(R0)
	SOB	R1,3$
	BR	4$
2$:	MOV	TABTOP,R3
5$:	MOV	(R3)+,(R0)+
	SOB	R1,5$
4$:	ADD	R2,TABTOP
	RETURN
;
POKJMP::MOV	(SP)+,@(SP)+
	JMP	@(R4)+
.IF EQ,MASINA-BK0011
POKJM1::MOV	(SP)+,R0
.IF DF,HOOKS
	CALL	HPOKE
.ENDC
	EMT	120
	BCS	IFCERR
	JMP	@(R4)+
.IFTF
;
OUTJMP::TST	(SP)+
	BEQ	1$
	BIS	(SP)+,@(SP)+
	JMP	@(R4)+
1$:	BIC	(SP)+,@(SP)+
	JMP	@(R4)+
.IFT
OUTJM1::MOV	(SP)+,R2	;ПРИЗНАК
	MOV	(SP)+,R1	;МАСКА
	MOV	2(SP),-(SP)
	MOV	2(SP),-(SP)
	EMT	60
	BCS	IFCERR
	TST	R2
	BNE	2$
	BIC	R1,R0
	BR	1$
2$:	BIS	R1,R0
1$:
.IF DF,HOOKS
	CALL	HPOKE
.ENDC
	EMT	120
	JMP	@(R4)+
.IFTF
;
PEEK::	MOV	@(SP),(SP)
	JMP	@(R4)+
.IFT
PEEK1::	EMT	60
	BCS	IFCERR
	MOV	R0,-(SP)
	JMP	@(R4)+
.IFTF
;
INP::	MOV	@2(SP),2(SP)
INPEND:	COM	(SP)
	BIC	(SP)+,(SP)
	JMP	@(R4)+
.IFT
INP1::	MOV	(SP)+,R1
	EMT	60
	BCS	IFCERR
	MOV	R0,-(SP)
	MOV	R1,-(SP)
	BR	INPEND
.IFTF
;
IFCERR:	TRAP	5
;
.IFT
$DEF0::	MOV	@SP,-(SP)
	MOV	#-1,2(SP)
$DEFUS::
.IIF DF,HOOKS	CALL	@#HDFUSR
	MOV	(R4)+,R0
	CMP	(R0)+,(R0)+
	MOV	(SP)+,-(R0)
	MOV	(SP)+,-(R0)
	JMP	@(R4)+
.IFF
$DEFUS::
.IF DF,HOOKS
	CALL	@#HDFUSR
.ENDC
	MOV	(SP)+,@(R4)+
	JMP	@(R4)+
.IFTF
$USR::	MOV	SP,R5
	MOV	(R4)+,R0
	MOV	(R4)+,R3
	MOV	R4,SAVJMP
.IFT
	ASL	R0
	ADD	#USRTAB,R0
	MOV	(R0)+,-(SP)
	BMI	1$
	MOV	(R0)+,-(SP)
	.BJSR0
	BR	2$
1$:	TST	(SP)+
	CALL	@(R0)+
2$:
.IFF
	CALL	@USRTAB(R0)
.IFTF
	MOV	SAVJMP,R4
	CLR	SAVJMP
	JMP	@(R4)+

.IFT
USEROM::MOV	(4)+,R3
	MOV	R4,@#SAVJMP
	MOV	#100000+STMT$,R5	;АДРЕС ВЫЗОВА В СТРАНИЦЕ
		;JEI NERA ROM,ISLEKS I MONITORIU!!!!!!!!!!!!!!!!!
 	MOV	#2,R1	;2 СТРАНИЦЫ ROM
	MOV	#11,R2
	CALL	USRC
	BCC	1$
	MOV	#3,R1	;3 СТРАНИЦЫ RAM
	MOV	#1,R2
	CALL	USRC
	BCS	NOUSR
1$:	MOV	@#SAVJMP,R4
	CLR	@#SAVJMP
	JMP	@(R4)+

USRC::	MOV	R2,-(SP)
3$:
	MOV	R1,-(SP)
	MOV	R1,-(SP)
	ADD	R2,@SP
	MOV	@SP,-(SP)
	MOV	@SP,-(SP)
	MOV	#100000+ID$,-(SP)
	EMT	60
	BCS	1$
	CMP	R0,#ID.		;ЭТО БЕЙСИК-СТРАНИЦА?
	BNE	1$
	MOV	R5,-(SP)
	EMT	60
	MOV	R0,-(SP)	;ЕСТЬ CALL(INIT) ВЫЗОВЫ?
	BEQ	1$
	CLC
	.BJSR0
	BCS	2$
	MOV	(SP)+,R1	;C NEKEICIA
	MOV	(SP)+,R2
	RETURN
1$:	CMP	(SP)+,(SP)+
2$:	MOV	(SP)+,R1
	SOB	R1,3$
	MOV	(SP)+,R2
	SEC
	RETURN
.ENDC

.IF EQ,MASINA-BK0010
USEROM::MOV	(4)+,R3
	MOV	R4,SAVJMP
	MOV	#620,R0
	MOV	@#BREAKW,R1
	MOV	#NOROM,@#BREAKW
NEXROM:	MOV	R0,@#ASPORT
	SWAB	R0
	ASLB	R0
	SWAB	R0
	CALL	@#AUSROM
	BCS	ERROM
	JSR	R0,STATUSQ
	JMP	@(R4)+

STATUSQ:TST	(SP)+
	MOV	R1,@#BREAKW
	MOV	#100220,@#ASPORT
	MOV	#220,@#ASPORT
	MOV	SAVJMP,R4
	CLR	SAVJMP
	MOV	R0,PC

NOROM:	ADD	#6,SP
ERROM:	CMP	R0,#10220
	BNE	NEXROM
	JSR	R0,STATUSQ
.ENDC

NOUSR::	TRAP	5

ONTRG$::MOV	(SP)+,R0
	CMP	(R4)+,(R4)+
	TST	(R4)+
	BNE	1$
	CALL	TSVCT
	.BJSR	NOSTOP
	MOV	@R0,-2(R4)
	MOV	@#VCTNMB,-4(R4)
	MOV	R0,@#VCTNMB
	.BJSR	STOPEN
1$:	MOV	#4537,@R4
	MOV	R4,(R0)+
	MOV	#340,@R0
	MOV	-6(R4),R4
	JMP	@(R4)+

TRON$::	MOV	(SP)+,R0
	CALL	TSVCT
	CALL	TSTVCT
	BCS	NOUSR
	MOV	#4537,@(R0)+
	JMP	@(R4)+

TROFF$::MOV	(SP)+,R0
	CALL	TSVCT
	CALL	TSTVCT
	BCS	NOUSR
	MOV	#2,@(R0)+
	MTPS	#0
	JMP	@(R4)+

TRSTP$::MOV	(SP)+,R0
	CALL	TSVCT
	CALL	RSTVCT
	BCS	NOUSR
	JMP	@(R4)+

RSTVCT::MOV	R1,-(SP)
	MOV	R2,-(SP)

	CALL	TSTVCT
	BCS	3$
	MOV	#VCTNMB,R2

1$:	CMP	@R2,#1		;TST	@R2
	BLO	3$		;BEQ
	CMP	@R2,R0
	BEQ	2$
	MOV	@(R2)+,R2
	CMP	-(R2),-(R2)
	BR	1$

2$:
.IF GE,MASINA-DVK2B
	.BJSR	NOSTOP
.IFTF
	MOV	@R0,R1
	MOV	-(R1),@R0
	CLR	@R1
	MOV	-(R1),@R2
.IFT
	.BJSR	STOPEN
.ENDC
	CLC
3$:	MOV	(SP)+,R2
	MOV	(SP)+,R1
	RETURN

TSVCT:	TST	R0
	BEQ	NOUSR
	CMP	R0,#400
	BHIS	NOUSR
	BIC	#3,R0
	CMP	R0,#30
	BEQ	NOUSR
	CMP	R0,#34
	BEQ	NOUSR
;.IF EQ,MASINA-BK0011
;	CMP	R0,#20
;	BEQ	NOUSR
;.ENDC
	RETURN

TSTVCT::CMP	@R0,@#TXEND
	BLO	1$
	CMP	@#STRREG,@R0
	BHI	1$
	SEC
1$:	RETURN

SREG::	CMP	SP,#420
	BHIS	1$
	TRAP	7
1$:	MOV	R4,-(SP)
	MOV	R3,-(SP)
	MOV	R2,-(SP)
	MOV	R1,-(SP)
	MOV	R0,-(SP)
	MOV	@#SAVJMP,-(SP)
	MOV	R5,PC

RREG::	CMP	SP,#STACK-20
	BHI	1$
	TST	(SP)+
	MOV	(SP)+,@#SAVJMP
	MOV	(SP)+,R0
	MOV	(SP)+,R1
	MOV	(SP)+,R2
	MOV	(SP)+,R3
	MOV	(SP)+,R4
	RTS	R5
1$:	;CLR	ONERFL
	TRAP	51.

;--------------------------------------------
OPINP::
	CLR	R1
	BR	OPFILE
;
OPOUT::
.IF EQ,MASINA-UKM
	MOV	#WR.*400,R1
.IFF
	MOV	#WR.,R1
.IFTF
OPFILE:;MOV	MAXFIL,R3	;JEI NERA AS#
;	MOV	(SP)+,R3	;JEI YRA AS#
;	BLE	BADFN
;	CMP	R3,MAXFIL
;	BHI	BADFN
	CALL	FNDBUF
	TST	(0)+
	BNE	2$
.IIF EQ,MASINA-DISK	CLR	@#ASCFLG
	JSR	R1,FILNAM
	MOV	#".D,(0)+
	MOV	#"AT,(0)+
	CALL	BLOCK0
	SUB	#22,R0
	MOV	(SP)+,@R0
	BNE	10$
	MOV	@#WRITE,-(SP)
	CALL	CASCII
	MOV	(SP)+,@#WRITE
.IFT
	MOV	#RD.*400,@#MAG
.IFF
	MOV	#RD.,@#MAG
.IFTF
	CALL	CAS
	MOV	@#MAG,@R0
	CALL	ISVARD
10$:
	CMP	@#WRITE,DRIVER
	BEQ	1$
.IFT
	BIS	#100000,@R0
.IFF
	BIS	#200,@R0
.IFTF
1$:	JMP	@(R4)+
2$:	TRAP	54.
BADFN:	TRAP	52.
;
FNDBUF::MOV	FCB,R0
;	SUB	#22,R0
1$:	ADD	#400,R0		;VELIAU #422
;	SOB	R3,1$
	RETURN
;
FILNAM::MOV	4(SP),R3
	MOV	(SP)+,2(SP)
	MOV	(SP)+,R2
	BEQ	5$
	MOV	R5,-(SP)
	MOV	R4,SAVJMP
	CALL	TTANAL
	MOV	SAVJMP,R4
	CLR	SAVJMP
	MOV	#6,R5
1$:	CMPB	(R3),#'.
	BEQ	6$
	MOVB	(R3)+,(R0)+
	DEC	R5
	BEQ	3$
	SOB	R2,1$
2$:	MOVB	#40,(R0)+
	SOB	R5,2$
3$:	DEC	R2
	BLE	4$
	CMPB	@R3,#'.
	BEQ	7$
5$:	TRAP	56.
6$:	MOVB	#40,(0)+
	SOB	R5,6$
7$:	MOV	#4,R5
8$:	MOVB	(3)+,(0)+
	DEC	R5
	BEQ	10$
	SOB	R2,8$
9$:	MOVB	#40,(0)+
	SOB	R5,9$
10$:	DEC	R2
	BGT	5$
	ADD	#10,R1
4$:	MOV	(SP)+,R5
	MOV	R1,PC
;
DEVICE::MOV	#1,IODEV	;MAXFIL,IODEV		;VERSIJA 1
	JMP	@(R4)+
;	MOV	(SP)+,IODEV
;	BLE	NOTOP
;	CMP	IODEV,MAXFIL
;	BGT	INIPRP		;VELIAU - BLOS
NOTOP:	TRAP	59.
;
DEVLP::	MOV	#-1,IODEV
INIPRP::JMP	@(4)+
;
CLRDEV::CLR	IODEV
	JMP	@(R4)+
;
.IIF NDF,SNG	DLPOS::	CMP	(SP)+,(SP)+
;
SLPOS::	TST	(SP)+
ILPOS::	TST	(SP)+
LPOS::
.IF DF,HOOKS
	CALL	HLPOS
.ENDC
	MOV	LPTPOS,-(SP)
	JMP	@(4)+
;
CLOFIL::
;	MOV	(R4)+,R2
;	BEQ	3$
;1$:	MOV	(SP)+,R3
;	BEQ	2$
;	CMP	R3,MAXFIL
;	BHI	BADFN
;	CALL	FNDBUF
;	CALL	CLOF
;2$:	SOB	R2,1$
;	JMP	@(R4)+
3$:	CALL	CLOALL
	JMP	@(R4)+
;
CLOALL::;MOV	MAXFIL,R2
;	BEQ	5$
	MOV	HIMEM,R0
4$:	SUB	#422,R0
;	CALL	CLOF
;	SOB	R2,4$
;5$:	RETURN
;
CLOF:	MOV	(R0),R3
	BEQ	6$
.IFT
	BIT	#WR.*400,R3
	BEQ	3$
.IFF
	BIT	#1,R3
	BNE	3$
.IFT
	BIC	#177400,R3
.IFF
	CLRB	R3
	SWAB	R3
.IFTF
	ADD	R0,R3
	ADD	#22,R3
	CALL	CLOUTF
	BR	6$

3$:	CALL	CASCII		;CLINF
	CALL	LASTBL
	CLR	@R0
	CALL	CAS
6$:
.IIF EQ,MASINA-DISK	CLR	@#ASCFLG
	RETURN
;
CLOUTF::MOVB	#32,(R3)+	;^Z
.IF EQ,MASINA-MSX
	INCB	@R0
	BNE	CLOUTF
.ENDC
	CALL	CASCII
	CLR	@R0
	CALL	CAS
	CALL	LASTBL
	CLR	@MAG+ADRW
	CALL	CAS
	RETURN
;
FNDCHR:	MOV	IODEV,R3
	BLE	NOTOP
FNDCR:	CALL	FNDBUF
	TST	@R0
	BEQ	NOTOP
.IFT
	BIT	#RD.*400,@R0
	BEQ	BADFN
.IFF
	BITB	#1,(0)+
	BEQ	BADFN
.IFTF
	CLR	R3
	BISB	@R0,R3
	ADD	R0,R3
.IFT
	ADD	#22,R3
.IFF
	ADD	#21,R3
.IFTF
	RETURN
;
EOF::	CLR	-(SP)
	CALL	FNDCR
	CMPB	@R3,#32	;^Z
	BNE	1$
	COM	@SP
1$:	JMP	@(4)+
;
FILINP::CALL	FNDCHR
	MOV	R0,R5
.IFT
	ADD	#22,R5
.IFF
	ADD	#21,R5
.IFTF
COPYIN::MOV	#BUF,R1
1$:	CMPB	@R3,#32
	BEQ	3$
	CMP	R1,#BUF+417
	BHIS	4$
	MOVB	(3)+,(1)
	INCB	(0)
	BNE	2$
	MOV	R1,-(SP)
	MOV	R0,-(SP)
.IFF
	DEC	R0
.ENDC
	SUB	#400,R3
	CALL	BLOCKN
	CALL	CASCII
.IIF EQ,MASINA-MSX	MOV	#16*400,@#MAG
	CALL	CAS
	CALL	ISVARD
	MOV	(SP)+,R0
	MOV	(SP)+,R1
2$:	CMPB	@R1,#15
	BEQ	1$
	CMPB	(1)+,#12
	BNE	1$
3$:	MOVB	#12,@R1
5$:	RETURN
4$:	TRAP	25.
;
;..........................................
$FIND::
.IIF EQ,MASINA-DISK CALL CLOALL

	MOV	DRIVER,@#WRITE
	MOV	#MAG,R0
.IF EQ,MASINA-UKM
	MOV	#FR.*400,(0)+
	MOV	#11,(0)+
.IFF
	MOV	#FR.,(0)+
	CMP	(0)+,(0)+
.IIF EQ,MASINA-BK0011	CLR	(0)+
.IFTF
	CLR	@R0
	MOV	R4,SAVJMP
	TST	@SP
	BEQ	FL6
	CALL	$ASC
	MOV	R4,SAVJMP
	MOV	-(0),-(SP)	;TURI BUTI >0
	MOV	#MAG+NAMEW+7,R3
.IF EQ,MASINA-BK0011
	JSR	R0,T113
.IFF
	TRAP	113
.ENDC
	.WORD	FILTYP
	.BYTE	3,2
	BR	L1
	DEC	R3
	MOV	R3,R0
	CALL	@BINCOD(R5)
	BR	L2
;
FL6:	MOV	SP,2(SP)
	BR	L6
;
$MERGE::MOV	R4,-(SP)
	BR	L0
;
$LOAD::	CLR	-(SP)
L0:
.IIF EQ,MASINA-DISK	CALL	CLOALL
	MOV	(SP)+,R5

	MOV	#MAG,R0
.IFT
	MOV	#RD.*400,(0)+
.IF EQ,MASINA-MSX
	MOV	#12,(0)+
.IFF
	MOV	#11,(0)+
.ENDC
	CALL	$ASC
	MOV	FCB,(R0)+
	MOV	#200,(0)+
.IFF
	MOV	#RD.,(R0)+
	MOV	FCB,(0)+
	MOV	#400,(0)+
	CALL	$ASC
.IIF EQ,MASINA-BK0011	MOV	#-1,(0)+
.ENDC
	MOV	R4,SAVJMP
.IFF
	MOV	#AVPK,R0
2$:	CALL	RSTVCT
	BCC	2$
	MOV	#TSTBRK,@R0

	MOV	LAST,FIRST
.IFTF
	CALL	CAS
	CALL	ISVARD
	TST	R5
	BNE	1$
	.BJSR	CLRTX1
1$:	MOV	#400,-(SP)
	MOV	FCB,-(SP)
4$:	MOV	(SP)+,R3
	MOV	SP,R0
	MOV	#MAG+NAMEW+20,R5
	CALL	COPYIN
	CMP	R1,#BUF
	BEQ	L6
	MOV	R3,-(SP)
.IF EQ,MASINA-BK0011
	.BJSR	ASCCD0
	BCS	7$
.IFF
	JSR	R1,ASCCOD
	BR	7$
.IFTF
	BR	4$
7$:	MOV	#12,R0
	MOVB	R0,@#BUF
.IFT
	CALL	ISVP
.IFF
	CALL	ISV
.ENDC
.IFF
	MOV	#AVPK,R0
3$:
	CALL	RSTVCT
	BCC	3$
	MOV	#IVS,@R0
.IFTF
	TRAP	57.
;
L6:	TST	(SP)+
L1:
.IIF EQ,MASINA-MSX	MOV	#12,@#MAG+2
	CALL	LASTBL
L2:	CALL	CAS
.IFF
	MOV	#AVPK,R0
1$:
	CALL	RSTVCT
	BCC	1$
	MOV	#IVS,@R0
.IFTF
	CALL	TESTR
ST0:	MOV	#5007,@#BUF
	BR	ST
;
$CLOAD::
.IIF EQ,MASINA-DISK	CALL	CLOALL

	MOV	#MAG,R0
.IF EQ,MASINA-UKM
	MOV	#RD.*400,(0)+
	MOV	#11,(0)+
	CALL	$COD
.IFF
	MOV	#3,(0)+
.IFTF
	MOV	#TEXT,(0)+
	MOV	FCB,@R0
.IFF
	SUB	#TEXT,(0)+
	CALL	$COD
.IIF EQ,MASINA-BK0011	MOV	#-1,(0)+
.IFT
	SUB	#TEXT,@R0
	CLC
	ROR	(0)+
.IFTF
	MOV	R4,@#SAVJMP
	.BJSR	CLRTX1
	CALL	CAS
	MOV	#MAG+ADRR,R3
	MOV	(R3)+,R2
	MOV	@R3,R0
.IFT
	ASL	R0
.IFTF
	MOV	#TEXT,R3
	ADD	R3,R0
	SUB	R2,R3
	MOV	FCB,R2
	MOV	R2,R4
	SUB	-(R0),R4
5$:	MOV	-(R0),-(R2)
	MOV	-(R0),-(R2)
	ADD	R3,@R2
	MOV	-(R0),-(R2)
	CMP	R2,R4
	BHI	5$
	MOV	R0,ENDCOD
	MOV	R0,TXEND
	MOV	R2,LENT
	MOV	R2,LIMIT
	MOV	R2,CIKL
	MOV	R4,TABTOP
	CALL	TESTR
ST:	MOV	#STACK,SP
.IF EQ,MASINA-BK0011
	JMP	STARP
.IFF
	JMP	STAR
.ENDC
;
$CSAVE::CMP	TABTOP,FCB
	BEQ	ST
.IIF EQ,MASINA-DISK	CALL	CLOALL

	MOV	#MAG,R0
.IFT
	MOV	#WR.*400,(0)+
	MOV	#11,(0)+
	CALL	$COD
.IFF
	MOV	#WR.,(0)+
.IFTF
	MOV	#TEXT,(0)+
	CLR	(0)+
.IFF
	CALL	$COD
.IIF EQ,MASINA-BK0011	MOV	#-1,(0)+
.IFTF
	MOV	R4,SAVJMP
	.BJSR	CLRCOD
	.BJSR	GRBCOL
	MOV	R1,R5
	MOV	TABTOP,R4
	MOV	R5,@#MAG+LENW
	SUB	#TEXT-2,@#MAG+LENW
7$:	MOV	(4)+,(5)+
	CMP	R4,FCB
	BLO	7$
	MOV	R4,@R5
	SUB	TABTOP,(R5)
	MOV	#MAG,R1
	ADD	(5)+,LENW(R1)
.IFT
	CLC
	ROR	LENW(R1)
.IFTF
.IF EQ,MASINA-BK0011
	MOV	#PAG1,-(SP)
	MOV	@#WRITE,-(SP)
	.BJSR0
.IFF
	CALL	@WRITE
.ENDC
	CALL	ATSTAB
	CALL	CASERR
	BR	ST
;
$SAVE::	CMP	TABTOP,FCB
	BEQ	ST
.IIF EQ,MASINA-DISK	CALL	CLOALL

	MOV	#MAG,R0
.IFT
	MOV	#WR.*400,(0)+
.IF EQ,MASINA-MSX
	MOV	#12,(0)+
.IFF
	MOV	#11,(0)+
.ENDC
	CALL	$ASC
	MOV	FCB,(R0)+
	MOV	#200,(0)+
.IFF
	MOV	#WR.,(0)+
	MOV	FCB,(R0)+
	MOV	#400,(R0)+
	CALL	$ASC
.IIF EQ,MASINA-BK0011	MOV	#-1,(0)+
.ENDC
.IF EQ,MASINA-MSX
	MOV	#WR.*400,-(SP)
.IFF
	CLR	-(SP)
.ENDC
	MOV	R4,@#SAVJMP
	MOV	@#FCB,R4
.IFF
	MOV	#AVPK,R0
	CALL	TSTVCT
	BCC	2$
	MOV	#TSTBRK,@R0
2$:
	MOV	LAST,FIRST
.IFTF
	MOV	R4,-(SP)
	TST	-(R4)
1$:	.BJSR	EILUTE
.IF NE,KLAS-BK0010
	MOVB	#15,-1(R3)
	MOVB	R0,(3)+
	INC	R2
.ENDC
	CMP	-(R4),-(R4)
	MOV	(SP)+,R3
	MOV	SP,R0
	MOV	#MAG+NAMEW+20,R5
	.BJSR	COPOUT
.IIF EQ,MASINA-MSX	MOV	@SP,@#MAG
	MOV	R3,-(SP)
	CMP	R4,TABTOP
	BHI	1$
	CALL	CLOUTF
.IFF
	MOV	#AVPK,R0
	CALL	TSTVCT
	BCC	3$
	MOV	#IVS,@R0
3$:
.IFTF
	MOV	#5007,@#BUF
	BR	BS4

.IF EQ,MASINA-BK0011
$BP0::	MOV	#-1,-(SP)
	JMP	@(R4)+
.ENDC

$BL0::
.IF EQ,MASINA-UKM
	MOV	#1,-(SP)
.IFF
	CLR	-(SP)
.IFTF
	JMP	@(R4)+

$BLOAD::
.IIF EQ,MASINA-DISK	CALL	CLOALL

	MOV	#MAG,R0
.IF EQ,MASINA-BK0011
	MOVB	(SP)+,PAGW+1(R0)
	MOVB	(SP)+,PAGW(R0)
.ENDC
.IFT
	MOV	#RD.*400,(0)+
	MOV	#11,(0)+
	MOV	(SP)+,20(0)
	MOV	#-2,22(0)
.IFF
	MOV	#RD.,(0)+
	MOV	(SP)+,(0)+
	MOV	#-2,(0)+
.IFTF
	CALL	$BIN
.IIF EQ,MASINA-BK0011	TST	(0)+
.IF EQ,MASINA-BK0010
	MOV	#100000,R1
.IFF
.IF EQ,MASINA-BK0011
	MOV	#140000,R1
.IFF
	MOV	#MEMTYP,R1
.ENDC
.ENDC
;	CMP	@R0,#TEXT
;	BLO	BS3
.IFF
	MOV	#MAG+ADRW,R0
.IFTF
	SUB	(0)+,R1
	BLOS	BS3
.IFT
	CLC
	ROR	R1
.ENDC
	MOV	R1,(0)+
	MOV	R4,@#SAVJMP
	MOV	(SP)+,R3
.IIF NE,STACK-1000	MOV	#1000,SP
	CALL	CAS
	CALL	ISVARD
	TST	R3
	BEQ	BL1
	CLR	LYGIS
.IF EQ,MASINA-BK0010
	CALL	@MSTART
.IFF
.IF EQ,MASINA-UKM
	BIT	#1,@#MAG+ADRW
	BNE	10$
.IFF
	TST	@#MAG+ADRW
	BEQ	10$
.ENDC
	CALL	@MAG+ADRW
	BR	BL1
10$:	CALL	@MAG+ADRR
.ENDC
BL1:
.IIF NE,STACK-1000	MOV	#STACK,SP
	BR	BS4
;
$BSAVE::
.IIF EQ,MASINA-DISK	CALL	CLOALL

	MOV	#MAG,R0
.IF EQ,MASINA-BK0011
	MOVB	(SP)+,PAGW+1(R0)
	MOVB	(SP)+,PAGW(R0)
.ENDC
.IF EQ,MASINA-UKM
	MOV	#WR.*400,(0)+
	MOV	#11,(0)+
	MOV	(SP)+,22(0)
	MOV	(SP)+,20(0)
	CALL	$BIN
	BIC	#1,@R0
	SUB	(0)+,@R0
	BLOS	BS3
	CLC
	ROR	@R0
	ADC	(0)+
.IFF
	MOV	#WR.,(0)+
	MOV	(SP)+,R1
	MOV	(SP)+,@R0
	SUB	(0)+,R1
	BLOS	BS3
	MOV	R1,(0)+
	CALL	$BIN
.ENDC
.IIF EQ,MASINA-BK0011	TST	(0)+
	MOV	R4,@#SAVJMP
	CALL	CAS
BS4:
	MOV	@#SAVJMP,R4
	CLR	@#SAVJMP
BS2::	JMP	@(4)+
BS3::	TRAP	5
;
TESTR:
	CALL	ISVARD
	MOV	(SP)+,R5
	MOV	@#SAVJMP,R4
	CLR	@#SAVJMP
	TST	(SP)+
	BGT	BS2
	BEQ	1$
	MOV	FCB,R4
	MOV	-(4),R5
	CLR	LYGIS
.IF EQ,MASINA-BK0011
	JMP	LRUNP
.IFF
	JMP	LRUN
.ENDC
1$:	MOV	R5,PC

DEVTAB:
.IIF EQ,MASINA-DISK	.BYTE	0,40
.IF GE,MASINA-DVK2B
.IIF EQ,MASINA-NET	.ASCII	/NET /
.IFTF
.IIF EQ,MASINA-CASET	.ASCII	/CAS /
.IFF
.IIF EQ,MASINA-NET	.ASCII	/NET /
.IFTF
.EVEN

DRIVER:
.IIF EQ,MASINA-DISK	.WORD	RT11
.IFT
.IIF EQ,MASINA-NET	.WORD	IRPS
.IFTF
.IIF EQ,MASINA-CASET	.WORD	MAGNIT
.IFF
.IIF EQ,MASINA-NET	.WORD	IRPS
.ENDC
DEND:

FILTYP::.ASCII	/COD BIN /
BINCOD:	.WORD	CODEND,BINEND
;
ISVARD::TSTB	LYGIS
	BNE	1$
	MOV	R0,-(SP)
	MOV	R1,-(SP)
	MOV	#MAG+NAMER,R1
	MOV	#20,R2
.IF EQ,MASINA-MSX
	CMP	6(R1),#401*352
	BNE	3$
	CMPB	#16,-NAMER+1(R1)
	BEQ	6$
	ADD	#12,R1
	SUB	#12,R2
3$:
.ENDC
.IF EQ,MASINA-BK0011
2$:	MOVB	(R1)+,R0
	CALL	ISVP
	SOB	R2,2$
.IFF
	.BJSR	ISVEIL
.IFTF
	MOV	#12,R0
.IFT
	CALL	ISVP
.IFF
	CALL	ISV
.ENDC
6$:	MOV	(SP)+,R1
	MOV	(SP)+,R0
1$:	RETURN
;
TTANAL::
	MOV	R4,-(SP)
	MOV	R2,-(SP)
	MOV	R3,-(SP)
	TRAP	112
	MOV	R3,-(SP)
	CMP	-(SP),-(SP)
	MOV	SP,R5
	MOVB	(3)+,(5)+
	MOVB	(3)+,(5)+
	MOVB	(3)+,(5)+
	MOVB	(3)+,(5)+
	MOV	SP,R3
.IF EQ,MASINA-BK0011
	JSR	R0,T113
.IFF
	TRAP	113
.ENDC
	.WORD	DEVTAB
	.BYTE	3,DEND-DRIVER-2
	BR	12$
	CMPB	@R3,#':
	BNE	12$
	INC	R3
	SUB	SP,R3
	CMP	(SP)+,(SP)+
	ADD	(SP)+,R3
	ADD	(SP),R2
	SUB	R3,R2
	BEQ	2$
	BMI	11$
	CMP	(SP)+,(SP)+
	BR	13$
12$:	ADD	#6,SP
11$:	MOV	(SP)+,R3
	MOV	(SP)+,R2
	CLR	R5
13$:	MOV	(SP)+,R4
	MOV	DRIVER(5),@#WRITE
	RETURN
2$:	TRAP	56.
;
$BIN:	MOV	(SP)+,R1
	JSR	R1,FILNAM
	MOV	#".B,(R0)+
	MOV	#"IN,(R0)+
BINEND:	MOV	#20040,(R0)+
	MOV	#20040,(R0)+
	MOV	#20040,(R0)+
	RTS	PC
;
$COD:	MOV	(SP)+,R1
	JSR	R1,FILNAM
	MOV	#".C,(R0)+
	MOV	#"OD,(R0)+
CODEND:	CLR	(R0)+
	CLR	(R0)+
	CLR	(R0)+
	RTS	PC
;
$ASC:	MOV	(SP)+,R1
	JSR	R1,FILNAM
	MOV	#".A,(R0)+
	MOV	#"SC,(R0)+
.IF GE,MASINA-DVK2B
BLOCK0::MOV	#" 0,(R0)+
	MOV	#"00,(R0)+
	MOV	#40433,(R0)+
	RETURN
.IFF
BLOCK0::MOV	#" #,(R0)+
	MOV	#"00,(R0)+
	MOV	#15060,(R0)+
	RETURN
.ENDC
;
LASTBL::
	MOV	#MAG+NAMEW+13,R1
.IF GE,MASINA-DVK2B
	CLRB	(R1)+
.IFF
	MOVB	#40,(R1)+
.ENDC
	CLR	(R1)+
	CLR	(R1)+
.IF EQ,MASINA-UKM
	MOV	#1,@#MAG+LENW
.IFF
	MOV	#2,@#MAG+LENW
.ENDC
.IF EQ,MASINA-MSX
	CMPB	@#MAG+1,#RD.
	BNE	1$
	MOV	#16*400,@#MAG
1$:
.ENDC
	RETURN
;
ATSTAB:	CMP	R5,TABTOP
	BLO	2$
	MOV	R5,R4
	SUB	FCB,R4
	ADD	R5,R4
1$:	MOV	-(R4),-(R5)
	CMP	R5,TABTOP
	BHI	1$	
2$:	RETURN
;
CASCII::CMP	R0,FCB
	BLO	2$
	MOV	DRIVER,@#WRITE
	MOV	#MAG,R1
.IF EQ,MASINA-UKM
	MOV	(0)+,(1)+
	BPL	10$
	BIC	#100000,-2(1)
	MOV	DRIVER+2,@#WRITE
10$:
.IF EQ,MASINA-MSX
	MOV	#12,(1)+
.IFF
	MOV	#11,(1)+
.ENDC
.IFF
	MOV	(0)+,(1)+
	TSTB	-2(R1)
	BPL	10$
	BIC	#200,-2(1)
	MOV	DRIVER+2,@#WRITE
10$:	MOV	R0,@R1
	ADD	#20,(1)+
	MOV	#400,(1)+
.ENDC
.ENDC
	MOV	#10,R5
1$:	MOV	(0)+,(1)+
	SOB	R5,1$
.IIF EQ,MASINA-BK0011	MOV	#-1,(1)+
	MOV	R0,R5
.IF EQ,MASINA-UKM
	MOV	R0,(1)+
	MOV	#200,(R1)+
.ENDC
	SUB	#22,R0
2$:	RETURN

CAS1:	CALL	ISVARD
CAS::
	MOV	#MAG,R1
.IF EQ,MASINA-BK0011
	MOV	#PAG1,-(SP)
	MOV	@#WRITE,-(SP)
	.BJSR0
.IFF
	CALL	@WRITE
.IFTF
CASERR:
.IFT
	MOVB	@#ERRBYT,R1
.IFF
.IF EQ,MASINA-UKM
	MOVB	@R1,R1
.IFF
	MOVB	1(R1),R1
.ENDC
.ENDC
	BEQ	2$
	CMPB	R1,#NER.
	BEQ	CAS1

.IF EQ,MASINA-UKM
	CMPB	R1,#RER.
	BGE	1$
.IFTF
	MOV	#5$,R0
3$:	TSTB	@R0
	BEQ	1$
	CMPB	R1,(R0)+
	BNE	3$

	SUB	#5$+1,R0
	ASL	R0
	JMP	@6$(R0)
5$:
.IFT
	.BYTE	RER.
.IFTF
	.BYTE	FER.,MER.,SER.
.EVEN
6$:
.IFT
	.WORD	1$
.ENDC
	.WORD	7$,8$,BRKO

2$:	RETURN
1$:	TRAP	19.
7$:	TRAP	53.
8$:	TRAP	7

BLOCKN::MOV	#3,R1
.IF GE,MASINA-DVK2B
	TST	-(R5)
.IFF
	DEC	R5
.IFTF
6$:	CMPB	-(R5),#'9
	BNE	7$
	MOVB	#'0,@R5
	SOB	R1,6$
.IFT
	CMPB	(R5)+,(R5)+
.IFF
	TST	(R5)+
.ENDC
7$:	INCB	@R5
	RETURN
;
;---------------------------------------------
.IF LE,MASINA-BK0010
.IF NE,MASINA-BK0011
$KEY::	MOV	(SP)+,R0
	CMP	R0,#16.
	BLOS	1$
	MOV	#16.,R0
1$:	MOV	(SP)+,R2
	MOV	(SP)+,R3
	BLE	5$
	CMP	R3,#10.
	BGT	5$
	MOV	#FNKSTR-17.,R1
2$:	ADD	#17.,R1
	SOB	R3,2$
.IF DF,HOOKS
	CALL	HKEY
.ENDC
	MOVB	R0,(R1)+
	BLE	4$
3$:	MOVB	(R2)+,(R1)+
	SOB	R0,3$
4$:	CALL	ISVSS
	JMP	@(R4)+
5$:	TRAP	5
;
.IF EQ,1
KEYISV::MOV	#FNKSTR,R3
4$:	MOV	R3,R1
	MOVB	(R1)+,R2
	BEQ	1$
3$:	MOVB	(R1)+,R0
	CMPB	#' ,R0
	BLE	2$
	MOVB	#' ,R0
2$:
.IF EQ,MASINA-BK0011
	CALL	ISVP
.IFF
	CALL	ISV
.IFTF
	SOB	R2,3$
1$:	MOV	#12,R0
.IFT
	CALL	ISVP
.IFF
	CALL	ISV
.ENDC
	ADD	#17.,R3
	CMP	R3,#FNKSTR+170.
	BLO	4$
	JMP	@(R4)+
;
KEYON::
	CALL	ISVSS
	JMP	@(R4)+
KEYOFF::MOV	@#214,R2
	MOV	@#212,@#214
	MOV	#236,R0
	CALL	ISV
	MOV	R2,@#214
	JMP	@(R4)+
;
.ENDC
.IFF
$KEY::	MOV	(SP)+,R1
	MOV	@SP,R2
	ADD	R1,R2
	MOVB	@R2,R1
	CLRB	@R2
	EMT	12
	MOVB	R1,@R2
	JMP	@(R4)+
.ENDC
;
.IFF
$MIDLE::MOV	(SP)+,R3
	MOV	LL,-(SP)
	BR	$M1
$WDMD::	MOV	(SP)+,R3
$M1:	MOV	#80.,R0
	SUB	LL,R0
	CMP	R3,R0
	BLOS	$W1
WERR:	TRAP	5
;
$WIDTH::
	MOV	#-1,R3
$W1:	TST	CRTGRP
	BNE	WERR
	MOV	(SP)+,R2
	BLE	WERR
	CMP	R2,#80.
	BGT	WERR
.IF NE,MASINA-UK
	MOV	#14,R0
	CALL	ISV
.ENDC
	.BJSR	NOSTOP
	CALL	SETWDT
	TST	R3
	BMI	1$
	MOV	R3,MIDLE
1$:	.BJSR	STOPEN
	MOV	#14,R0
	CALL	ISV
	JMP	@(4)+
.ENDC
;
TRUN::	TST	TRFLAG
	BNE	TEND
	INC	TRFLAG
	TSTB	LYGIS
	BEQ	TEND
	TST	NUMBER
	BEQ	1$
	NEG	TRFLAG
	BR	TEND
1$:	CMP	@R4,#ENDPRG
	BEQ	TEND
.IF EQ,MASINA-BK0011
	JMP	TRINP
.IFF
	JMP	TRIN
.ENDC
;
TREND::	TST	TRFLAG
	BLE	1$
	TSTB	LYGIS
	BEQ	1$
.IF EQ,MASINA-BK0011
	JSR	R2,SEPAPP
.IFF
	JSR	R2,SEPAP
.ENDC
1$:	CLR	TRFLAG
TEND:	JMP	@(4)+

;-----------------------------------------------------
.IF NE,MASINA-UK
.IF NE,MASINA-BK0011
BEEP0::	MOV	#7,R0	;TIK PYPT
	CALL	ISV
	JMP	@(R4)+
.IFF
BEEP0::	MOV	#400,-(SP)
BEEP1::	MOV	#144,-(SP)
BEEP2::
	EMT	124

;	MOV	(SP)+,R2
;	MOV	(SP)+,R1
;	CALL	SIGNAL
	JMP	@(R4)+
.ENDC
.ENDC

.IF EQ,1
$ERASE::TST	@R4
	BEQ	$ER0
3$:	MOV	(R4)+,R2
	BEQ	$EREND
	TST	@R2
	BEQ	1$
	CALL	MASREG
	MOV	@R2,R3
	CLR	(R2)+
	CLR	R0
	BISB	@R2,R0
	CLRB	@R2
	ASL	R0
	ADD	R0,R1
	MOV	R3,R2
	ADD	R1,R2
	MOV	R3,R0
6$:	MOV	(R2)+,(R3)+
	CMP	R2,ENDCOD
	BLO	6$
	MOV	R3,ENDCOD
	MOV	LENT,R2
2$:	TSTB	@R2
	BPL	7$
	TST	(R2)+
	CMP	@R2,R0
	BLO	8$
	SUB	R1,@R2
8$:	CMP	(R2)+,(R2)+
	BR	9$
7$:	CALL	PRAL
9$:	CMP	R2,TABTOP
	BLO	2$
	BR	3$
1$:	TRAP	5
;
.IF NDF,SNG
PRAL:	TSTB	@R2
	BMI	1$
	MOV	(R2)+,R1
	BMI	1$
	ASL	R1
	BMI	8$
	ASL	R1
	BMI	1$
	CMP	(R2)+,(R2)+
1$:	TST	(R2)+
8$:	TST	(R2)+
	RETURN
.ENDC
;
$ER0:	MOV	LENT,R2
1$:	TSTB	@R2
	BMI	2$
	CALL	PRAL
	BR	4$
2$:	TST	(R2)+
	CLR	(R2)+
	CLRB	(R2)+
	INC	R2
4$:	CMP	R2,TABTOP
	BLO	1$
	MOV	STRREG,R0
	ADD	STRSIZ,R0
	MOV	R0,ENDCOD
	TST	(R4)+
$EREND:	JMP	@(R4)+
;
.IF NDF,SNG
SWPD::	MOV	#4,R2
	BR	EXSWAP
.ENDC
SWPR::	MOV	#2,R2
	BR	EXSWAP
SWPI::	MOV	#1,R2
EXSWAP:	MOV	(SP)+,R0
	MOV	(SP)+,R1
1$:	MOV	@R0,-(SP)
	MOV	@R1,(0)+
	MOV	(SP)+,(1)+
	SOB	R2,1$
	JMP	@(4)+
;
SWSSI::	MOV	(SP)+,R1
	MOV	(SP)+,R0
	BR	SWAP1
SWSIS::	MOV	(SP)+,R0
	MOV	(SP)+,R1
SWAP1:	MOV	@R0,-(SP)
	MOVB	@R1,@R0
	MOVB	(SP)+,(1)+
	TST	(0)+
	MOV	#2,R2
	BR	EXSSW
SWSII::	MOV	(SP)+,R0
	MOV	(SP)+,R1
	MOV	#3,R2
;
EXSSW:	MOVB	@R0,-(SP)
	MOVB	@R1,(0)+
	MOVB	(SP)+,(1)+
	SOB	R2,EXSSW
	JMP	@(4)+
;
MTONV::	MOV	#1,MAG
MONV1:	CALL	CAS
	JMP	@(4)+
MTOFFV::CLR	MAG
	BR	MONV1
.ENDC
;
;--------------------------------------------
.IF EQ,MASINA-BK0011
;------------------------------
; ДОПОЛНЕНИЯ К ВТОРОЙ СТРАНИЦЕ
;------------------------------
ISVP::
.IIF DF,HOOKS  CALL	HPUTCH
	CMPB	R0,#12
	BNE	1$
	EMT	16
	MOV	#15,R0
1$:
	EMT	16
	RETURN
;
SEPAPP::
	TST	(SP)+
	INC	ERRS
	TST	PNTSAV
	BEQ	5$
	MOV	PNTSAV,SP
2$:	DEC	NUMBER
	BLT	5$
	MOV	(SP)+,@(SP)+
	BR	2$
5$:	CLR	PNTSAV
	MOV	#STACK,SP
	CLR	ERRS
	CLR	NUMBER
	MOV	R2,PC
;
SET1:	CMPB	@-(4),#50	;REM
	BEQ	1$
	CMPB	@0(4),#2	;'
	BEQ	1$
	CMPB	@0(4),#100	;DATA
	BNE	SET2
1$:	CMP	-(4),-(4)
SETSTP::
	CMP	R4,TABTOP
	BHIS	SET1
	TST	(0)+
SET2:	TST	-(4)
	RTS	R0

STARP::	MOV	#PAG1,-(SP)
	MOV	#STAR,-(SP)
	.BJMP

TRINP:	MOV	#PAG1,-(SP)
	MOV	#TRIN,-(SP)
	.BJMP

LRUNP:	MOV	#PAG1,-(SP)
	MOV	#LRUN,-(SP)
	.BJMP
.ENDC
.END
